;
;--------------------------------------
;*** CHANGE SLOT	
;*** (ENASLT)
;
;IN:	HL=ADDRESS
;	A=SLOT
;USE:	ALL
;--------------------------------------
;	SLTTBL:	equ $fcc5 ; Mirror of the four possible Secondary Slot Registers (4b)
;
CHGSLT:
	CALL	.MKSLTV
	JP	M,.CHGSLT_1
	; BASIC SLOT
	IN	A,(0A8H)
	AND	C
	OR	B
	OUT	(0A8H),A
	RET
.CHGSLT_1:
	; EXTEND SLOT
	PUSH	HL
	CALL	.CHGEXSLT
	LD	C,A
	LD	B,0
	LD	A,L
	AND	H
	OR	D		;SLOT TABLE VALUE
	LD	HL,SLTTBL
	ADD	HL,BC
	LD	(HL),A		;EX SLOT TABLE
	POP	HL
	LD	A,C
	JR	CHGSLT
;
	; MAKE SLOT REGISTER MASK AND VALUE
.MKSLTV:
	DI
	PUSH AF
	LD	A,H
	RLCA
	RLCA
	AND	03H	; HL BIT 15-14 -> A BIT 1-0
	LD	E,A	; E=PAGE
	LD	A,0C0H	; 1100 0000B
.MKSLTV_1:
	RLCA
	RLCA
	DEC	E
	JP	P,.MKSLTV_1
	LD	E,A		;E=PAGE MASK
	CPL
	LD	C,A		;REVERSE PAGE MASK
	POP	AF
	PUSH	AF
	AND	03H		;BASE SLOT
	INC	A
	LD	B,A
	LD	A,0ABH
.MKSLTV_2:
	ADD	A,55H
	DJNZ	.MKSLTV_2
	LD	D,A		; BASE SLOT NO.: 000000/01010101/10101010/11111111
	AND	E		; E=PAGE MASK
	LD	B,A		; B=VALUE / C=REVERSE PAGE MASK
	POP	AF
	AND	A
	RET
;
;	CHANGE EXTEND SLOT
.CHGEXSLT:
	PUSH	AF
	LD	A,D
	AND	0C0H
	LD	C,A
	POP	AF
	PUSH	AF
	LD	D,A
	IN	A,(0A8H)	;SLOT REG
	LD	B,A
	AND	3FH
	OR	C
	OUT	(0A8H),A	;CHANGE PAGE3 BASE SLOT (FOR MEMORY MAPPED I/O)
	LD	A,D
	RRCA
	RRCA
	AND	03H
	LD	D,A		;D=EX SLOT NO.
	LD	A,0ABH
.CHGEXSLT_1:
	ADD	A,55H
	DEC	D
	JP	P,.CHGEXSLT_1
	AND	E		; E=PAGE MASK
	LD	D,A		; EX SLOT NO.: 000000/010101/101010/111111
	LD	A,E
	CPL
	LD	H,A		; H=REVERSE PAGE MASK
	LD	A,(0FFFFH)
	CPL
	LD	L,A
	AND	H
	OR	D
	LD	(0FFFFH),A	;CHANGE EXTEND SLOT
	LD	A,B
	OUT	(0A8H),A	;CHANGE BASE SLOT
	POP	AF
	AND	03H
	RET
;
