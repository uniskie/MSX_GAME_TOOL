10 '==========================
20 '= EXTENSION FOR PARODIUS =
30 '==========================
40 CLEAR2000,&HBFFF:DEFINTA-Z
50 VDP(9)=VDP(9)AND&HDF'COLOR0=TRANSPARENT
60 '
70 BLOAD"PARODIUS.BIN"
80 A$=",run-":'CLS+"RUN"+RETURN
90 A=&HF3F8:POKE A,&HF0+LEN(A$):POKE A+1,&HFB:POKE A+2,&HF0:POKE A+3,&HFB:FORI=1TOLEN(A$):POKE&HFBEF+I,ASC(MID$(A$,I,1))-32:NEXT
100 DELETE 70-100
110 '
120 COLOR 9,0,0:KEYOFF:SCREEN0:WIDTH40:GOSUB 990'PAL
130 T$="WWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWW"
140 PRINT"X"+T$+"YV               PARODIUS               VV        RC-759 (C) KONAMI 1988        VV  QUICK SAVER/LOADER (PATCH PROGRAM)  VZ";T$;"[";
150 '
160 DEFUSR9=&H156
170 DEFUSR0=&HC000:DEFUSR1=&HC003:CA=&HC006
180 '
190 PRINT CHR$(10);"      - INSERT CARTRIDGE in SLOT -"
200 PRINT CHR$(10);"PAUSE ¼Ã ROM ¦ »¼Ã¶× PAUSE ¶²¼Þ® ¼Ã¸ÀÞ»²"
210 '
220 A=USR1(&H5907):IF A=0 THEN 220
230 '
240 IF A<4 THEN S$=" "+HEX$(A)+" " ELSE S$=HEX$(A AND 3)+"-"+HEX$((A AND 12)\4)
250 PRINT"  - Identified CARTRIDGE in SLOT ";S$;" -";CHR$(10)
260 '
270 SOUND0,110:SOUND1,0:SOUND7,190:SOUND8,0
280 FORJ=0TO7:SOUND8,10-J:FORI=0TO4:SOUND0,25+I*24-J:TIME=0:FORK=0TO1:K=TIME:NEXT:NEXT:NEXT:SOUND8,0
290 '
300 PRINT"      SAVE DATA:VRAM 10000H-17FFFH";CHR$(10)
310 PRINT SPC(14);"NEW GAME:(G)"
320 IF PEEK(CA) THEN PRINTSPC(12);"CONT' GAME:(C)"
330 PRINT SPC(12);"LOAD STATE:(L)"
340 PRINT SPC(12);"SAVE STATE:(S)"
350 PRINT SPC(12);"PAL CHANGE:(P)"
360 PRINT CHR$(10);SPC(6);"COMMAND? _";
370 A=USR9(0)
380 A$="":I$=INPUT$(1):I=ASC(I$):I$=CHR$(I+((I>&H60)AND(I<&H7B))*&H20)
390 IF I$="L" THEN A$="LOAD"
400 IF I$="S" THEN A$="SAVE"
410 IF I$="G" THEN A$="NEW GAME":POKE CA,0
420 IF PEEK(CA)AND(I$="C") THEN A$="CONTINUE GAME"
430 IF I$="P" THEN PT=-(PT=0):GOSUB 990:GOTO 370
440 IF A$="" THEN BEEP:GOTO 380
450 PRINT A$;
460 SOUND 0,110:SOUND 1,0:SOUND 7,190:SOUND 8,0
470 FORJ=0TO7:SOUND 8,10-J:FORI=0TO4:SOUND 0,230-I*28-J\6:TIME=0:FORK=0TO1:K=TIME:NEXT:NEXT:NEXT:SOUND8,0
480 '
490 IF A$="LOAD" THEN GOSUB 730:IF E1 THEN 120
500 IF A$="SAVE" THEN GOSUB 770:IF E1 THEN 120
510 '
520 SCREEN0:WIDTH40:GOSUB 990'PAL
530 PRINT"X"+T$+"YV";SPC(38);"V";
540 PRINT"V STOP :  EXTRA MENU (BLINK CAPS LED)  VV";SPC(38);"VT- EXTRA MENU ------------------------WSV";SPC(38);"V";
550 PRINT"V     STOP:      QUIT EXTRA-MENU       VV      'S':        SAVE STATE          VV      'L':        LOAD STATE          VV CTRL+STOP:       RESET GAME          VV";SPC(38);"V";
560 PRINT"V CTRL+'@':   TSAVE > RETURN BASIC     VV CTRL+'S': SAVE STATE > TSAVE > BASIC VV CTRL+'L':    RESTORE TSAVE STATE     VV      'C':    CURE & FILL UP ITEM     VV      'P':         JUKE-BOX           V";
570 PRINT"Z"+T$+"["
580 PRINT"           - HIT ANY KEY -";
590 I$=INPUT$(1)
600 '
610 SCREEN2,2:GOSUB 990'PAL
620 KEY 1,"A=USR(0)"+CHR$(13)
630 A=USR(0)
640 '
650 SCREEN0:WIDTH40:GOSUB 990'PAL
660 PRINT" DATA SAVE TO DISK ? (Y/N)_";
670 A=USR9(0):I$=INPUT$(1)
680 IF I$="Y"OR I$="y" THEN A$="SAVE":GOSUB 770:GOTO 710
690 IF I$<>"N"AND I$<>"n" THEN BEEP:GOTO670
700 '
710 GOTO 120'END
720 '
730 'LOAD
740 GOSUB 810:SCREEN5:SETPAGE2,2:E1=0:ONERRORGOTO1080:BLOAD F$+".PAR",S
750 POKE CA,1:GOSUB 970:RETURN
760 '
770 'SAVE
780 GOSUB 810:SCREEN5:SETPAGE2,2:E1=0:ONERRORGOTO1080:BSAVE F$+".PAR",&H2000,&H7FFF,S
790 GOSUB 970:RETURN
800 '
810 SCREEN0:WIDTH40
820 LOCATE0,6:PRINT"-------------- DATA FILES --------------";
830 ONERRORGOTO1050:FILES "*.PAR":PRINT:ONERRORGOTO0
840 PRINT STRING$(40,"-");
850 LOCATE 0,0:I=USR9(0)
860 PRINT A$;:LINEINPUT" FILENAME _";F$
870 '
880 IF F$="" THEN F$="PARODIUS"
890 F$=LEFT$(F$+"       ",8)
900 LOCATE5,2:PRINT "FILE NAME = ";F$;".PAR"
910 '
920 LOCATE7,4:PRINT A$;" OK? (Y/N)_";
930 I$=INPUT$(1)
940 IF I$="Y"OR I$="y" THEN RETURN
950 IF I$="N"OR I$="n" THEN 850 ELSE BEEP:GOTO930
960 '
970 ON ERROR GOTO 0
980 RETURN
990 '-PAL-
1000 IF PT THEN 1020
1010 COLOR=NEW:RETURN'MSX2PAL
1020 A$="000000353464327437532356632643563664241535555777"'MSX1PAL
1030 FOR I=0 TO 15:COLOR=(I,VAL(MID$(A$,I*3+1,1)),VAL(MID$(A$,I*3+2,1)),VAL(MID$(A$,I*3+3,1))):NEXT:RETURN
1040 '
1050 '-FilesError
1060 PRINT"NO FILE":RESUME NEXT
1070 '
1080 '-ONERROR
1090 E1=ERR:E2=ERL
1100 IF E1=66 THEN ER$="DISK FULL"
1110 IF E1=68 THEN ER$="WRITE PROTECT"
1120 IF E1=70 THEN ER$="NOT READY"
1130 RESUME 1180
1140 '
1150 '-ERROR
1160 GOTO1180
1170 '
1180 '-DISP.ERROR
1190 ONERRORGOTO0:FORI=0TO1:I=-(INKEY$=""):NEXT:ER$="ERROR: "+ER$
1200 SCREEN0:LOCATE 20-LEN(ER$)\2,11:PRINT ER$;
1210 BEEP:I$=INPUT$(1):RETURN
1220 '
