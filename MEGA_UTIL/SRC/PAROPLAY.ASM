;SETWRT:	EQU	0171H
;WRTVRM:	EQU	0177H

; PARODIUS ROM RUTINES
IMUSIC:	EQU	6000H
SMUSIC:	EQU	43B4H
PMUSIC:	EQU	6006H

; PARODIUS WORK AREA
MUS_NO:	EQU	0E0B4H	;0E1C3H

; 0xE094
; 0xE0C1
; 0xE0EE
; 0xE11B
; 0xE148
SCCMXW:	EQU	0E1F3H

BGMNO_FADEOUT:	EQU	81H
BGMNO_FADEOFF:	EQU	80H
BGMNO_PAUSE:	EQU	83H
BGMNO_PAUSEOFF:	EQU	82H
BGMNO_STOP:	EQU	01H

TCOUNT:	DB	0
WAITCT:	DB	0	;WAIT CNT

;--------------------------------------
SETMUSIC:
	PUSH	AF
;	PUSH	BC
;	PUSH	DE
;	PUSH	HL
	DI
	CALL	SMUSIC
	EI
;	POP	HL
;	POP	DE
;	POP	BC
	POP	AF
	RET

;--------------------------------------
PLAY_END:
	DI

	; MUTE PSG
	LD	BC,300H + PSGIOA
	LD	A,0BFH
	LD	E,7
	OUT	(C),E		;R#7: MIX
	OUT	(PSGIOW),A	;0BFH - MUTE ALL
	XOR	A
PLAY_R_ML:
	INC	E
	OUT	(C),E		;R#8-10: VOL
	OUT	(PSGIOW),A	;0
	DJNZ	PLAY_R_ML

	; MUTE SCC
	CALL	SEL_SCC
	XOR	A
	LD	(SCCR_CHENA),A
	CALL	RET_SCC

	XOR	A
	LD	(SCCR_CHENA),A	

	JP	PLAY_R

;--------------------------------------
PLAY_SUB:
	LD	A,2	;CURSOR:EXIT
	LD	(PLAYWK+2),A

	LD	A,(MUS_NO)	;CURRENT BGM
	LD	(PLAYWK+0),A
	CALL	SETMUSIC

	LD	HL,2000H
	LD	(PAT_G_TB),HL
	LD	HL,0000H
	LD	(COLOR_TB),HL
	CALL	SETFONT

	LD	HL,TITLE_STR
	CALL	WR_MES

	EI

;--------------------------------------
L_PLAY:
	CALL	WR_CUR
	CALL	WR_PNUM
	CALL	WR_TITL
	XOR	A
	LD	(WAITCT),A
	EI
L_PLAYW:
	LD	DE,8FFH
	CALL	KEYIN	;D=KEYMTX NO / E=MASK
	CPL
	OR	A
	JP	Z,L_PLAYJ
	LD	A,(WAITCT)	;WAIT INTERVAL
	CP	20		;20/60 SEC
	JR	C,L_PLAYW
L_PLAYJ:
	EI
L_PLAY0:
	LD	DE,8FFH
	CALL	KEYIN	;D=KEYMTX NO / E=MASK
	LD	HL,P_JUMPT
	LD	B,8
L_PL0:	RLA
	JP	C,J_PL0
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	EX	DE,HL
	JP	(HL)
J_PL0:	INC	HL
	INC	HL
	DJNZ	L_PL0
	LD	DE,0710H	;STOP
	CALL	KEYIN	;D=KEYMTX NO / E=MASK
	JP	Z,P_STOP
	JP	L_PLAY0
P_JUMPT:
	DW	P_RIGH,P_UP,P_DOWN,P_LEFT
	DW	P_DEL,P_INS,P_HOME,P_SPAC

P_LCH:	POP	DE
	LD	A,(PLAYWK+2)
	CP	2
	JP	Z,L_PLAY
	LD	HL,PLAYWK+1
	PUSH	DE
	OR	A
	RET	Z
	DEC	HL
	RET

P_INS:	LD	C,4
	JR	P_RIGH_
P_RIGH:
	LD	C,1
P_RIGH_:
	CALL	P_LCH
	OR	A
	JR	NZ,P_ADD2
P_ADD:	LD	A,(HL)
	ADD	A,C
	AND	127
	LD	(HL),A
	JP	L_PLAY
P_ADD2:	LD	A,(HL)
	ADD	A,C
	CP	30H
	JR	C,J_PADD1
	CP	3EH
	JR	C,J_PADD2
	LD	A,1
	JR	J_PADD2
J_PADD1:
	LD	A,30H
J_PADD2:
	LD	(HL),A
	JP	L_PLAY

P_DEL:	LD	C,4
	JR	P_LEFT_
P_LEFT:	LD	C,1
P_LEFT_:
	CALL	P_LCH
	OR	A
	JR	NZ,P_SUB2
P_SUB:	LD	A,(HL)
	SUB	C
	AND	127
	LD	(HL),A
	JP	L_PLAY
P_SUB2:	LD	A,(HL)
	SUB	C
	CP	3EH
	JR	NC,J_PSUB1
	OR	A
	JR	Z,J_PSUB1
	CP	30H
	JR	NC,J_PSUB2
	LD	A,01H
	JR	J_PSUB2
J_PSUB1:
	LD	A,3DH
J_PSUB2:
	LD	(HL),A
	JP	L_PLAY

P_DOWN:	LD	A,(PLAYWK+2)
	DEC	A
	CP	255
	JR	NZ,L_PUD
	LD	A,2
	JR	L_PUD

P_UP:	LD	A,(PLAYWK+2)
	INC	A
	CP	3
	JR	NZ,L_PUD
	XOR	A
L_PUD:	LD	(PLAYWK+2),A
	JP	L_PLAY


P_HOME:	LD	A,BGMNO_FADEOUT	;81H
	CALL	SETMUSIC
	JP	L_PLAY

P_STOP:
	LD	A,BGMNO_FADEOFF	;80H
	CALL	SETMUSIC
	LD	A,BGMNO_STOP	;01H
	CALL	SETMUSIC
	JP	L_PLAY

P_SPAC:	LD	A,(PLAYWK+2)
	CP	2
	JP	Z,PLAY_END	;END JUKEBOX
	CALL	P_LCH
	LD	C,(HL)
	JP	Z,J_PL3
	LD	A,1
	CALL	SETMUSIC
	LD	(TCOUNT),A
P_SPAC_:
	LD	A,(TCOUNT)
	OR	A
	JR	NZ,P_SPAC_
	LD	A,80H
	CALL	SETMUSIC
	LD	A,87H
	CALL	SETMUSIC
J_PL3:	LD	A,C
	CALL	SETMUSIC
J_PL4:	LD	DE,0801H
	CALL	KEYIN	;D=KEYMTX NO / E=MASK
	JR	Z,J_PL4
	JP	L_PLAY

;--------------------------------------
WR_PNUM:
	DI
	LD	HL,3849H
	CALL	SETWRT
	LD	A,(PLAYWK+1)
	CALL	WR_PNU_
	LD	HL,3889H
	CALL	SETWRT
	LD	A,(PLAYWK+0)
	CALL	WR_PNU_
	EI
	RET
WR_PNU_:
	LD	B,A
	RRA
	RRA
	RRA
	RRA
	AND	15
	CALL	STR_NUM
	OUT	(VDP),A
	LD	A,B
	AND	15
	CALL	STR_NUM
	OUT	(VDP),A
	RET

;--------------------------------------
STR_NUM:
	OR	A
	DAA
	CP	10
	JR	C,STR_NUM_
	INC	A
STR_NUM_:
	ADD	A,30H
	RET

;--------------------------------------
WR_CUR:
	LD	HL,3841H
	LD	BC,300H
WR_CUR_L:
	LD	A,(PLAYWK+2)
	CP	C
	LD	A,0
	LD	D,0
	JR	NZ,WR_CUR_
	LD	A,'<'
	LD	D,'>'
WR_CUR_:
	DI
	CALL	WRTVRM
	LD	A,L
	ADD	A,6
	LD	L,A
	LD	E,A
	LD	A,C
	CP	2
	JR	NZ,WR_CUR_2
	DEC	L
WR_CUR_2:
	LD	A,D
	CALL	WRTVRM
	EI
	LD	L,E
	INC	C
	LD	A,40H-6
	ADD	A,L
	LD	L,A
	DJNZ	WR_CUR_L
	RET

;--------------------------------------
PLAY_TR:
	DI
	CALL	RDVDP
	CALL	PMUSIC

	LD	A,(TCOUNT)
	DEC	A
	LD	(TCOUNT),A

	CALL	RDVDP

	LD	A,(WAITCT)
	ADD	A,1
	SBC	A,0	; 255 = STOP
	LD	(WAITCT),A

	EI
	RET

;--------------------------------------
WR_TITL:
	DI
	LD	HL,388FH
	CALL	SETWRT
	LD	D,0
	LD	A,(PLAYWK)
	CP	30H
	JR	C,J_WTIT0
	CP	3EH
	JR	C,J_WTIT1

J_WTIT0:
	LD	A,3EH
J_WTIT1:
	SUB	30H
	RLA
	RLA
	RLA
	RLA
	JR	NC,J_WTIT3
	INC	D
J_WTIT3:
	AND	0F0H
	LD	E,A
	LD	HL,MUS_TIT
	ADD	HL,DE
	CALL	WR_MES_
	RET

;--------------------------------------
	INCLUDE	"MOJI-24.ASM"	;*FILE*
	INCLUDE	"PAROTITL.ASM"	;*FILE*
