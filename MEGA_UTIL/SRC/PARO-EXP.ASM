;*********************************
;     TIMER & KEY  INTERRUPT
; RAM C000-FEFF > VRAM 8000-BEFF
;*********************************
;       NAME  PARO-EXP

PSG_SV:	EQU	VCBC+19	;0FB8BH+19
SCCWAV:	EQU	VCBA	;0FB41H

V_PG1:	EQU	2
V_PG2:	EQU	4
V_PG3:	EQU	6
M_SIZE:	EQU	1CH
M_ADR:	EQU	0E000H

SVW_SP:	EQU	SAVSP	; STACK SAVE AREA
;STACK:	EQU	BLDCHK+1	;DISK WORK AREA HEAD

;--------------------------------------
; WORK AREA
;--------------------------------------
;STACKA:	DW	M_ADR-2	; MY STACK ADDRESS
STACKA:	DW	0D300H-2	; MY STACK ADDRESS

CAPSTA:	DW	0
W_CAP:	DB	0

;--------------------------------------
EX_TRAP:
	DI
;	IM	0

	; WAIT UNTIL RELEASE KEY
	IN	A,(KEYIO1)
	AND	0F0H
	OR	7
	OUT	(KEYIO1),A
	IN	A,(KEYIO0)
;	AND	4	;ESC
	AND	10H	;STOP
	JP	NZ,TRAP_MS

	;--------------------------------------
	LD	(SVW_SP),SP
	LD	HL,(STACKA)
	LD	SP,HL

	; SAVE PSG REG.
	LD	HL,PSG_SV
	LD	BC,PSGIOR
	XOR	A
L_PSGSV:
	OUT	(PSGIOA),A
	INI
	INC	A
	CP	13
	JP	NZ,L_PSGSV

	;(SAFETY ACCESS PSG R#7 JOYSTICK I/O)
	LD	A,(PSG_SV+7)
	AND	3FH
	OR	80H
	LD	(PSG_SV+7),A	; 10XXXXXX

	; MUTE PSG
	LD	A,7
	OUT	(PSGIOA),A
	LD	A,0BFH
	OUT	(PSGIOW),A

	; SAVE SCC
	LD	A,3FH
	LD	(BANK2),A
	LD	HL,9800H
	LD	DE,SCCWAV
	LD	BC,80H
	LDIR
	XOR	A
	LD	(SCCR_CHENA),A

	XOR	A
	CALL	CHGCAP

;--------------------------------------
;	LD	DE,0704H	;ESC
	LD	DE,0710H	;STOP
L_MENU_START_WAIT:
	CALL	KEYIN	;D=KEYMTX NO / E=MASK
	JP	Z,L_MENU_START_WAIT
S_CAP:	LD	DE,0
L_FUNC:	LD	HL,FC_TBL
L_FUNC_:
;MSX1未対応なのでvsync監視待ちはしない
;	CALL	WTVSYNC
;	CP	1
;	JR	NZ,J_CAP_
;	INC	DE
;	LD	A,E
;	AND	32	;32 FRAME

	INC	DE
	LD	A,D
	AND	20H	;2000H

;--------------------------------------
	LD	A,1
	JR	NZ,J_CAP
	XOR	A
J_CAP:	LD	BC,(CAPSTA)
	CP	C
	LD	(CAPSTA),A
	CALL	NZ,CHGCAP
J_CAP_:
	PUSH	DE
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	LD	A,E
	OR	D
	EX	AF,AF'	;AF':DE=0 ->LOOP
	INC	HL
	LD	C,(HL)
	INC	HL
	LD	B,(HL)
	INC	HL
	CALL	KEYIN	;D=KEYMTX NO / E=MASK
	POP	DE
	JP	NZ,L_FUNC_
	EX	AF,AF'	;AF:DE=0 ->LOOP
	LD	A,0
	CALL	NZ,CHGCAP
	LD	H,B
	LD	L,C
	JP	(HL)


RETURN_AT:
	JP	S_CAP

;--------------------------------------
RETURN:
;	LD	DE,0704H	;ESC
	LD	DE,0710H	;STOP
L_RET:	CALL	KEYIN	;D=KEYMTX NO / E=MASK
	JP	Z,L_RET

	; RESTORE PSG
	LD	HL,PSG_SV
	LD	BC,PSGIOW
	XOR	A
L_PSGLD:
	OUT	(PSGIOA),A
	OUTI
	INC	A
	CP	13
	JP	NZ,L_PSGLD

	; RESTORE SCC BANK
	LD	A,3FH
	LD	(BANK2),A
	LD	HL,SCCWAV
	LD	DE,9800H
	LD	BC,80H
	LDIR
	LD	A,(SCCMXW)
	LD	(SCCR_CHENA),A
	LD	A,(WBANK2)
	LD	(BANK2),A

	LD	A,1
	CALL	CHGCAP
	LD	SP,(SVW_SP)
;	IM	1
	JP	TRAP_MS

;--------------------------------------
; FUNCTIONS: [KEY]
FC_TBL:	DW	0602H,CTRL	; CTRL
	DW	0501H,SAVE	;'S'
	DW	0402H,LOAD	;'L'
	DW	0301H,CURE	;'C'
	DW	0420H,PLAY	;'P'
;	DW	0704H,RETURN	; ESC
	DW	0710H,RETURN	; STOP
	DW	0,L_FUNC	; END of DATA

;--------------------------------------
; FUNCTIONS: [CTRL]+[KEY]
CTRL:	LD	HL,FC_TBL_AT
	JP	L_FUNC_
FC_TBL_AT:
	DW	0120H,QUIT	;'@'
	DW	0501H,SAVE_AT_Q	;'S'
	DW	0402H,LOAD_AT	;'L'
;	DW	0420H,PLAY_	;'P'
	DW	0710H,RESET	; STOP
	DW	0,L_FUNC	; END of DATA

;--------------------------------------
SAVE_AT_Q:
	LD	A,V_PG2
	CALL	SAVE_
QUIT:
	LD	A,255
	LD	(PAR_REF),A
	LD	A,V_PG1
	CALL	SAVE_
	LD	A,V_PG3
	CALL	LOAD_
	JP	RET_BAS	;COMMON


;--------------------------------------
SAVE_AT:
	LD	A,255
	LD	(PAR_REF),A
	LD	A,V_PG1
	JP	SAVE_P
SAVE:	LD	A,V_PG2
SAVE_P:	LD	HL,RETURN_AT
	PUSH	HL
SAVE_:	INC	A
	LD	(SAVE__+1),A
	DEC	A
	LD	HL,M_ADR
	LD	B,M_SIZE
	LD	C,20H+40H
	CALL	TF_VRM

	LD	D,0
L_SAVE:	LD	C,D
	LD	HL,BUF
	LD	B,1
	LD	A,0
	CALL	TF_RAM
	LD	A,40H
	OR	D
	LD	C,A
	LD	HL,BUF
	LD	B,1
SAVE__:	LD	A,0	;vram page +1
	CALL	TF_VRM
	CALL	VRESET

	INC	D
	LD	A,40H
	SUB	A,D
	JP	NZ,L_SAVE

	RET

;--------------------------------------
LOAD_AT:
	LD	A,V_PG1
	JP	LOAD_M
LOAD:	LD	A,V_PG2
LOAD_M:
	LD	HL,(STACKA)
	LD	SP,HL
	LD	HL,RETURN_AT
	PUSH	HL
LOAD_:	LD	D,0
	LD	(LOAD__+1),A
	INC	A
	LD	(L_LOAD+1),A
L_LOAD:	LD	A,0	;vram page +1
	LD	HL,BUF
	LD	B,1
	LD	C,D
	CALL	TF_RAM
	LD	A,40H
	OR	D
	LD	C,A
	LD	HL,BUF
	LD	B,1
	LD	A,0
	CALL	TF_VRM

	INC	D
	LD	A,40H
	SUB	A,D
	JP	NZ,L_LOAD

	LD	HL,M_ADR
	LD	B,M_SIZE
	LD	C,20H
LOAD__:	LD	A,0	;vram page
	CALL	TF_RAM
	CALL	VRESET

;	LD	A,(WBANK0)
;	LD	(BANK0),A
	LD	A,(WBANK1)
	LD	(BANK1),A
	LD	A,(WBANK2)
	LD	(BANK2),A
	LD	A,(WBANK3)
	LD	(BANK3),A
	CALL	WR_CRG
	RET

;--------------------------------------
RESET:	
	LD	DE, 0602H	; CTRL
RESET1:	CALL	KEYIN	;D=KEYMTX NO / E=MASK
	JP	Z,RESET1
;	LD	DE,0704H	;ESC
	LD	DE,0710H	;STOP
RESET2:	CALL	KEYIN	;D=KEYMTX NO / E=MASK
	JP	Z,RESET2

	LD	SP,GAMESPA

	LD	A,1
	CALL	CHGCAP

	JP	PAR_ADR

;--------------------------------------
; PARODIUS TRAINER

CURE:
	LD	DE,CURETBL
CURE_L:
	LD	A,(DE)
	LD	L,A
	INC	DE
	LD	A,(DE)
	LD	H,A
	OR	L
	JR	Z,CURE_COPY_POS

	INC	DE
	LD	A,(DE)
	LD	(HL),A
	INC	DE
	JR	CURE_L

CURE_COPY_POS:
	; COPY POS
	;0E404H: PALYER POS Y
	;0E406H: PALYER POS X
	;0E414H: OPTION 1 POS Y
	;0E416H: OPTION 1 POS X
	;0E424H: OPTION 2 POS Y
	;0E426H: OPTION 2 POS X
	;0E40EH: PLAYER POSITION COUNTER (0-255)
	;0E440H-0E45FH: PLAYER POSITION LOG ([XX,YY] x 16)
	
	LD	A,(0E404H)	;PLAYER X
	LD	(0E414H),A	;OPTION 1 Y
	LD	(0E424H),A	;OPTION 1 Y
	LD	E,A
	LD	A,(0E406H)	;PLAYER Y
	LD	(0E416H),A	;OPTION 1 X
	LD	(0E426H),A	;OPTION 1 X
	LD	D,A
	
	LD	HL,0E440H
	LD	B,16
CURE_COPY_POS_L:
	LD	(HL),E
	INC	HL
	LD	(HL),D
	INC	HL
	DJNZ	CURE_COPY_POS_L

	JP	RETURN

CURETBL:
	; CHEAT TABLE
;	DB 25H, 0E2H,   X	;0E225H:  X ;PLAYER 1 TYPE
;	DB 26H, 0E2H,   X	;0E226H:  X ;PLAYER 2 TYPE
;				            ;0 - TAKO
;				            ;1 - PENGUIN
;				            ;2 - GOEMON
;				            ;3 - POPOLON
;				            ;4 - BIG VIPER

;	DB 04H, 0E4H,   X	;0E404H:  X ;PALYER POS Y
;	DB 06H, 0E4H,   X	;0E406H:  X ;PALYER POS X
;	DB 07H, 0E4H,   X	;0E407H:  X ;PALYER MAIN SPR PAT 
;	DB 08H, 0E4H,   X	;0E408H:  X ;PALYER MAIN COLOR
;	DB 09H, 0E4H,  15	;0E409H:04H ;PALYER SUB SPR PAT (SHIELD)
;	DB 0AH, 0E4H,  15	;0E40AH:0FH ;PALYER SUB COLOR (SHIELD)
;			                    ;SPR PAT:
;			                    ; 00H/08H:PLAYER
;			                    ; 04H/0CH:BARRIER
;			                    ; 10H/14H:OPTION

;	DB 2CH, 0E2H,   X	;0E22CH:  X ;DIFFICULTY (0:HARD / 1:EASY)
;	DB 67H, 0E2H,  00H	;0E267H-0E268H: ;DIFFICULTY?(UNIT:80H) (0F00H:ZENBU / 0400H:KATAI)
;	DB 68H, 0E2H,  00H

	DB 40H, 0E2H,  99	;0E240H:  X ;lIVES
;	DB 41H, 0E2H,   1	;0E241H:  X ;STAGE (1-6 NORMAL; 7-9 BONUS)

	DB 6DH, 0E2H,   0	;0E26DH:  X ;PASSWORD READY (0:ENABLE / 1:DISABLE)

	DB 35H, 0E3H,   4	;0E335H:04H ;SPEED (0-12)

	DB 0BH, 0E4H,   2	;0E40BH:02H ;OPTION (0-2)
	DB 10H, 0E4H,   1	;0E410H:  X ;ENABLE OPTION 1 (01H:ON)
;	DB 14H, 0E4H,   X	;0E414H:  X ;OPTION 1 POS Y
;	DB 16H, 0E4H,   X	;0E416H:  X ;OPTION 1 POS X
	DB 20H, 0E4H,   1	;0E420H:  X ;ENABLE OPTION 2 (01H:ON)
;	DB 24H, 0E4H,   X	;0E424H:  X ;OPTION 2 POS Y
;	DB 26H, 0E4H,   X	;0E426H:  X ;OPTION 2 POS X
;		                ;0E40EH:  X ;PLAYER POSITION COUNTER (0-255)
;		                ;0E440H-0E45FH: PLAYER POSITION LOG ([XX,YY] x 16)

	DB 30H, 0E4H,   0	;0E430H:00H ;NORMAL SHOT (0-2 / DEFAULT=2)
	DB 31H, 0E4H,   1	;0E431H:01H ;DOUBLE (0-1)  (DOUBLE: 0E430H AND 0E431H = 1)
	DB 32H, 0E4H,   2	;0E432H:02H ;LASER (0-2)
	DB 33H, 0E4H,   2	;0E433H:02H ;MISSILE (0-2)
	DB 00H, 0E4H,   2	;0E400H:02H ;SHIELD (2=ON / OTHER=OFF / DAFAULT:1 )
	DB 01H, 0E4H, 255	;0E401H:  X ;SHIELD POWER

	DB 4DH, 0E2H,   7	;0E24DH:  x ;POWER GAUGE POS (0-7)

	DB 50H, 0E2H,   0	;0E250H:FFH ;POWER UP MASK (0:ON 255:OFF) SPEED
	DB 51H, 0E2H, 0FFH	;0E251H:FFH ;POWER UP MASK (0:ON 255:OFF) MISSILE
	DB 52H, 0E2H, 0FFH	;0E252H:FFH ;POWER UP MASK (0:ON 255:OFF) DOUBLE 
	DB 53H, 0E2H, 0FFH	;0E253H:FFH ;POWER UP MASK (0:ON 255:OFF) LASER 
	DB 54H, 0E2H, 0FFH	;0E254H:FFH ;POWER UP MASK (0:ON 255:OFF) OPTION
;	DB 55H, 0E2H, 0FFH	;0E255H:FFH ;POWER UP MASK (0:ON 255:OFF) RESET
	DB 56H, 0E2H, 0FFH	;0E256H:FFH ;POWER UP MASK (0:ON 255:OFF) SHIELD

	DB 20H, 0E2H,  99H	;0E220H: 99 ;SPECIAL OPTION TIMER (BCD)
	DB 36H, 0E4H,   5	;0E436H:  X ;SPECIAL OPTION
;				            ;1 - VECTOR LASER
;				            ;2 - MINE
;				            ;3 - FROZEN ENEMIES AND SCROLL
;				            ;4 - UP LASER
;				            ;5 - INVINCIBLE DRILL
;				            ;6 - VERTICAL SCREEN WARP
;				            ;7 - HORIZONTAL SCREEN WARP


	DB 0,0

;GUIDE_DAT:
;	DB '- PAUSE >PASWORD >RETURN KEY -',13
;	DB 'ZENBU   :FULL WEAPON',13
;	DB 'KATAI   :HARD SHIELD',13
;	DB 'PARO    :W BELL (H WARP)',13
;	DB 'TAKO18TH:B BELL (DRILL)',13
;	DB 'BUTAKO  :G BELL (UP LASER)',13
;	DB 'KONAMI  :R BELL (VECTOR LASER)',13
;	DB 13
;	DB '- BELL -',13
;	DB 'YELLOW       :MEGA CRASH',13
;	DB 'WHITE        :HORIZONAL WARP',13
;	DB 'LIGHT GREEEN :VIRTILCAL WARP',13
;	DB 'LIGHT BLUE   :TIME STOP 5 SEC',13
;	DB 'PURPLE       :SPACE MINE',13
;	DB 'BLUE         :INVINCIBLE DRILL',13
;	DB 'GREEN        :UP LASER',13
;	DB 'RED          :VECTOR LASER',0
;	DB 0

;https://bhdaa.sakura.ne.jp/parodius/msx-parodius.html
;連射（改造）：HexEditなどのエディターで「003B57」の列にある「0F」を「01」に書き換る。
;無敵（改造）：HexEditなどのエディターで「00042F0」の列にある「D0」を「C9」に書き換え、「0006A60」の列にある「C8」を「C9」に書き換える。

