10 '==========================
20 '= EXT FOR KONAMI GENERAL =
30 '==========================
40 CLEAR2000,&HBFFF:DEFINTA-Z
50 VDP(9)=VDP(9)AND&HDF'COLOR0=TRANSPARENT
60 '
70 BLOAD"KONAMI.BIN"
80 A=&HF3F8:POKE A,&HF5:POKE A+2,&HF0:POKE A+1,&HFB:POKE A+3,&HFB
90 FORI=0TO4:POKE&HFBF0+I,ASC(MID$(",run-",I+1,1))-32:NEXT 'CLS+"RUN"+RETURN
100 DELETE 70-100
110 '
120 COLOR15,0,0:KEYOFF:SCREEN0:WIDTH40:GOSUB 1060'PAL
130 T$="WWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWW"
140 PRINT"X"+T$+"YV     QUICK SAVE/LOAD PATCH PROGRAM    VV       for KONAMI (MSX1) GAME         VZ";T$;"[";
150 '
160 DEFUSR9=&H156
170 DEFUSR0=&HC000:DEFUSR1=&HC003:DEFUSR2=&HC006:DEFUSR3=&HC009:DEFUSR4=&HC00C:CA=&HC00F
180 '
190 PRINT CHR$(10);"      - INSERT CARTRIDGE in SLOT -"
200 PRINT CHR$(10);"PAUSE ¼Ã ROM ¦ »¼Ã¶× PAUSE ¶²¼Þ® ¼Ã¸ÀÞ»²"
210 '
220 A=USR1(0):IF A=0 THEN 220
230 G$=USR4(""):ID=USR3(0)
240 DF$=MID$(G$,1,8):EX$="."+HEX$(ID)
250 IF A<4 THEN S$=" "+HEX$(A)+" " ELSE S$=HEX$(A AND 3)+"-"+HEX$((A AND 12)\4)
260 PRINT"  - Identified CARTRIDGE in SLOT ";S$;" -  "
270 A$="RC"+EX$+": "+G$:PRINT SPC(20-LEN(A$)\2);A$:PRINT
280 '
290 SOUND0,110:SOUND1,0:SOUND7,190:SOUND8,0
300 FORJ=0TO7:SOUND8,10-J:FORI=0TO4:SOUND0,25+I*24-J:TIME=0:FORK=0TO1:K=TIME:NEXT:NEXT:NEXT:SOUND8,0
310 '
320 PRINT"      SAVE DATA:VRAM 10000H-17FFFH";CHR$(10)
330 PRINT SPC(14);"NEW GAME:(G)"
340 IF PEEK(CA) THEN PRINTSPC(12);"CONT' GAME:(C)"
350 PRINT SPC(12);"LOAD STATE:(L)"
360 PRINT SPC(12);"SAVE STATE:(S)"
370 PX=POS(0):PY=CSRLIN
380 LOCATE PX,PY
390 PRINT SPC(12);"PAL CHANGE:(P) MSX";HEX$(2-PT)
400 PRINT CHR$(10);SPC(6);"COMMAND? _";
410 A=USR9(0)
420 A$="":I$=INKEY$
430 A=USR1(0):IF(A=0)OR(ID<>USR3(0))THEN 120
440 IF I$="" THEN 420
450 I=ASC(I$):I$=CHR$(I+((I>&H60)AND(I<&H7B))*&H20)
460 IF I$="L" THEN A$="LOAD"
470 IF I$="S" THEN A$="SAVE"
480 IF I$="G" THEN A$="NEW GAME":POKE CA,0
490 IF PEEK(CA)AND(I$="C") THEN A$="CONTINUE GAME"
500 IF I$="P" THEN PT=-(PT=0):GOSUB 1060:GOTO 380
510 IF A$="" THEN BEEP:GOTO 420
520 PRINT A$;
530 SOUND 0,110:SOUND 1,0:SOUND 7,190:SOUND 8,0
540 FORJ=0TO7:SOUND 8,10-J:FORI=0TO4:SOUND 0,230-I*28-J\6:TIME=0:FORK=0TO1:K=TIME:NEXT:NEXT:NEXT:SOUND8,0
550 '
560 IF A$="LOAD" THEN GOSUB 800:IF E1 THEN 120
570 IF A$="SAVE" THEN GOSUB 840:IF E1 THEN 120
580 '
590 SCREEN0:WIDTH40:GOSUB 1060'PAL
600 PRINT"X"+T$+"YV";SPC(38);"V";
610 PRINT"V STOP :  EXTRA MENU (BLINK CAPS LED)  VV";SPC(38);"VT- EXTRA MENU ------------------------WSV";SPC(38);"V";
620 PRINT"V     STOP:      QUIT EXTRA-MENU       VV      'S':        SAVE STATE          VV      'L':        LOAD STATE          VV CTRL+STOP:       RESET GAME          VV";SPC(38);"V";
630 PRINT"V CTRL+'@':   TSAVE > RETURN BASIC     VV CTRL+'S': SAVE STATE > TSAVE > BASIC VV CTRL+'L':    RESTORE TSAVE STATE     V";
640 PRINT"Z"+T$+"["
650 PRINT"           - HIT ANY KEY -";
660 I$=INPUT$(1)
670 '
680 SCREEN2,2:GOSUB 1060'PAL
690 KEY 1,"A=USR(0)"+CHR$(13)
700 A=USR(0)
710 '
720 SCREEN0:WIDTH40:GOSUB 1060'PAL
730 PRINT" DATA SAVE TO DISK ? (Y/N)_";
740 A=USR9(0):I$=INPUT$(1)
750 IF I$="Y"OR I$="y" THEN A$="SAVE":GOSUB 840:GOTO 780
760 IF I$<>"N"AND I$<>"n" THEN BEEP:GOTO740
770 '
780 GOTO 120'END
790 '
800 'LOAD
810 GOSUB 880:SCREEN5:SETPAGE2,2:E1=0:ONERRORGOTO1150:BLOAD F$+EX$,S
820 POKE CA,1:GOSUB 1040:RETURN
830 '
840 'SAVE
850 GOSUB 880:SCREEN5:SETPAGE2,2:E1=0:ONERRORGOTO1150:BSAVE F$+EX$,&H2000,&H7FFF,S
860 GOSUB 1040:RETURN
870 '
880 SCREEN0:WIDTH40
890 LOCATE0,6:PRINT"-------------- DATA FILES --------------";
900 ONERRORGOTO1120:FILES "*"+EX$:PRINT:ONERRORGOTO0
910 PRINT STRING$(40,"-");
920 LOCATE 0,0:I=USR9(0)
930 PRINT A$;:LINEINPUT" FILENAME _";F$
940 '
950 IF F$="" THEN F$=DF$
960 F$=LEFT$(F$+"       ",8)
970 LOCATE5,2:PRINT "FILE NAME = ";F$;EX$
980 '
990 LOCATE7,4:PRINT A$;" OK? (Y/N)_";
1000 I$=INPUT$(1)
1010 IF I$="Y"OR I$="y" THEN RETURN
1020 IF I$="N"OR I$="n" THEN 920 ELSE BEEP:GOTO1000
1030 '
1040 ON ERROR GOTO 0
1050 RETURN
1060 '-PAL-
1070 IF PT THEN 1090
1080 COLOR=NEW:RETURN'MSX2PAL
1090 A$="000000353464327437532356632643563664241535555777"'MSX1PAL
1100 FOR I=0 TO 15:COLOR=(I,VAL(MID$(A$,I*3+1,1)),VAL(MID$(A$,I*3+2,1)),VAL(MID$(A$,I*3+3,1))):NEXT:RETURN
1110 '
1120 '-FilesError
1130 PRINT"NO FILE":RESUME NEXT
1140 '
1150 '-ONERROR
1160 E1=ERR:E2=ERL
1170 IF E1=66 THEN ER$="DISK FULL"
1180 IF E1=68 THEN ER$="WRITE PROTECT"
1190 IF E1=70 THEN ER$="NOT READY"
1200 RESUME 1250
1210 '
1220 '-ERROR
1230 GOTO1250
1240 '
1250 '-DISP.ERROR
1260 ONERRORGOTO0:FORI=0TO1:I=-(INKEY$=""):NEXT:ER$="ERROR: "+ER$
1270 SCREEN0:LOCATE 20-LEN(ER$)\2,11:PRINT ER$;
1280 BEEP:I$=INPUT$(1):RETURN
1290 '
