; SEARCH KONAMI ROM DATA
;--------------------------------------
;; JUMP TO GAME HKEYI TRAP
;TRAP_MS:
;	RET		;RE-WRITE -> C3H
;GAME_TRAP_ADR:
;	DW	0000H	;RE-WRITE -> GAME_TRAP_ADR

GAME_SLOT:	EQU	ROMSLT
;--------------------------------------
GET_SLOT_BAS:
	CP	2	;INTEGER
	RET	NZ
	PUSH	HL
	POP	IX
	LD	A,(ROMSLT)
	LD	(IX+2),A
	LD	(IX+3),0
	RET

GET_GAME_ID_BAS:
	CP	2	;INTEGER
	RET	NZ
	PUSH	HL
	POP	IX
	LD	HL,(R_GAME_ID+0)	;DB 07 XX
	LD	(IX+2),H	;07
	LD	(IX+3),L	;XX
	RET

GET_TITLE_BAS:
	CP	3	;STRIING
	RET	NZ
	PUSH	DE
	POP	IX
	LD	A,(R_GAME_TITLE)
	LD	HL,R_GAME_TITLE+1
	LD	(IX+0),A
	LD	(IX+1),L
	LD	(IX+2),H
	RET

;--------------------------------------
; ROM SEARCH FOR BASIC
; (BASIC) A$=USR(0)
;	OK:	A$=ゲーム名
;	NG:	A$=空文字列
SEARCH_ROM_BAS:
	PUSH	DE	;DE=STRING DESCRIPTOR
	PUSH	HL	;HL=DAC
	PUSH	AF	;A=VALTYP

	CALL	SEARCH_ROM

	LD	A,(ROMSLT)
	
	POP	AF	;A=VALTYP
	CP	3	;文字列型
	JR	Z,	SEARCH_ROM_RET_STR

			;文字型以外
	POP	IX	;IX=DAC
	POP	DE	;DE=STRING DESCRIPTOR

	LD	A,2	;整数型に強制
	LD	(VALTYP),A
	LD	A,(ROMSLT)
	LD	(IX+2),A
	LD	(IX+3),0

	RET

SEARCH_ROM_RET_STR:
	POP	HL	;HL=DAC
	POP	IX	;DE=STRING DESCRIPTOR

	LD	A,(R_GAME_TITLE)
	LD	(IX+0),A	;文字数
	LD	HL,R_GAME_TITLE+1
	LD	(IX+1),L	;文字列本体アドレス
	LD	(IX+2),H	;文字列本体アドレス

	RET

;--------------------------------------
; SEARCH KONAMI ROM (WITH GAME_ID LIST)
; IN:	---
; OUT:	A : (ROMSLT) = FOUND SLOT
;	Z : 0=FOUND / 1=NOT_FOUND
SEARCH_ROM:
	LD	HL,0
	LD	(GAME_ID),HL
	LD	HL,KONAMI_ID_LIST
IFDEF SIN10BAI_SEARCH
	PUSH	HL
	CALL	GET_KONAMI_ID	;ID取得
	POP	HL
	JR	NC,NOT_FOUND_ROM	;見つからない
ENDIF;IFDEF SIN10BAI_SEARCH

SEARCH_GAMEID_L:
IFDEF SIN10BAI_SEARCH
	PUSH	HL
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	LD	HL,(GAME_ID)
	RST	DCOMPR	;CALL	CPHLDE
	POP	HL
	JR	Z,SEARCH_ROM_1	;見つかった
ELSE
	CALL	CHECK_ROM	;ID取得
	JR	Z,SEARCH_ROM_1	;見つかった
ENDIF
	LD	DE,KONAMI_ID_ISIZE
	ADD	HL,DE
	LD	A,(HL)
	OR	A
	JR	NZ,SEARCH_GAMEID_L
	JR	NOT_FOUND_ROM

SEARCH_ROM_1:
IFDEF SIN10BAI_SEARCH
ELSE
	PUSH	HL

	PUSH	HL
	POP	IX	;IX=HL
	LD	L,(IX+0)
	LD	H,(IX+1)
	LD	(GAME_ID),HL

	POP	HL
ENDIF;IFDEF SIN10BAI_SEARCH

	LD	DE,RESULT
	LD	BC,KONAMI_ID_ISIZE
	LDIR

	LD	IX,(4002H)	;IX=BOOT ADDRESS
	LD	C,31H	; LD SP,HHLL	;31 LL HH
	CALL	SEARCH_LDXXXX
	JR	NC,SEARCH_HOOK
	LD	(GAME_SP_ADR),HL	;FOUND SP
SEARCH_HOOK:
	LD	HL,(4002H)	;HL=BOOT ADDRESS
	LD	DE,H_KEYI	;LD (H_KEYI),A	;32 XX XX
	CALL	SEARCH_DE
	JR	Z,FOUND_HOOK
	LD	HL,(4002H)	;HL=BOOT ADDRESS
	LD	DE,H_TIMI	;LD (H_TIMI),A	;32 XX XX
	CALL	SEARCH_DE
FOUND_HOOK:
	LD	C,E
	LD	B,D	; BC = H_KEYI or H_TIMI
	LD	A,(HL)	; LD HL,XXXX	 ;21 XX XX (3)
	CP	21H	
	JR	NZ,NOT_FOUND_ROM
	INC	HL
	LD	A,(HL)
	LD	(GAME_TRAP_ADR+0),A
	INC	HL
	LD	A,(HL)
	LD	(GAME_TRAP_ADR+1),A
	INC	HL	; LD HL,XXXX	 ;21 XX XX (3) の次のアドレスへ
	LD	A,(HL)	; LD(HKEYI+1),HL ;22 9B FD (3)
	CP	22H
	JR	NZ,NOT_FOUND_ROM
	INC	HL
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	INC	BC	;HKEYI+1
	CALL	CPBCDE	;CP (BC-DE)
	JR	NZ,NOT_FOUND_ROM
	INC	HL	; LD(HKEYI+1),HL ;22 9B FD (3) の次のアドレスへ
	LD	(GAME_MAIN_ADR),HL
DETECT_ROM:
	JR	ROM_DETECTED

;TEST (BC - DE)
; OUT:	Cy = BC < DE
;	Z = BC == DE
; USE:	AF
CPBCDE:
	LD	A,B
	SUB	D
	RET	NZ
	LD	A,C
	SUB	E
	RET

NOT_FOUND_ROM:
	LD	HL,0
	LD	(GAME_ID),HL
	LD	HL,RET_BAS
	LD	(GAME_MAIN_ADR),HL
	LD	HL,0F0F0H
	LD	(GAME_SP_ADR),HL
	XOR	A
	LD	(ROMSLT),A

ROM_DETECTED:
	;RESTORE SLOT
	LD	A,(EXPTBL)
	LD	H,40H
	CALL	ENASLT

	LD	A,(ROMSLT)
	OR	A

	RET

;--------------------------------------
; IN:	HL=START ADDRESS
;	(RANGE= IX to IX+255)
;	DE=SEARCH VALUE
; OUT:	C=1:FOUND / 0:NOT FOUND
;	HL=NEXT ADDRESS
SEARCH_DE:
	LD	BC,0100H
SEARCH_DE_L:
	LD	A,E
	CPIR
	JR	NZ,SEARCH_DE_NF	;NOT MATCH
	JP	PO,SEARCH_DE_NF	;BC=0
	LD	A,D
	CP	(HL)
	JR	NZ,SEARCH_DE_NF
	;FOUND
	INC	HL
	SCF
	RET	;CY=1
SEARCH_DE_NF:
	OR	A
	RET	;CY=0

;--------------------------------------
; IN:  IX:START ADDRESS
;         (RANGE= IX to IX+255)
;      C:LD ?,XXXX
; OUT: HL:XXXX
SEARCH_LDXXXX:
	LD	B,0
SEARCH_SP_L:
	LD	A,(IX+0)
	INC	IX
	CP	C
	JR	Z,FOUND_LDSP
	DJNZ	SEARCH_SP_L
	OR	A
	RET	;CY=0
FOUND_LDSP:
	LD	L,(IX+0)
	LD	H,(IX+1)
	LD	DE,0C001H
	RST	DCOMPR	;CALL	CPHLDE
	
	JR	C,SEARCH_SP_L	; HL < 0C001H
	LD	DE,0F381H
	RST	DCOMPR	;CALL	CPHLDE
	JR	NC,SEARCH_SP_L	; HL > 0F380H
	INC	IX
	INC	IX
	SCF
	RET	;CY=1


; (Aを使用しないDCOMPRの代替）
; CP HL,DE
; IN:	HL,DE
; USE:	F
;CPHLDE:
;	OR	A
;	SBC	HL,DE
;	PUSH	AF
;	ADD	HL,DE
;	POP	AF
;	RET
;--------------------------------------

;--------------------------------------
	INCLUDE "KONALIST.ASM"	;*FILE*
