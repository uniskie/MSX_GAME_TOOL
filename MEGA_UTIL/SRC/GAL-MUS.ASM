;***************************
; Maze of GALIOUS SOUND-BOX
; FOR KANJI
;***************************
; A=USR(0)
	FNAME	"GAL-MUS.BIN"	;*BINHEAD*
	DB	0FEH	;*BINHEAD*
	DW	ORGADR	;*BINHEAD*
	DW	ENDFL-1	;*BINHEAD*
	DW	ORGADR ;*BINHEAD*;

;BIOS LABELS for GAME UTILS
	INCLUDE "BIOS-DEF.ASM"

USE_KEYBUFFER: EQU	1	;ENABLE KEY BUFFER in EX-BIOOS

;--------------------------------------
;--- GALIOUS ROM MUSIC RUTINE
SETMUSIC:	EQU	4E6BH
PMUSIC:	EQU	6004H

;--------------------------------------
ORGADR:	EQU	0C000H
	ORG	ORGADR

ORGFL:
	JP	GALIOUS_
;--------------------------------------
PLAYWK:	;BGM,SE,LINE(BGM,SE,EXIT)
	DB	88H,01H,0,0
SVW_SP:	DW	0

WAITCT:	DB	0	;WAIT CNT
;--------------------------------------
SV_HOOK:
	DS	5

;--------------------------------------
GALIOUS_:
	DI
;	IM	1
	LD	(SVW_SP),SP
	LD	SP,0D300H

	LD	HL,H_KEYI
	LD	DE,SV_HOOK
	LD	BC,5
	LDIR

	LD	A,4
	LD	C,40H
	LD	HL,8000H
	LD	B,40H
	CALL	TF_VRM	;COMMON
	LD	A,6
	LD	C,40H+20H
	LD	HL,0E000H
	LD	B,1FH
	CALL	TF_VRM	;COMMON

	LD	A,(RAMPG0+1)
	LD	H,40H
	CALL	ENASLT

	LD	A,5
	LD	C,0
	LD	HL,4000H
	LD	B,60H
	CALL	TF_RAM	;COMMON

	LD	HL,PLAY_TRAP
	LD	(H_KEYI+1),HL
	LD	A,0C3H
	LD	(H_KEYI),A

	CALL	PLAY

;--------------------------------------
RET_BAS:
	LD	A,4
	LD	C,0
	LD	HL,8000H
	LD	B,40H
	CALL	TF_RAM	;COMMON
	LD	A,6
	LD	C,20H
	LD	HL,0E000H
	LD	B,1FH
	CALL	TF_RAM	;COMMON

	LD	A,(EXPTBL)
	LD	H,40H
	CALL	ENASLT

	LD	HL,SV_HOOK
	LD	DE,H_KEYI
	LD	BC,5
	LDIR

	LD	SP,(SVW_SP)
;	IM	0
	EI

	RET

;--------------------------------------
PLAY:	LD	HL,0E000H
	LD	DE,0E001H
	LD	BC,010EFH
	LD	(HL),0
	LDIR
	JP	PLAY_SUB

;--------------------------------------
PLAY_R:	DI
	LD	A,7
	OUT	(PSGIO),A
	LD	A,0BFH
	OUT	(PSGIO+1),A

	LD	DE,0801H	;'SPC'
	CALL	KEYIN	;D=KEYMTX NO / E=MASK
	JP	Z,$-6
	RET

;--------------------------------------
PLAY_TRAP:
PLAY_TR:
	DI
	CALL	RDVDP 	;STATUS READ
	CALL	PMUSIC 	;MUSIC PLAY RUTINE
	CALL	SPRITE

	LD	A,(WAITCT)
	ADD	A,1
	SBC	A,0	; 255 = STOP
	LD	(WAITCT),A

	EI
	RET


;--------------------------------------
PLAY_SUB:
	XOR	A	;STOP BGM
	CALL	SETMUSIC

	LD	A,2	;CURSOR:EXIT
	LD	(PLAYWK+2),A
	LD	A,87H	;START BGM
	LD	(PLAYWK+0),A
	CALL	SETMUSIC
	EI

	LD	HL,TITLE_STR
	CALL	WR_MES

	CALL	WR_CUR
	CALL	WR_PNUM1
	CALL	WR_PNUM2
	CALL	WR_TITL

;--------------------------------------
L_PLAY:
	XOR	A
	LD	(WAITCT),A
	EI
L_PLAYW:
	CALL	KEYUPD	;KEY MATRIX UPDATE

	LD	DE,8FFH
	CALL	KEYIN	;D=KEYMTX NO / E=MASK
	CPL
	OR	A
	JP	Z,L_PLAYJ
	LD	A,(WAITCT)	;WAIT INTERVAL
	CP	20		;20/60 SEC
	JR	C,L_PLAYW

L_PLAYJ:
	EI
L_PLAY0:
	LD	DE,8FFH
	CALL	KEYIN	;D=KEYMTX NO / E=MASK
;	CALL	KEYTRG
	LD	HL,P_JUMPT
	LD	B,8
L_PL0:	RLA
	JP	C,J_PL0
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	EX	DE,HL
	JP	(HL)
J_PL0:	INC	HL
	INC	HL
	DJNZ	L_PL0

	CALL	KEYUPD	;KEY MATRIX UPDATE

	JP	L_PLAY0
P_JUMPT:
	DW	P_RIGH,P_UP,P_DOWN,P_LEFT
	DW	P_DEL,P_INS,P_HOME,P_SPAC

P_LCH:	POP	DE
	LD	A,(PLAYWK+2)
	CP	2
	JP	Z,L_PLAY
	LD	HL,PLAYWK+1
	PUSH	DE
	OR	A
	RET	Z
	DEC	HL
	RET

P_INS:	LD	C,4
	JP	$+5
P_RIGH:	LD	C,1
	CALL	P_LCH
	OR	A
	JP	NZ,P_ADD2
P_ADD:	LD	A,(HL)
	ADD	A,C
	CP	31H
	JP	C,$+5
	SUB	30H
	JP	P_SUBE
P_ADD2:	LD	A,(HL)
	ADD	A,C
	AND	255
	OR	128
P_ADDE:	LD	(HL),A
	CALL	WR_PNUM2
	CALL	WR_TITL
	JP	L_PLAY

P_DEL:	LD	C,4
	JP	$+5
P_LEFT:	LD	C,1
	CALL	P_LCH
	OR	A
	JP	NZ,P_SUB2
P_SUB:	LD	A,(HL)
	SUB	C
	JP	C,$+6
	JP	NZ,$+5
	ADD	A,30H
P_SUBE:	LD	(HL),A
	CALL	WR_PNUM1
	JP	L_PLAY
P_SUB2:	LD	A,(HL)
	SUB	C
	AND	255
	OR	128
	JP	P_ADDE

P_DOWN:	LD	A,(PLAYWK+2)
	DEC	A
	CP	255
	JP	NZ,L_PUD
	LD	A,2
	JP	L_PUD

P_UP:	LD	A,(PLAYWK+2)
	INC	A
	CP	3
	JP	NZ,L_PUD
	XOR	A
L_PUD:	LD	(PLAYWK+2),A
	CALL	WR_CUR
	JP	L_PLAY

P_HOME:	XOR	A
	LD	(0E1C1H),A
	INC	A
	LD	(0E1C0H),A
	JP	L_PLAY

P_SPAC:	LD	A,(PLAYWK+2)
	CP	2
	JP	Z,PLAY_R
	CALL	P_LCH
	LD	C,(HL)
	JP	Z,J_PL3
	LD	A,C
	CP	0FBH
	JP	NC,J_PL3
	XOR	A
	CALL	SETMUSIC
J_PL3:	LD	A,C
	CALL	SETMUSIC
	LD	DE,0801H
	CALL	KEYIN	;D=KEYMTX NO / E=MASK
	JP	Z,$-6	; ≈Ωœ√ﬁœ¬
	JP	L_PLAY

;--------------------------------------
	INCLUDE	"GMUS-SUB.ASM" ;*FILE*
	INCLUDE	"EX-BIOS.ASM" ;*FILE*
	INCLUDE	"KJ-VDP.ASM" ;*FILE*
	INCLUDE	"GAL-TL-K.ASM" ;*FILE*

ENDFL:
