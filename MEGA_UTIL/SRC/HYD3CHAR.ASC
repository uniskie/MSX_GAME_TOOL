1000 CLEAR1800,&HBEFF:DEFINT A-Z:PM=30:IM=69
1010 CL$="FighterRobber Priest Monk   WarriorThief  Bishop Bishop Hero   "
1020 WP$="45000102030405060708090a0b0c0d"'WEAPON
1030 AR$="450e0f1011121342"'ARMOR
1040 SH$="451415161718"'SHIELD
1050 HE$="45191a1b1c"'HELMET
1060 KEYOFF:SCREEN0:WIDTH40
1070 A=&H800+254*8:FORI=2TO3:VPOKE A+I*2,&HAA:VPOKE A+I*2+1,&H55:NEXT
1080 PRINT"USR5";:A$="FE02C02AF8F706FFAF862310FCC64A6F260022F8F7C9":FOR I=1TO LEN(A$)-1 STEP 2:POKE &HBF00+I\2,VAL("&H"+MID$(A$,I,2)):PRINT".";:NEXT:DEFUSR5=&HBF00:PRINT
1090 AB=&HC000:DIM IT$(IM),PR$(PM),PL(3),PA(3),P$(2),OD(4)
1100 ON STOP GOSUB 2210:STOPON:ON ERROR GOTO 3260
1110 'PARAM
1120 MV=20*2:MV$=STRING$(MV*2,255):PRINT"PARAM";
1130 FOR I=0 TO PM:PRINT".";
1140  READ PR$(I)
1150  PR$=PR$(I):X=VAL(MID$(PR$(I),1,2)):Y=VAL(MID$(PR$(I),3,2))
1160  J=Y*2+X\20:MID$(MV$,J*2+1,2)=RIGHT$("0"+MID$(STR$(I+1),2),2)
1170 NEXT:PRINT
1180 'ITEM
1190 PRINT"ITEM";:FOR I=0 TO IM-1:PRINT".";
1200  READ IT$(I)
1210  IT$(I)=IT$(I)+SPACE$(16-LEN(IT$(I)))
1220 NEXT:PRINT
1230 IT$(I)="   - empty -    "
1240 'CHECK
1250 FOR I=0TO3
1260  AD=AB+I*256:A=USR5(AD)
1270  OD(I)=-((A=PEEK(AD+255))AND(0<>PEEK(AD)))
1280 NEXT
1290 '
1300 'START EDIT
1310 AD=AB+PI*256:OD=OD(PI)
1320 X=0:Y=0:BN=0
1330 'SCREEN
1340 CLS:LOCATE0,23,0:PRINT"-HYDLIDE III CHARACTER EDITOR-";:LOCATE0,0
1350 FOR I=0 TO MV
1360  N=VAL(MID$(MV$,I+I+1,2))-1
1370  IF N>-1 THEN GOSUB 2300
1380  I=I+(1-OD)*MV
1390 NEXT
1400 IF OD=O THEN PRINT" (NO DATA)"
1410 'LOOP
1420 BN=(BN+X+Y*2)*OD
1430 IF BN>=MV THEN BN=BN-MV
1440 IF BN<0 THEN BN=MV+BN
1450 PN=VAL(MID$(MV$,BN*2+1,2))
1460 IF PN=0 THEN 1410
1470 A$=PR$(PN-1)
1480 LOCATE VAL(LEFT$(A$,2)),VAL(MID$(A$,3,2)),1:TIME=0:BK=1
1490 IF TIME>29 THEN BK=1-BK:LOCATE,,BK:TIME=0
1500 K=ASC(INKEY$+CHR$(0)):IF K=0 THEN 1490
1510 X=(K=29)-(K=28):Y=(K=30)-(K=31)
1520 IF K<>13 THEN 1410
1530 N=PN-1:GOSUB 2580:GOSUB 1560 'EDIT
1540 IF PL>13 THEN 1330'REDRAW
1550 GOSUB 2300:GOTO 1410
1560 'EDIT PARAM
1570 LOCATE 0,20,0':PRINT"No.";PN;
1580 PRINT PR$
1590 PL=PL(0):DO=AD+PA(0)
1600 ON PL GOSUB 1630,1640,1650,1660,1670,1680,1690,1700,1710,1720,1730,1740,1750,1760,2190
1610 LOCATE 0,20:PRINT SPACE$(39):RETURN
1620 'INPUT FUNC
1630 GOSUB 2430:GOTO 2670'NAME
1640 MX#=12:GOSUB 2400:GOTO 2780
1650 MX#=255:GOSUB 2400:GOTO 2780
1660 POKE DO,1-PEEK(DO):RETURN'FLAG
1670 MX#=65535!:GOSUB 2410:GOTO 2780
1680 GOTO 3140'CLASS
1690 RETURN'G
1700 MX#=16777215#:GOSUB 2420:GOTO 2780
1710 EN$=WP$:GOTO 3050'WEAPON
1720 EN$=AR$:GOTO 3050'ARMOR
1730 EN$=SH$:GOTO 3050'SHIELD
1740 EN$=HE$:GOTO 3050'HELMET
1750 GOTO 2960'ITEM
1760 'DISPLAY ITEMS
1770 CLS:PRINT"---- Item Display ";STRING$(22,"-")
1780 EM=19:VM=EM+4:VN=0:VX=0:VY=0
1790 FOR VN=0 TO VM:GOSUB 2030:NEXT:VN=0
1800 PRINT STRING$(40,"-");
1810 PRINT"* ESC=END, RET=ITEM, C=COUNT, SPC=USE";
1820 '
1830 VN=VN+VX+VY*2
1840 IF VN>VM THEN VN=VN-VM-1
1850 IF VN<0 THEN VN=VM+VN+1
1860 IF VN=VM THEN 1830'SKIP
1870 IF VN>EM THEN VA=&H83+VN-EM-1 ELSE VA=&H47+VN*3
1880 LOCATE (VN AND 1)*20,(VN\2)+1,1:TIME=0:BK=1
1890 IF TIME>29 THEN BK=1-BK:LOCATE,,BK:TIME=0
1900 K=ASC(INKEY$+CHR$(0)):IF K=0 THEN 1890
1910 VX=(K=29)-(K=28):VY=(K=30)-(K=31)
1920 IF K=ASC("C")OR K=ASC("c")THEN 1960'C
1930 IF K=13 THEN IF VN>EM THEN 1960 ELSE 1980 'RETURN
1940 IF K=32 THEN IF VN>EM THEN 1960 ELSE 1970'SPACE
1950 IF K=27 THEN PL=14:RETURN ELSE 1820
1960 P$(0)="Count":PA(0)=VA-(VN<=EM):PL(0)=3:PL(1)=0:GOTO 1990
1970 P$(0)="Using":PA(0)=VA+2:PL(0)=4:PL(1)=0:GOTO 1990
1980 P$(0)="Item":PA(0)=VA+0:PL(0)=13:PL(1)=0:GOTO 1990
1990 DO=AD+PA(0):GOSUB 2000:PR$=A$+" - "+P$(0):GOSUB 1560:LOCATE,,0:GOSUB2030:GOTO 1820
2000 'ITEM NAME (VN)
2010 IF VN>EM THEN A$=IT$(47+VN-EM-1):RETURN
2020 HI=PEEK(AD+VA):IF HI<IM THEN A$=IT$(HI):RETURN ELSE A$=IT$(IM):RETURN
2030 'PRINT ITEM(1)
2040 IF VN>=VM THEN PRINT SPC(20);:RETURN ELSE IF VN>EM THEN 2120
2050  DO=AD+&H47+VN*3
2060  HI=PEEK(DO)
2070  IC$=MID$(STR$(PEEK(DO+1)),2)
2080  IF HI<IM THEN A$=IT$(HI) ELSE A$=IT$(IM)
2090  LOCATE (VN AND 1)*20,(VN\2)+1
2100  PRINT RIGHT$("  "+IC$,3);MID$(":+",1+PEEK(DO+2),1);A$;
2110 RETURN
2120 'COIN
2130  A=VN-EM-1:DO=AD+&H83+A
2140  IC$=MID$(STR$(PEEK(DO)),2)
2150  A$=IT$(47+A)
2160  LOCATE (VN AND 1)*20,(VN\2)+1
2170  PRINT RIGHT$("  "+IC$,3);":";A$;
2180 RETURN
2190 'CHANGE CHARA
2200 PI=(PI+1)AND3:GOTO1300 'REDRAW ALL
2210 'END
2220 LOCATE,,0:BEEP
2230 FOR I=0TO3
2240  IF OD(I) THEN AD=AB+I*256:A=USR5(AD):POKE AD+255,A
2250 NEXT
2260 LOCATE0,0:PRINTSPC(40):POKE &HFBB2,&HAF:LOCATE0,0:PRINT"RUN"+CHR$(34)+"HYDLIDE3.BAS"
2270 A=&HF3F8:POKE A,&HF3:POKE A+1,&HFB:POKE A+2,&HF0:POKE A+3,&HFB:POKE &HFBF0,30:POKE &HFBF1,30:POKE &HFBF2,14
2280 ON ERROR GOTO 0:END
2290 '
2300 'RPINT PARAM
2310 A$=PR$(N):GOSUB 2580:P=0
2320 LOCATE CX,CY
2330 PRINT PR$;
2340 PL=PL(P):P$=P$(P)
2350 DO=AD+PA(P)
2360 ON PL GOSUB 2430,2400,2400,2510,2410,2500,2530,2420,2450,2460,2470,2480,2440,2520,2540
2370 IF PL=0 THEN DO=AD+PA(0):RETURN
2380 PRINT P$;PP$;
2390 P=P+1:GOTO 2340
2400 A#=PEEK(DO):GOTO 2560
2410 A#=PEEK(DO)+PEEK(DO+1)*256:GOTO 2560
2420 A#=PEEK(DO)+PEEK(DO+1)*256+PEEK(DO+2)*65536!:GOTO 2560
2430 PP$="":FOR K=0 TO 15:A=PEEK(DO+K):PP$=PP$+CHR$(A-(A=0)*32):NEXT:RETURN
2440 A=PEEK(DO):IF(A<IM)THEN PP$=IT$(A):RETURN ELSE PP$=IT$(IM):RETURN'ITEM
2450 A=PEEK(DO):E=A- 1:IF(A>0)AND(E<IM)THEN PP$=IT$(E):RETURN ELSE PP$=IT$(IM):RETURN'WEAPON
2460 A=PEEK(DO):E=A+13:IF(A>0)AND(E<IM)THEN PP$=IT$(E):RETURN ELSE PP$=IT$(IM):RETURN'ARMOR
2470 A=PEEK(DO):E=A+19:IF(A>0)AND(E<IM)THEN PP$=IT$(E):RETURN ELSE PP$=IT$(IM):RETURN'SHIELD
2480 A=PEEK(DO):E=A+24:IF(A>0)AND(E<IM)THEN PP$=IT$(E):RETURN ELSE PP$=IT$(IM):RETURN'HELMET
2490 MX=LEN(EN$)\2-1:IF(A>0)AND(A<=MX)THEN PP$=IT$(VAL("&H"+MID$(EN$,2*A+1,2))):RETURN ELSE PP$=IT$(IM):RETURN
2500 PP$=MID$(CL$,PEEK(DO)*7+1,7):RETURN
2510 PP$=MID$("OFFON ",PEEK(DO)*3+1,3):RETURN
2520 PL=0:RETURN
2530 PRINT"g":PL=0:RETURN
2540 PP$=HEX$(PI):RETURN
2550 '
2560 PP$=STR$(A#):PP$=RIGHT$(PP$,LEN(PP$)-1)
2570 PP$=RIGHT$("0000000"+PP$,PL):RETURN
2580 'GET PARAMETER
2590 CX=VAL(LEFT$(A$,2)):CY=VAL(MID$(A$,3,2))
2600 D=INSTR(A$,":"):PR$=MID$(A$,5,D-5)
2610 FOR K=0 TO 2
2620  PL(K)=VAL("&h"+MID$(A$,D+K*4+1,1))
2630  PA(K)=VAL("&h"+MID$(A$,D+K*4+2,2))
2640  P$(K)=MID$(A$,D+K*4,1)
2650 NEXT:RETURN
2660 '
2670 'INPUT NAME
2680 NL=15:PP$="":FOR K=0TO NL:PP$=PP$+CHR$(PEEK(DO+K)):NEXT
2690 IX=0:IY=PEEK(&HF3DC)-1:LOCATE0:PRINT"Input Name:";:GOSUB 2770
2700 LOCATE11+IX,,1:A=ASC(INPUT$(1))
2710 IF(A>31)AND(A<>127)THEN MID$(PP$,IX+1,1)=CHR$(A):PRINT CHR$(A);:A=28
2720 IF A=127 THEN PP$=LEFT$(PP$,IX)+MID$(PP$,IX+2)+CHR$(0):LOCATE11,,0:GOSUB 2770
2730 IX=IX+(A=29 AND IX>0)-(A=28 AND IX<NL)
2740 IF A<>13 THEN 2700
2750 PP$=LEFT$(PP$+STRING$(16,0),16):FORI=OTO15:POKE DO+I,ASC(MID$(PP$,I+1,1)):NEXT
2760 LOCATE0,IY:GOTO 3240'END
2770 FORI=0TOPL:A=ASC(MID$(PP$,I+1,1)):PRINT CHR$(-A*(A>31 AND A<>127)-254*(A<32 OR A=127));:NEXT:RETURN
2780 'INPUT NUMERIC
2790 IX=0:IY=PEEK(&HF3DC)-1
2800 PRINT CHR$(13)+"Input Value:";PP$;" max:";MX#;
2810 LOCATE IX+12,,1:A=ASC(INPUT$(1))
2820 IF A>47 AND A<58 THEN MID$(PP$,IX+1,1)=CHR$(A):PRINT CHR$(A);:A=28
2830 IX=IX+(A=29 AND IX>0)-(A=28):IF IX=PL THEN IX=0
2840 IF A<>13 THEN 2810
2850 A#=VAL(PP$)
2860 IF A#>MX# THEN BEEP:GOTO 2810
2870 A2=INT(A#/65536#):A3#=A#-A2*65536#
2880 A1=INT(A3#/256#):A0=A3#-A1*256#
2890 FOR K=0 TO 1
2900  IF(K>0)AND(PL(K)<>5)THEN 2940 'PL=5:CUR/MAX
2910  DO=AD+PA(K):POKE DO,A0
2920  IF MX#>256# THEN POKE DO+1,A1
2930  IF MX#>65535# THEN POKE DO+2,A2
2940 NEXT
2950 GOTO 3240'ENDI
2960 'SELECT ITEM
2970 MX=IM:EN=PEEK(DO):IF EN>IM THENEN=IM
2980 PRINT CHR$(13)+"ITEM(";RIGHT$(" "+MID$(STR$(EN),2),2);"):";IT$(EN);
2990 A=ASC(INPUT$(1))
3000 EN=EN+(A=30)+(A=29)-(A=31)-(A=28)
3010 IF EN<0 THEN EN=MX
3020 IF EN>MX THEN EN=0
3030 IF A<>13 THEN 2980
3040 IF EN=MX THEN EN=255:GOTO 3220 ELSE 3220'WRITE&ENDI
3050 'SELECT EQUIPMENT
3060 MX=LEN(EN$)\2-1:EN=PEEK(DO):EB=VAL("&h"+MID$(EN$,3,2)):IF EN>MX THEN EN=MX
3070 E=VAL("&h"+MID$(EN$,EN*2+1,2)):EI=-(E-EB+1)*(EN>0):PRINT CHR$(13)+"EQUIPMENT(";HEX$(EI);"):";IT$(E);
3080 A=ASC(INPUT$(1))
3090 EN=EN+(A=30)+(A=29)-(A=31)-(A=28)
3100 IF EN<0 THEN EN=MX
3110 IF EN>MX THEN EN=0
3120 IF A<>13 THEN 3070
3130 EN=EI:GOTO 3220'WRITE&ENDI
3140 'SELECT CLASS
3150 MX=LEN(CL$)\7-1:EN=PEEK(DO)
3160 PRINT CHR$(13)+"CLASS(";HEX$(EN);"):";MID$(CL$,EN*7+1,7);
3170 A=ASC(INPUT$(1))
3180 EN=EN+(A=30)+(A=19)-(A=31)-(A=28)
3190 IF EN<0 THEN EN=MX
3200 IF EN>MX THEN EN=0
3210 IF A<>13 THEN 3160
3220 'WRITE BACK
3230 POKE DO,EN
3240 'END INPUT
3250 LOCATE 0,,0:PRINT CHR$(13);SPC(39);:RETURN
3260 'ERROR
3270 FOR I=0TO3
3280  IF OD(I) THEN AD=AB+I*256:A=USR5(AD):POKE AD+255,A
3290 NEXT
3300 ON ERROR GOTO 0:RESUME
3310 '
3320 'PARAM
3330 DATA"0000No.:f00/0"
3340 DATA"0001Name:100/0
3350 DATA"3001Savior:4fc/0
3360 DATA"0002Class  :613/0
3370 DATA"3002Fairy :43b/0
3380 DATA"0003Monster:43c/0
3390 DATA"0004Level  :312/0
3400 DATA"2504Magic Spell:224/0
3410 DATA"0006Life   :516/514/0
3420 DATA"0007Magic  :520/51e/0
3430 DATA"0008Experience:835/0
3440 DATA"2006Attack Point:325/326/0
3450 DATA"2007Armor Class :327/328/0
3460 DATA"2008Agility     :329/32a/0
3470 DATA"0009Strength   :31c/31d/0
3480 DATA"2009Dextality   :32c/0
3490 DATA"0010Mind Force :334/0
3500 DATA"2010Inteligence :32e/0
3510 DATA"0011Charm      :332/333/0
3520 DATA"2011Luck        :330/331/0
3530 DATA"0012Have Weight:53eg7
3540 DATA"2012Master Weight:540g7
3550 DATA"0013< Item Display >:e47/0
3560 DATA"2213Item Weight:542g7
3570 DATA"0015Weapon:993/0
3580 DATA"0016Armor :a94/0
3590 DATA"0017Shield:b95/0
3600 DATA"0018Helmet:c96/0
3610 DATA"2315Attack Range :397/0
3620 DATA"2316Shot Distance:398/0
3630 DATA"2317Bullet Shape :399/0
3640 'ITEM
3650 DATA"Dagger","Short Sword","Long Sword","Holy Sword"
3660 DATA"Silver Sword","Fire Sword","Laser Sword","sling"
3670 DATA"Spear","Little Bow","Great Bow","Club"
3680 DATA"Axe","Battle Axe","Leather Suit","Chain Suit"
3690 DATA"Steal Armor","Silver Armor","Magic Armor","Fairy Armor"
3700 DATA"Shield","Mind Shield","Steel Shield","Life Sield"
3710 DATA"Fairy Shield","Helmet","Yamatec","Star Helmet"
3720 DATA"Fairy Helmet","Heal","Antidote","Herb"
3730 DATA"+3 Heal","+5 Heal","Max Heal","Cup of Soma"
3740 DATA"Phony Medicine","Gold Exchanger","Cross of God","Stone of Fighter"
3750 DATA"Tool of Camp","Midnight eye","Amulet of Magic","Food"
3760 DATA"Lamp","Oil","Super Oil","$10-Coin"
3770 DATA"$100-Coin","$1000-Coin","VIP Card","Cream of NIBEA'N"
3780 DATA"Amulet of ATSUTA","Doll of YUMI","T&E Package","Odo-Later"
3790 DATA"Stone of Pickles","Sun-Oil","Fang of Dragon","JIM'S Scroll"
3800 DATA"ID-Card","Stone of Cloud","Bugle","Idol of VARARYS"
3810 DATA"Amulet of Exit","Space Compass","Space Suit","Amulet of DM"
3820 DATA"Holy water"
