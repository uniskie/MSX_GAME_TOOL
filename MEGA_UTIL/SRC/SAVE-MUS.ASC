10 ' PARODIUS/GALIOUS ROM SAVE for MUSIC
20 CLEAR2000,&HBFFF:DEFINTA-Z
30 BLOAD"TFROM.BIN"
40 ONINTERVAL=1GOSUB500:INTERVALON
50 '
60 COLOR 13,0,0:KEYOFF:SCREEN0:WIDTH40
70 A$="WWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWW
80 PRINT"X"+A$+"YV      ROM EXTRACTER for JUKE BOX      VV     (PARODIUS/GALIOUS/F1-SPIRIT)     VV";SPC(38);"VZ";A$;"["
90 '
100 DEFUSR9=&H156 'CLEAR KEYBUF
110 DEFUSR0=&HC000'SEARCH ROM
120 DEFUSR2=&HC003'GET SLOT
130 DEFUSR3=&HC006'GET ID
140 DEFUSR4=&HC009'GET TITLE
150 DEFUSR1=&HC00C'ROM COPY
160 '
170 PRINT CHR$(10);"      - INSERT CARTRIDGE in SLOT -      "
180 PRINT CHR$(10);"PAUSE ¼Ã ROM ¦ »¼Ã¶× PAUSE ¶²¼Þ® ¼Ã¸ÀÞ»²
190 '
200 A=USR0(0):ID=USR3(0)
210 RESTORE 530
220 READ I:IF I=0 THEN 200
230 READ T$,N$,BK$
240 IF ID<>I THEN 220
250 '
260 IF A<4 THEN S$=" "+HEX$(A)+" " ELSE S$=HEX$(A AND 3)+"-"+HEX$((A AND 12)\4)
270 A$="- Identified "+T$+" in SLOT "+S$+" -"
280 PRINT SPC(20-LEN(A$)\2);A$
290 '
300 SOUND0,110:SOUND1,0:SOUND7,190:SOUND8,0
310 FORJ=0TO7:SOUND8,10-J:FORI=0TO4:SOUND0,25+I*24-J:W=0:FORK=0TO1:K=W:NEXT:NEXT:NEXT:SOUND8,0
320 '
360 B$=CHR$(0)+CHR$(&H40):FORI=1TOLEN(BK$)STEP2:B$=B$+CHR$(VAL("&H"+MID$(BK$,I,2))):NEXT
370 '
380 SCREEN7:SETPAGE1,1:CLS
390 A$=USR1(B$)
400 IF ASC(MID$(A$,1,1))<>255 THEN SCREEN0:BEEP:PRINT"ERROR":END
410 '
420 ED=&H40+(LEN(B$)-2)*&H20-1:ED$=RIGHT$("0"+HEX$(ED),2)
430 BSAVE N$,&H4000,VAL("&H"+ED$+"FF"),S
440 SCREEN0
450 PRINT"BANK:";:FORI=3TOLEN(B$):PRINT HEX$(ASC(MID$(B$,I,1)));"h ";:NEXT:PRINT
460 PRINT"ROM DATA:VRAM 14000H-1";ED$;"FFH"
470 PRINT"SCREEN7:SETPAGE1,1:";
480 PRINT"BSAVE";CHR$(34);N$;CHR$(34);",&h4000,&H";ED$;"FF,S"
490 END
500 '
510 W=W-(W<255):RETURN
520 '
530 DATA &H759,"PARODIUS","PARO-MUS.OBJ","0405060A"
540 DATA &H749,"GAROIUS" ,"GAR-MUS.OBJ" ,"000D0E"
550 DATA &H751,"GRADIUS2","GRA2-MUS.OBJ","040506"
560 DATA &H752,"F1SPIRIT","F1SP-MUS.OBJ","0D0E0F"
570 DATA 0
