10 '==========================
20 '= EXTENSION FOR GRADIUS2 =
30 '==========================
40 CLEAR2000,&HBFFF:DEFINTA-Z:COLOR 7,0,0
50 YC=1974:RC=&H751:T$="GRADIUS2":X$=".GR2":TL$="GRADIUS 2"
60 VDP(9)=VDP(9)AND&HDF'COLOR0=TRANSPARENT
70 '
80 BLOAD T$+".BIN"
90 A$=",run-":'CLS+"RUN"+RETURN
100 A=&HF3F8:POKE A,&HF0+LEN(A$):POKE A+1,&HFB:POKE A+2,&HF0:POKE A+3,&HFB:FORI=1TOLEN(A$):POKE&HFBEF+I,ASC(MID$(A$,I,1))-32:NEXT
110 DELETE 80-110
120 '
130 KEYOFF:SCREEN0:WIDTH40:GOSUB 1020'PAL
140 I$="RC-"+HEX$(RC)+" ¢ "+TL$+" £":I=LEN(I$):A$=SPACE$(38):MID$(A$,(LEN(A$)-I)\2+1,I)=I$
150 I$="(C) "+MID$(STR$(YC),2)+" KONAMI":I=LEN(I$):B$=SPACE$(38):MID$(B$,(LEN(B$)-I)\2+1,I)=I$
160 L$="WWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWW"
170 PRINT"X"+L$+"YV"+A$+"VV"+B$+"VV  QUICK SAVER/LOADER (PATCH PROGRAM)  VZ";L$;"[";
180 '
190 DEFUSR9=&H156
200 DEFUSR0=&HC000:DEFUSR1=&HC003:CA=&HC006
210 '
220 PRINT CHR$(10);"      - INSERT CARTRIDGE in SLOT -"
230 PRINT CHR$(10);"PAUSE ¼Ã ROM ¦ »¼Ã¶× PAUSE ¶²¼Þ® ¼Ã¸ÀÞ»²"
240 '
250 A=USR1(RC):IF A=0 THEN 250
260 '
270 IF A<4 THEN S$=" "+HEX$(A)+" " ELSE S$=HEX$(A AND 3)+"-"+HEX$((A AND 12)\4)
280 PRINT"  - Identified CARTRIDGE in SLOT ";S$;" -";CHR$(10)
290 '
300 SOUND0,110:SOUND1,0:SOUND7,190:SOUND8,0
310 FORJ=0TO7:SOUND8,10-J:FORI=0TO4:SOUND0,25+I*24-J:TIME=0:FORK=0TO1:K=TIME:NEXT:NEXT:NEXT:SOUND8,0
320 '
330 PRINT"      SAVE DATA:VRAM 10000H-17FFFH";CHR$(10)
340 PRINT SPC(14);"NEW GAME:(G)"
350 IF PEEK(CA) THEN PRINTSPC(12);"CONT' GAME:(C)"
360 PRINT SPC(12);"LOAD STATE:(L)"
370 PRINT SPC(12);"SAVE STATE:(S)":PY=CSRLIN
380 LOCATE 12,PY:PRINT"PAL CHANGE:(P) MSX";HEX$(2-PT)
390 PRINT CHR$(10);SPC(6);"COMMAND? _";
400 A=USR9(0)
410 A$="":I$=INPUT$(1):I=ASC(I$):I$=CHR$(I+((I>&H60)AND(I<&H7B))*&H20)
420 IF I$="L" THEN A$="LOAD"
430 IF I$="S" THEN A$="SAVE"
440 IF I$="G" THEN A$="NEW GAME":POKE CA,0
450 IF PEEK(CA)AND(I$="C") THEN A$="CONTINUE GAME"
460 IF I$="P" THEN PT=-(PT=0):GOSUB 1020:GOTO 380
470 IF A$="" THEN BEEP:GOTO 410
480 PRINT A$;
490 SOUND 0,110:SOUND 1,0:SOUND 7,190:SOUND 8,0
500 FORJ=0TO7:SOUND 8,10-J:FORI=0TO4:SOUND 0,230-I*28-J\6:TIME=0:FORK=0TO1:K=TIME:NEXT:NEXT:NEXT:SOUND8,0
510 '
520 IF A$="LOAD" THEN GOSUB 760:IF E1 THEN 130
530 IF A$="SAVE" THEN GOSUB 800:IF E1 THEN 130
540 '
550 SCREEN0:WIDTH40:GOSUB 1020'PAL
560 PRINT"X"+L$+"YV";SPC(38);"V";
570 PRINT"V STOP :  EXTRA MENU (BLINK CAPS LED)  VV";SPC(38);"VT- EXTRA MENU ------------------------WSV";SPC(38);"V";
580 PRINT"V     STOP:      QUIT EXTRA-MENU       VV      'S':        SAVE STATE          VV      'L':        LOAD STATE          VV CTRL+STOP:       RESET GAME          VV";SPC(38);"V";
590 PRINT"V CTRL+'@':   TSAVE > RETURN BASIC     VV CTRL+'S': SAVE STATE > TSAVE > BASIC VV CTRL+'L':    RESTORE TSAVE STATE     VV      'C':    CURE & FILL UP ITEM     VV      'P':         JUKE-BOX           V";
600 PRINT"Z"+L$+"["
610 PRINT"           - HIT ANY KEY -";
620 I$=INPUT$(1)
630 '
640 SCREEN2,2:GOSUB 1020'PAL
650 KEY 1,"A=USR(0)"+CHR$(13)
660 A=USR(0)
670 '
680 SCREEN0:WIDTH40:GOSUB 1020'PAL
690 PRINT" DATA SAVE TO DISK ? (Y/N)_";
700 A=USR9(0):I$=INPUT$(1)
710 IF I$="Y"OR I$="y" THEN A$="SAVE":GOSUB 800:GOTO 740
720 IF I$<>"N"AND I$<>"n" THEN BEEP:GOTO700
730 '
740 GOTO 130'END
750 '
760 'LOAD
770 GOSUB 840:SCREEN5:SETPAGE2,2:E1=0:ONERRORGOTO1110:BLOAD F$+X$,S
780 POKE CA,1:GOSUB 1000:RETURN
790 '
800 'SAVE
810 GOSUB 840:SCREEN5:SETPAGE2,2:E1=0:ONERRORGOTO1110:BSAVE F$+X$,&H2000,&H7FFF,S
820 GOSUB 1000:RETURN
830 '
840 SCREEN0:WIDTH40
850 LOCATE0,6:PRINT"-------------- DATA FILES --------------";
860 ONERRORGOTO1080:FILES "*"+X$:PRINT:ONERRORGOTO0
870 PRINT STRING$(40,"-");
880 LOCATE 0,0:I=USR9(0)
890 PRINT A$;:LINEINPUT" FILENAME _";F$
900 '
910 IF F$="" THEN F$=T$
920 F$=LEFT$(F$+"       ",8)
930 LOCATE5,2:PRINT "FILE NAME = ";F$;X$
940 '
950 LOCATE7,4:PRINT A$;" OK? (Y/N)_";
960 I$=INPUT$(1)
970 IF I$="Y"OR I$="y" THEN RETURN
980 IF I$="N"OR I$="n" THEN 880 ELSE BEEP:GOTO960
990 '
1000 ON ERROR GOTO 0
1010 RETURN
1020 '-PAL-
1030 IF PT THEN 1050
1040 COLOR=NEW:RETURN'MSX2PAL
1050 A$="000000353464327437532356632643563664241535555777"'MSX1PAL
1060 FOR I=0 TO 15:COLOR=(I,VAL(MID$(A$,I*3+1,1)),VAL(MID$(A$,I*3+2,1)),VAL(MID$(A$,I*3+3,1))):NEXT:RETURN
1070 '
1080 '-FilesError
1090 PRINT"NO FILE":RESUME NEXT
1100 '
1110 '-ONERROR
1120 E1=ERR:E2=ERL
1130 IF E1=66 THEN ER$="DISK FULL"
1140 IF E1=68 THEN ER$="WRITE PROTECT"
1150 IF E1=70 THEN ER$="NOT READY"
1160 RESUME 1210
1170 '
1180 '-ERROR
1190 GOTO1210
1200 '
1210 '-DISP.ERROR
1220 ONERRORGOTO0:FORI=0TO1:I=-(INKEY$=""):NEXT:ER$="ERROR: "+ER$
1230 SCREEN0:LOCATE 20-LEN(ER$)\2,11:PRINT ER$;
1240 BEEP:I$=INPUT$(1):RETURN
1250 '
