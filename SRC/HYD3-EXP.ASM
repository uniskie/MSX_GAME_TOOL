;******************************
;    TIMER & KEY  INTERRUPT
;RAM:	C000-FEFF > VRAM 8000-BEFF
;******************************
	FNAME	"HYD3-EXP.BOF"	;*BINHEAD*
	DB	0FEH	;*BINHEAD*
	DW	ORGADR	;*BINHEAD*
	DW	ENDFL-1	;*BINHEAD*
	DW	ORGADR	;*BINHEAD*
;
VERSION:	EQU	00102H	;VERSION ID
;
	INCLUDE "HYD3HEAD.ASM"
;
GAMESP:	EQU	SAVSP
EXP_SP:	EQU	EXPSP	;SVWE_SP+1
EXPSIZ:	EQU	ENDFL-ORGADR
;
ORGADR:	EQU	EXPADR
	ORG	ORGADR
;
	DW	VERSION
	DW	EXPSIZ
	DW	EX_TRAP
	DW	SET_UP
	DW	CONTINUE
;WAITTM:	DW	0	; WAIT
;CAPSTA:	DB	0
;
;--- SET_UP SUB
; IN:	DE=RETURN ADDRESS
;	B=RETURN RAM SLOT
;	C=GAME ROM SLOT
SETUPSUB:	;
	LD	(RET_BAS+1),HL
	LD	A,B
	LD	(RAM_SLOT+1),A
	LD	A,C
	LD	H,80H
	JP	ENASLT
;--- SET_UP
SET_UP:	LD	SP,STACKA
	CALL	SETUPSUB
;
	LD	HL,VERSION
	LD	(EXPVER),HL	;SET VIRSION
;
	LD	HL,4016H
;
START:	LD	A,0C9H
	LD	(H_KEYI),A
	LD	(H_TIMI),A
	JP	(HL)
;
;---- CONTINUE
CONTINUE:	;
	LD	SP,(EXP_SP)
;SVWE_SP:	LD	SP,0	;over wr.
	CALL	SETUPSUB
;   (ﾊﾞﾝｸ ｾﾚｸﾄ)
	LD	HL,BANK_0
	LD	DE,6000H
	LDI
	LD	DE,6800H
	LDI
	LD	DE,7000H
	LDI
	LD	DE,7800H
	LDI
	JP	S_CAP
;
;
;----- EXTRA TRAP (iNTERRUPT)
EX_TRAP:	;
	DI
	PUSH	HL
	PUSH	AF
;
;		LD	HL,(WAITTM)
;	WAITL:	LD	A,L
;		OR	H
;		JR	Z,WAITE
;		DEC	HL
;		JR	WAITL
;	WAITE:	;
;
	IN	A,(KEYIO2)
	AND	0F0H
	OR	7
	OUT	(KEYIO2),A
	IN	A,(KEYIO1)
	AND	16	;STOP
	JP	NZ,RETRET
;
;--- COMMAND MODE ---
	LD	(GAMESP),SP
	LD	SP,STACKA
;	PUSH	HL
	PUSH	DE
	PUSH	BC
	EXX
	EX	AF,AF'
	PUSH	HL
	PUSH	DE
	PUSH	BC
	PUSH	AF
	PUSH	IX
	PUSH	IY
;
	LD	(EXP_SP),SP
;
	LD	HL,PSG_SV
	LD	BC,PSGIOR+0D00H+0D00H
	XOR	A
L_PSGSV:	;
	OUT	(PSGIOA),A
	INI
	INC	A
	DJNZ	L_PSGSV
;
;	safty psg#7 joy I/O mode
	LD	A,(PSG_SV+7)
	AND	3FH
	OR	80H
	LD	(PSG_SV+7),A	; 10XXXXXX
;
	LD	A,7
	OUT	(PSGIOA),A
	LD	A,0BFH
	OUT	(PSGIOW),A
;
	LD	DE,0710H	;STOP
L_ESC:	CALL	KEYIN
	JR	Z,L_ESC
S_CAP:	LD	DE,0
L_FUNC:	LD	HL,FC_TBL
L_FUNC_:	;
;MSX1未対応&ﾒﾓﾘ不足なのでvsync監視待ちはしない
;	CALL	WTVSYNC
;	CP	1
;	JR	NZ,J_CAP_
;	INC	DE
;	LD	A,E
;	AND	32	;32 FRAME
;
	INC	DE
	LD	A,D
	AND	20H	;2000H
;
	LD	A,1
	JR	NZ,J_CAP
	XOR	A
J_CAP:	LD	BC,(CAPSTA)	;B=NOT USE
	CP	C
	LD	(CAPSTA),A
	CALL	NZ,CHGCAP
J_CAP_:	;
	PUSH	DE
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	INC	HL
	LD	C,(HL)
	INC	HL
	LD	B,(HL)
	INC	HL
	CALL	KEYIN
	POP	DE
	JR	NZ,L_FUNC_
	LD	H,B
	LD	L,C
	JP	(HL)
;
;-------------
;	WAITO1:	LD	A,13
;		JR	WAITCH
;	WAITO2:	LD	A,26
;		JR	WAITCH
;	WAITOF:	XOR	A
;	WAITCH:	LD	(WAITTM+1),A
;		JR	L_FUNC
;--------------
RETURN:	LD	DE,0710H	;STOP
L_RET:	CALL	KEYIN
	JR	Z,L_RET
	LD	HL,PSG_SV
	LD	BC,PSGIOW+0D00H+0D00H
	XOR	A
L_PSGLD:	;
	OUT	(PSGIOA),A
	OUTI
	INC	A
	DJNZ	L_PSGLD
;
	LD	A,(CAPST)
	NEG
	INC	A
	CALL	CHGCAP
;
	POP	IY
	POP	IX
	POP	AF
	POP	BC
	POP	DE
	POP	HL
	EX	AF,AF'
	EXX
	POP	BC
	POP	DE
;	POP	HL
;
	LD	SP,(GAMESP)
;
RETRET:	POP	AF
	POP	HL
	JP	KEYINT
;
;-------------
FC_TBL:	DW	0602H,CTRL	; CTRL
	DW	0710H,RETURN	; STOP
;	DW	0001H,WAITOF	; 0
;	DW	0002H,WAITO1	; 1
;	DW	0004H,WAITO2	; 2
;;	DW	0510H,WAITO2	; W
	DW	0,L_FUNC	; END of DATA
;-------------
CTRL:	LD	HL,FC_TBL@
	JP	L_FUNC_
FC_TBL@:	;
	DW	0120H,QUIT	; CTRL+'@'
	DW	0702H,ENDING	; CTRL+F5
	DW	0802H,RESET	; CTRL+HOME
;	DW	0510H,WAITOF	; CTRL+W
	DW	0,L_FUNC	; END of DATA
;-------------
;
QUIT:	;
RAM_SLOT:	;
	LD	A,0	;SLOT OVER-WRITE
	LD	H,80H
	CALL	ENASLT
	LD	HL,BANK_0	;BANK No. LIST (RECEIVE DATA)
RET_BAS:	;
	JP	0	;RETURN (ADR OVER-WRITTE)
;-------------
RESET:	;
	XOR	A
	LD	(6000H),A
	LD	HL,4010H
	JP	START
;-------------
ENDING:	XOR	A
	LD	(6000H),A
	LD	A,025H
	LD	(6800H),A
	LD	HL,4013H
	JP	START
;------ KEY ON CHECK
; D:KEYﾏﾄﾘｸｽ No.
; E:MASK
KEYIN:	IN	A,(KEYIO2)
	AND	0F0H
	OR	D
	OUT	(KEYIO2),A
	IN	A,(KEYIO1)
	AND	E
	RET
;
;; ※ﾒﾓﾘ不足なので未使用
;; WAIT VSYNC
;; USE:AF,BC
;; RET:A 1=ok/0=wait
;; *** NEED DI !
;WTVSYNC:	;
;;	DI
;	LD	C,99H
;	LD	A,2	;S#2
;	OUT	(C),A
;	LD	A,15+80H
;	OUT	(C),A	;SET S#2 READ
;;
;	LD	A,(WTVSYNCS)
;	LD	B,A	;NEXT WTVSYNCS
;	CP	1
;	JR	Z,WTVSYN1
;	JR	NC,WTVSYN2
;;
;WTVSYN0:	; NO VBI = 0
;	IN	A,(C)
;	AND	40H	;VERTIACL BLANKING INTERVAL
;	JR	Z,WTVSYNCE
;	INC	B	;B=1
;	JR	WTVSYNCE
;WTVSYN1:	; VBI EDGE = 1
;	INC	B	;B=2
;	JR	WTVSYNCE
;WTVSYN2:	; IN VBI = 2
;	IN	A,(C)
;	AND	40H	;VERTIACL BLANKING INTERVAL
;	JR	NZ,WTVSYNCE
;	LD	B,0
;WTVSYNCE:	;
;	XOR	A	;S#0
;	OUT	(C),A
;	LD	A,15+80H
;	OUT	(C),A	;SET S#0 READ (DEFAULT)
;;	EI
;	LD	A,B
;	LD	(WTVSYNCS),A
;	RET
;WTVSYNCS:	;
;	DB	0
;
ENDFL:	;
;-> tniASM : SIZE CHECK
IF ENDFL>=VOICQM
	* SIZE OVER ERROR
ENDIF
;<-
