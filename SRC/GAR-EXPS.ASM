;*********************************
;     TIMER & KEY  INTERRUPT
; RAM C000-FEFF > VRAM 8000-BEFF
;*********************************
;       NAME  GAR-EXPS
;
KEYIO:	EQU	0A9H
KEYIO1:	EQU	0A9H
KEYIO2:	EQU	0AAH
PSGIOA:	EQU	0A0H
PSGIOW:	EQU	0A1H
PSGIOR:	EQU	0A2H
PSG_SV:	EQU	0FB8BH+19
;
BUF:	EQU	0F55EH+1	;0F55EH,258
HIMEM:	EQU	0FC4AH	;BASIC FREE AREA
;BLDCHK:	EQU	0F377H
;STACK:	EQU	BLDCHK+1	;DISK WORK AREA HEAD
SAVSP:	EQU	0FB36H	;PLAYÌÞÝ Ö³ SP Î¿ÞÝ
REG_0SV:	EQU	0F3DFH
CHGCAP:	EQU	00132H
CHGSND:	EQU	00135H
;
V_PG1:	EQU	2
V_PG2:	EQU	4
V_PG3:	EQU	6
M_SIZE:	EQU	1CH
M_ADR:	EQU	0E000H
;
W_BANK:	EQU	0F0F1H
BANK1:	EQU	06000H
BANK2:	EQU	08000H
BANK3:	EQU	0A000H
;
TRAP_MS:	EQU	40EFH	;M-GALIOUS
;
;STACKA:	DW	M_ADR	; MY STACK ADDRESS
STACKA:	DW	0D300H	; MY STACK ADDRESS
;
EX_TRAP:	DI
;	IM	0
	IN	A,(KEYIO2)
	AND	0F0H
	OR	7
	OUT	(KEYIO2),A
	IN	A,(KEYIO1)
	AND	4	;ESC
	JP	NZ,TRAP_MS
;
	LD	(SVW_SP),SP
	LD	HL,(STACKA)
	LD	SP,HL
;
	LD	HL,PSG_SV
	LD	BC,PSGIOR
	XOR	A
L_PSGSV:	OUT	(PSGIOA),A
	INI
	INC	A
	CP	13
	JP	NZ,L_PSGSV
;
;	safty psg#7 jOy I/O mode
	LD	A,(PSG_SV+7)
	AND	3FH
	OR	80H
	LD	(PSG_SV+7),A	; 10XXXXXX
;
	LD	A,7
	OUT	(PSGIOA),A
	LD	A,0BFH
	OUT	(PSGIOW),A
;
	XOR	A
	CALL	CHGCAP
;
	LD	DE,0704H	;ESC
L_ESC:	CALL	KEYIN
	JP	Z,L_ESC
S_CAP:	LD	DE,0
L_FUNC:	LD	HL,FC_TBL
L_FUNC_:	;
;MSX1–¢‘Î‰ž‚È‚Ì‚ÅvsyncŠÄŽ‹‘Ò‚¿‚Í‚µ‚È‚¢
;	CALL	WTVSYNC
;	CP	1
;	JR	NZ,J_CAP_
;	INC	DE
;	LD	A,E
;	AND	32	;32 FRAME
;
	INC	DE
	LD	A,D
	AND	20H	;2000h
;
	LD	A,1
	JP	NZ,J_CAP
	XOR	A
J_CAP:	LD	BC,(CAPSTA)
	CP	C
	LD	(CAPSTA),A
	CALL	NZ,CHGCAP
J_CAP_:	;
	PUSH	DE
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	LD	A,E
	OR	D
	EX	AF,AF'	;AF':DE=0 ->LOOP
	INC	HL
	LD	C,(HL)
	INC	HL
	LD	B,(HL)
	INC	HL
	CALL	KEYIN
	POP	DE
	JP	NZ,L_FUNC_
	EX	AF,AF'	;AF:DE=0 ->LOOP
	LD	A,0
	CALL	NZ,CHGCAP
	LD	H,B
	LD	L,C
	JP	(HL)
;-------------
;
CAPSTA:	DW	0
;
RETURN@:	JP	S_CAP
W_CAP:	DB	0
;
RETURN:	LD	DE,0704H	;ESC
L_RET:	CALL	KEYIN
	JP	Z,L_RET
;
	LD	HL,PSG_SV
	LD	BC,PSGIOW
	XOR	A
L_PSGLD:	OUT	(PSGIOA),A
	OUTI
	INC	A
	CP	13
	JP	NZ,L_PSGLD
;
	CALL	CHGCAP
	LD	SP,(SVW_SP)
;	IM	1
	JP	TRAP_MS
;
FC_TBL:	DW	0602H,CTRL	; CTRL
	DW	0501H,SAVE	;'S'
	DW	0402H,LOAD	;'L'
	DW	0301H,CURE	;'C'
	DW	0420H,PLAY	;'P'
	DW	0704H,RETURN	; ESC
	DW	0,L_FUNC	; END of DATA
;
CTRL:	LD	HL,FC_TBL@
	JP	L_FUNC_
FC_TBL@:	DW	0120H,QUIT	;'@'
	DW	0501H,SAVE@Q	;'S'
	DW	0402H,LOAD@	;'L'
	DW	0420H,PLAY_	;'P'
	DW	0710H,RESET	; STOP
	DW	0,L_FUNC	; END of DATA
;
;
SAVE@Q:	LD	A,V_PG2
	CALL	SAVE_
QUIT:	LD	A,V_PG1
	CALL	SAVE_
	LD	A,V_PG3
	CALL	LOAD_
	JP	RET_BAS	;COMMON
;
SAVE@:	LD	A,V_PG1
	JP	SAVE+2
SAVE:	LD	A,V_PG2
	LD	HL,RETURN@
	PUSH	HL
SAVE_:	INC	A
	LD	(SAVE__+1),A
	DEC	A
	LD	HL,M_ADR
	LD	B,M_SIZE
	LD	C,20H+40H
	CALL	TF_VRM
;
	LD	D,0
L_SAVE:	LD	C,D
	LD	HL,BUF
	LD	B,1
	LD	A,0
	CALL	TF_RAM
	LD	A,40H
	OR	D
	LD	C,A
	LD	HL,BUF
	LD	B,1
SAVE__:	LD	A,0	;vram page +1
	CALL	TF_VRM
;
	INC	D
	LD	A,40H
	SUB	A,D
	JP	NZ,L_SAVE
;
	RET
;
LOAD@:	LD	HL,(STACKA)
	LD	SP,HL
	LD	A,V_PG1
	JP	LOAD+2
LOAD:	LD	A,V_PG2
	LD	HL,RETURN@
	PUSH	HL
LOAD_:	LD	D,0
	LD	(LOAD__+1),A
	INC	A
	LD	(L_LOAD+1),A
L_LOAD:	LD	A,0	;vram page +1
	LD	HL,BUF
	LD	B,1
	LD	C,D
	CALL	TF_RAM
	LD	A,40H
	OR	D
	LD	C,A
	LD	HL,BUF
	LD	B,1
	LD	A,0
	CALL	TF_VRM
;
	INC	D
	LD	A,40H
	SUB	A,D
	JP	NZ,L_LOAD
;
	LD	HL,M_ADR
	LD	B,M_SIZE
	LD	C,20H
LOAD__:	LD	A,0	;vram page
	CALL	TF_RAM
;
	LD	A,(W_BANK+0)
	LD	(BANK1),A
	LD	A,(W_BANK+1)
	LD	(BANK2),A
	LD	A,(W_BANK+2)
	LD	(BANK3),A
	CALL	WR_CRG
	RET
;
;
RESET:	INC	A
	CALL	CHGCAP
;
	JP	40ABH
;
CURE:	LD	A,(LIFE1W)
	LD	B,A
	LD	A,(LIFE1W+1)
	SUB	B
	LD	(LIFEUP1),A
	LD	A,(LIFE2W)
	LD	B,A
	LD	A,(LIFE2W+1)
	SUB	B
	LD	(LIFEUP2),A
	LD	HL,ITEM_W
	LD	DE,999H
	LD	B,3
L_CURE:	LD	(HL),E
	INC	HL
	LD	(HL),D
	INC	HL
	DJNZ	L_CURE
	JP	RETURN
;
LIFE1W:	EQU	0E056H
LIFE2W:	EQU	0E052H
LIFEUP1:	EQU	0E061H
LIFEUP2:	EQU	0E05EH
ITEM_W:	EQU	0E046H
WR_AROW:	EQU	05F79H
WR_COIN:	EQU	05F84H
WR_KEYS:	EQU	05F8FH
WR_LIF1:	EQU	0618BH
WR_LIF2:	EQU	061B2H
;
;
PLAY_:	LD	HL,0C007H
	BIT	7,(HL)
	JP	Z,J_PLAY_
	XOR	A
	CALL	4E34H
J_PLAY_:	LD	A,(HL)
	CALL	4E34H
	DI
	JP	RETURN
;
;
;D:ÏÄØ¸½ No. /E:FILTER
KEYIN:	IN	A,(KEYIO2)
	AND	0F0H
	OR	D
	OUT	(KEYIO2),A
	IN	A,(KEYIO1)
	AND	E
	RET
;-------------
;KEY BUFFER READ
;
;CURRENT
;D:ÏÄØ¸½ No. /E:FILTER
KEYCUR:	PUSH	HL
	LD	HL,KEYBMAT
	LD	A,D
	ADD	A,L
	LD	L,A
	LD	A,H
	ADC	A,0
	LD	H,A
	LD	A,(HL)
	AND	E
	POP	HL
	RET
;
;TRRIGER (¾ÞÝ¶² µ¼Ã²Å² ·° ÉÐ)
;D:ÏÄØ¸½ No. /E:FILTER
KEYTRG:	PUSH	HL
	LD	HL,KEYBTRG
	LD	A,D
	ADD	A,L
	LD	L,A
	LD	A,H
	ADC	A,0
	LD	H,A
	LD	A,(HL)
	AND	E
	POP	HL
	RET
;
;·°Æ­³Ø®¸ º³¼Ý
; µ¼À¼­Ý¶Ý ÊÝÃ²
; USE: AF,BC,DE,HL
KEYUPD:	LD	HL,KEYBMAT+10
	LD	DE,KEYBTRG+10
	LD	B,10	; 10+1=11º
KEYUPD_L:	;
	LD	A,(HL)	;A=¾ÞÝ¶² µ¼Ã²À ·°
	CPL	; BITÊÝÃÝ
	LD	C,A	;C=¾ÞÝ¶²µ¼Ã²Å²·° É Ï½¸
;KEYIN
	IN	A,(KEYIO2)
	AND	0F0H
	OR	B
	OUT	(KEYIO2),A
	IN	A,(KEYIO1)
;
	LD	(HL),A	;A=CURRENT:²Ï‚Ì¼Þ®³À²
	OR	C	;1=key off
	LD	(DE),A	;A=PRESS EDGE:¾ÞÝ¶² µ¼Ã²À ·° ²¶Þ² É ¼Þ®³À²
	LD	A,B
	OR	A
	RET	Z	;B=10~0
	DEC	HL
	DEC	DE
	DEC	B
	JP	KEYUPD_L
;
;	·°ÊÞ¯Ì§
KEYBMAT:	DS	10	; CURRENT:²Ï‚Ì¼Þ®³À²
KEYBTRG:	DS	10	; PRESS EDGE:¾ÞÝ¶² µ¼Ã²À ·° ²¶Þ² É ¼Þ®³À²
;
;
;RESTORE VDP (READ BASIC WORKAREA)
WR_CRG:	XOR	A
	OUT	(99H),A
	LD	A,17+80H
	OUT	(99H),A
;
	LD	HL,REG_0SV
	LD	BC,089BH
	OTIR
	RET
;
; VRAM R/W SET
; A:VRAM bit14-16
; C:VRAM bit 8-13 + 40H( WHEN WRITE )
; (VRAM ADDRES=A*4000H+(C AND 3FH)*100H)
VSET:	OUT	(99H),A
	LD	A,14+80H
	OUT	(99H),A
	XOR	A
	OUT	(99H),A
	LD	A,C
	OUT	(99H),A
	RET
;
; RAM -> VRAM
; (VRAM ADDRES=A*4000H+(C AND 3FH)*100H)
;
; HL:RAM ADDRESS
; A:VRAM bit14-16
; C:VRAM bit 8-13 + 40H( WHEN WRITE )
; B:SIZE/100H
;
;TF_VRM:	 LD    C,40H
TF_VRM:	CALL	VSET
	LD	A,B
	LD	BC,0098H
L_FVRM:	OTIR
	DEC	A
	JP	NZ,L_FVRM
	RET
;
; VRAM -> RAM
; (VRAM ADDRES=A*4000H+(C AND 3FH)*100H)
;
; HL:RAM ADDRESS
; A:VRAM bit14-16
; C:VRAM bit 8-13 + 40H( WHEN WRITE )
; B:SIZE/100H
;
;TF_RAM:	 LD    C,0
;USE: A,BC,IX
TF_RAM:	;POP	IX	;IX=RET ADDRESS
	CALL	VSET
	LD	A,B
	LD	BC,0098H
L_FRAM:	INIR
	DEC	A
	JP	NZ,L_FRAM
	;JP	(IX)	;RET IX=RET ADDRESS
	RET
;
;
; WAIT VSYNC
; USE:AF,BC
; RET:A 1=ok/0=wait
; *** NEED DI !
WTVSYNC:	;
;	DI
	LD	C,99H
	LD	A,2	;S#2
	OUT	(C),A
	LD	A,15+80H
	OUT	(C),A	;SET S#2 READ
;
	LD	A,(WTVSYNCS)
	LD	B,A	;NEXT WTVSYNCS
	CP	1
	JR	Z,WTVSYN1
	JR	NC,WTVSYN2
;
WTVSYN0:	; NO VBI = 0
	IN	A,(C)
	AND	40H	;VERTIACL BLANKING INTERVAL
	JR	Z,WTVSYNCE
	INC	B	;B=1
	JR	WTVSYNCE
WTVSYN1:	; VBI EDGE = 1
	INC	B	;B=2
WTVSYN2:	; IN VBI = 2
	IN	A,(C)
	AND	40H	;VERTIACL BLANKING INTERVAL
	JR	NZ,WTVSYNCE
	LD	B,0
WTVSYNCE:	;
	XOR	A	;S#0
	OUT	(C),A
	LD	A,15+80H
	OUT	(C),A	;SET S#0 READ (DEFAULT)
;	EI
	LD	A,B
	LD	(WTVSYNCS),A
	RET
WTVSYNCS:	;
	DB	0
;
SVW_SP:	EQU	SAVSP
