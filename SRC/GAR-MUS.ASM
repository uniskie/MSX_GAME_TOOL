;***************************
; Maze of GALIOUS SOUND-BOX
;***************************
; A=USR(0)
	FNAME	"GAR-MUS.BOF"	;*BINHEAD*
	DB	0FEH	;*BINHEAD*
	DW	ORGADR	;*BINHEAD*
	DW	ENDFL-1	;*BINHEAD*
	DW	ORGADR ;*BINHEAD*;
;
ENASLT:	EQU	0024H
DACINT:	EQU	0F7F8H
H_KEYI:	EQU	0FD9AH
H_TIMI:	EQU	0FD9FH
RAMPG0:	EQU	0F341H
EXPTBL:	EQU	0FCC1H
;	
ORGADR:	EQU	0C000H
	ORG	ORGADR
;
ORGFL:	;
	JP	GALIOUS_
MUS_WORK:	;
	DB	88H,01H,0,0
SVW_SP:	DW	0
;
GALIOUS_:	;
	DI
;	IM	1
	LD	(SVW_SP),SP
	LD	SP,0D300H
;
	LD	HL,H_KEYI
	LD	DE,SV_HOOK
	LD	BC,5
	LDIR
;
	LD	A,4
	LD	C,40H
	LD	HL,8000H
	LD	B,40H
	CALL	TF_VRM	;COMMON
	LD	A,6
	LD	C,40H+20H
	LD	HL,0E000H
	LD	B,1FH
	CALL	TF_VRM	;COMMON
;
	LD	A,(RAMPG0+1)
	LD	H,40H
	CALL	ENASLT
;
	LD	A,5
	LD	C,0
	LD	HL,4000H
	LD	B,60H
	CALL	TF_RAM	;COMMON
;
	LD	HL,PLAY_TR
	LD	(H_KEYI+1),HL
	LD	A,0C3H
	LD	(H_KEYI),A
;
	CALL	PLAY
;
;
RET_BAS:	;
	LD	A,4
	LD	C,0
	LD	HL,8000H
	LD	B,40H
	CALL	TF_RAM	;COMMON
	LD	A,6
	LD	C,20H
	LD	HL,0E000H
	LD	B,1FH
	CALL	TF_RAM	;COMMON
;
	LD	A,(EXPTBL)
	LD	H,40H
	CALL	ENASLT
;
	LD	HL,SV_HOOK
	LD	DE,H_KEYI
	LD	BC,5
	LDIR
;
	LD	SP,(SVW_SP)
;	IM	0
	EI
;
	RET
;
;
SV_HOOK:	;
	DS	5
;
;
;	WRTVRM	EQU	004DH	;HL=VRAM ADDRESS(14bit),
;	SETWRT	EQU	0053H	;HL=VRAM ADDRESS(14bit), A=DATA
;	FILVRM:	EQU	0056H	;HL=VRAM ADDRESS(14bit), BC=SIZE, A=DATA
NSTWRT:	EQU	0171H
NWRVRM:	EQU	0177H
BIGFIL:	EQU	016BH
;
PSG:	EQU	0A0H
;
;
;--- GALIOUS ROM RUTINE
SETMUSIC:	EQU	4E6BH
;
PLAY:	LD	HL,0E000H
	LD	DE,0E001H
	LD	BC,010EFH
	LD	(HL),0
	LDIR
;
	XOR	A
	CALL	SETMUSIC
	LD	A,87H
	CALL	SETMUSIC
;
	LD	A,2
	LD	(MUS_WORK+2),A
	LD	A,87H
	LD	(MUS_WORK+0),A
	EI
;
	CALL	WR_MES@
	DB	'\0000*CEÅü *C3ñÇèÈì`ê‡ÅE-ÉKÉäÉEÉXÇÃñ¿ã{-*CE Åü'	;*DM*
	DB	'\1000*CA     ÅsÇrÇnÇtÇmÇcÅEÇaÇnÇwÅt     '	;*DM*
	DB	'                                '	;*DM*
	DB	'\4058å¯â âπ No.01'	;*DM*
	DB	'\5858ÇaÇfÇl No.87'	;*DM*
	DB	'\7058èIóπ'	;*DM*
	DB	'\9070*0EHOME KEY:FADE OUT '	;*DM*
	DB	'\A000*C3                                '	;*DM*
	DB	'                                '	;*DM*
	DB	'                                '	;*DM*
	DB	'\A04C*C3-BGM INDEX-|'	;*DM*
;
	CALL	WR_PNO
	CALL	WR_PNUM1
	CALL	WR_PNUM2
	CALL	WR_TITL
;
	JP	L_PLAY
;
;
; EXIT
PLAY_R:	DI
	LD	A,7
	OUT	(PSG),A
	LD	A,0BFH
	OUT	(PSG+1),A
;
	LD	DE,0801H	;'SPC'
	CALL	KEYIN
	JP	Z,$-6
	RET
;
L_PLAY:	;DI
	XOR	A
	LD	(WAITCT),A
	EI
L_PLAYW:	;
	CALL	KEYUPD	;KEY MATRIX UPDATE
;
	LD	DE,8FFH
	CALL	KEYIN
	CPL
	OR	A
	JP	Z,L_PLAYJ
	LD	A,(WAITCT)	;WAIT INTERVAL
	CP	20		;20/60 SEC
	JR	C,L_PLAYW
;
L_PLAYJ:	;
	EI
L_PLAY0:	;
	LD	DE,8FFH
	CALL	KEYIN
;	CALL	KEYTRG
	LD	HL,P_JUMPT
	LD	B,8
L_PL0:	RLA
	JP	C,J_PL0
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	EX	DE,HL
	JP	(HL)
J_PL0:	INC	HL
	INC	HL
	DJNZ	L_PL0
;
	CALL	KEYUPD	;KEY MATRIX UPDATE
;
	JP	L_PLAY0
P_JUMPT:	;
	DW	P_RIGH,P_UP,P_DOWN,P_LEFT
	DW	P_DEL,P_INS,P_HOME,P_SPAC
;
P_LCH:	POP	DE
	LD	A,(MUS_WORK+2)
	CP	2
	JP	Z,L_PLAY
	LD	HL,MUS_WORK+1
	PUSH	DE
	OR	A
	RET	Z
	DEC	HL
	RET
;
P_INS:	LD	C,4
	JP	$+5
P_RIGH:	LD	C,1
	CALL	P_LCH
	OR	A
	JP	NZ,P_ADD2
P_ADD:	LD	A,(HL)
	ADD	A,C
	CP	31H
	JP	C,$+5
	SUB	30H
	JP	P_SUBE
P_ADD2:	LD	A,(HL)
	ADD	A,C
	AND	255
	OR	128
P_ADDE:	LD	(HL),A
	CALL	WR_PNUM2
	CALL	WR_TITL
	JP	L_PLAY
;
P_DEL:	LD	C,4
	JP	$+5
P_LEFT:	LD	C,1
	CALL	P_LCH
	OR	A
	JP	NZ,P_SUB2
P_SUB:	LD	A,(HL)
	SUB	C
	JP	C,$+6
	JP	NZ,$+5
	ADD	A,30H
P_SUBE:	LD	(HL),A
	CALL	WR_PNUM1
	JP	L_PLAY
P_SUB2:	LD	A,(HL)
	SUB	C
	AND	255
	OR	128
	JP	P_ADDE
;
P_DOWN:	LD	A,(MUS_WORK+2)
	DEC	A
	CP	255
	JP	NZ,L_PUD
	LD	A,2
	JP	L_PUD
;
P_UP:	LD	A,(MUS_WORK+2)
	INC	A
	CP	3
	JP	NZ,L_PUD
	XOR	A
L_PUD:	LD	(MUS_WORK+2),A
	CALL	WR_PNO
	JP	L_PLAY
;
;
P_HOME:	XOR	A
	LD	(0E1C1H),A
	INC	A
	LD	(0E1C0H),A
	JP	L_PLAY
;
P_SPAC:	LD	A,(MUS_WORK+2)
	CP	2
	JP	Z,PLAY_R
	CALL	P_LCH
	LD	C,(HL)
	JP	Z,J_PL3
	LD	A,C
	CP	0FBH
	JP	NC,J_PL3
	XOR	A
	CALL	SETMUSIC
J_PL3:	LD	A,C
	CALL	SETMUSIC
	LD	DE,0801H
	CALL	KEYIN
	JP	Z,$-6	; ≈Ωœ√ﬁœ¬
	JP	L_PLAY
;
	INCLUDE	"GMUS-SUB.ASM" ;*FILE*
	INCLUDE	"EX-BIOS.ASM" ;*FILE*
	INCLUDE	"KJ-VDP.ASM" ;*FILE*
	INCLUDE	"GAR-TL-K.ASM" ;*FILE*
;
ENDFL:	;
