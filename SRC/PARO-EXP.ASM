;*********************************
;     TIMER & KEY  INTERRUPT
; RAM C000-FEFF > VRAM 8000-BEFF
;*********************************
;       NAME  PARO-EXP
;
PSGIOA:	EQU	0A0H
PSGIOW:	EQU	0A1H
PSGIOR:	EQU	0A2H
PSG_SV:	EQU	0FB8BH+19
;
HIMEM:	EQU	0FC4AH	;BASIC FREE AREA
; BASIC COMMAND LINE BUFFER
BUF:	EQU	0F55EH+1	;0F55EH,258
;
;BLDCHK:	EQU	0F377H
;STACK:	EQU	BLDCHK+1	;DISK WORK AREA HEAD
SCCWAV:	EQU	0FB41H
SAVSP:	EQU	SCCWAV+80H
CHGCAP:	EQU	00132H
;
V_PG1:	EQU	2
V_PG2:	EQU	4
V_PG3:	EQU	6
M_SIZE:	EQU	1CH
M_ADR:	EQU	0E000H
;
;
TRAP_MS:	EQU	402EH	;PARODIUS
;
;STACKA:	DW	M_ADR-2	; MY STACK ADDRESS
STACKA:	DW	0D300H-2	; MY STACK ADDRESS
;
EX_TRAP:	;
	DI
;	IM	0
	IN	A,(KEYIO+1)
	AND	0F0H
	OR	7
	OUT	(KEYIO+1),A
	IN	A,(KEYIO)
	AND	4	;ESC
	JP	NZ,TRAP_MS
;
	LD	(SVW_SP),SP
	LD	HL,(STACKA)
	LD	SP,HL
;
	LD	HL,PSG_SV
	LD	BC,PSGIOR
	XOR	A
L_PSGSV:	;
	OUT	(PSGIOA),A
	INI
	INC	A
	CP	13
	JP	NZ,L_PSGSV
;
;	safty psg#7 jOy I/O mode
	LD	A,(PSG_SV+7)
	AND	3FH
	OR	80H
	LD	(PSG_SV+7),A	; 10XXXXXX
;
	LD	A,7
	OUT	(PSGIOA),A
	LD	A,0BFH
	OUT	(PSGIOW),A
;
	LD	A,3FH
	LD	(BANK2),A
	LD	HL,9800H
	LD	DE,SCCWAV
	LD	BC,80H
	LDIR
	XOR	A
	LD	(988FH),A
;
	XOR	A
	CALL	CHGCAP
;
	LD	DE,0704H	;ESC
L_ESC:	CALL	KEYIN
	JP	Z,L_ESC
S_CAP:	LD	DE,0
L_FUNC:	LD	HL,FC_TBL
L_FUNC_:	;
;MSX1–¢‘Î‰ž‚È‚Ì‚ÅvsyncŠÄŽ‹‘Ò‚¿‚Í‚µ‚È‚¢
;	CALL	WTVSYNC
;	CP	1
;	JR	NZ,J_CAP_
;	INC	DE
;	LD	A,E
;	AND	32	;32 FRAME
;
	INC	DE
	LD	A,D
	AND	20H	;2000H
;
	LD	A,1
	JR	NZ,J_CAP
	XOR	A
J_CAP:	LD	BC,(CAPSTA)
	CP	C
	LD	(CAPSTA),A
	CALL	NZ,CHGCAP
J_CAP_:	;
	PUSH	DE
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	LD	A,E
	OR	D
	EX	AF,AF'	;AF':DE=0 ->LOOP
	INC	HL
	LD	C,(HL)
	INC	HL
	LD	B,(HL)
	INC	HL
	CALL	KEYIN
	POP	DE
	JP	NZ,L_FUNC_
	EX	AF,AF'	;AF':DE=0 ->LOOP
	LD	A,0
	CALL	NZ,CHGCAP
	LD	H,B
	LD	L,C
	JP	(HL)
;--------------
;
CAPSTA:	DW	0
;
RETURN@:	;
	JP	S_CAP
W_CAP:	DB	0
;
RETURN:	LD	DE,0704H	;ESC
L_RET:	CALL	KEYIN
	JP	Z,L_RET
;
	LD	HL,PSG_SV
	LD	BC,PSGIOW
	XOR	A
L_PSGLD:	;
	OUT	(PSGIOA),A
	OUTI
	INC	A
	CP	13
	JP	NZ,L_PSGLD
;
	LD	A,3FH
	LD	(BANK2),A
	LD	HL,SCCWAV
	LD	DE,9800H
	LD	BC,80H
	LDIR
	LD	A,(SCCMIX)
	LD	(988FH),A
	LD	A,(W_BANK+1)
	LD	(BANK2),A
;
	CALL	CHGCAP
	LD	SP,(SVW_SP)
;	IM	1
	JP	TRAP_MS
;
;
;
FC_TBL:	DW	0602H,CTRL	; CTRL
	DW	0501H,SAVE	;'S'
	DW	0402H,LOAD	;'L'
	DW	0301H,CURE	;'C'
	DW	0420H,PLAY	;'P'
	DW	0704H,RETURN	; ESC
	DW	0,L_FUNC	; END of DATA
;
CTRL:	LD	HL,FC_TBL@
	JP	L_FUNC_
FC_TBL@:	;
	DW	0120H,QUIT	;'@'
	DW	0501H,SAVE@Q	;'S'
	DW	0402H,LOAD@	;'L'
	DW	0710H,RESET	; STOP
	DW	0,L_FUNC	; END of DATA
;
;
SAVE@Q:	LD	A,V_PG2
	CALL	SAVE_
QUIT:	LD	A,V_PG1
	CALL	SAVE_
	LD	A,V_PG3
	CALL	LOAD_
	JP	RET_BAS	;COMMON
;
SAVE@:	LD	A,V_PG1
	JP	SAVE+2
SAVE:	LD	A,V_PG2
	LD	HL,RETURN@
	PUSH	HL
SAVE_:	INC	A
	LD	(SAVE__+1),A
	DEC	A
	LD	HL,M_ADR
	LD	B,M_SIZE
	LD	C,20H+40H
	CALL	TF_VRM
;
	LD	D,0
L_SAVE:	LD	C,D
	LD	HL,BUF
	LD	B,1
	LD	A,0
	CALL	TF_RAM
	LD	A,40H
	OR	D
	LD	C,A
	LD	HL,BUF
	LD	B,1
SAVE__:	LD	A,0	;vram page +1
	CALL	TF_VRM
;
	INC	D
	LD	A,40H
	SUB	A,D
	JP	NZ,L_SAVE
;
	RET
;
LOAD@:	LD	HL,(STACKA)
	LD	SP,HL
	LD	A,V_PG1
	JP	LOAD+2
LOAD:	LD	A,V_PG2
	LD	HL,RETURN@
	PUSH	HL
LOAD_:	LD	D,0
	LD	(LOAD__+1),A
	INC	A
	LD	(L_LOAD+1),A
L_LOAD:	LD	A,0	;vram page +1
	LD	HL,BUF
	LD	B,1
	LD	C,D
	CALL	TF_RAM
	LD	A,40H
	OR	D
	LD	C,A
	LD	HL,BUF
	LD	B,1
	LD	A,0
	CALL	TF_VRM
;
	INC	D
	LD	A,40H
	SUB	A,D
	JP	NZ,L_LOAD
;
	LD	HL,M_ADR
	LD	B,M_SIZE
	LD	C,20H
LOAD__:	LD	A,0	;vram page
	CALL	TF_RAM
;
	LD	A,(W_BANK+0)
	LD	(BANK1),A
	LD	A,(W_BANK+1)
	LD	(BANK2),A
	LD	A,(W_BANK+2)
	LD	(BANK3),A
	CALL	WR_CRG
	RET
;
;
RESET:	INC	A
	CALL	CHGCAP
;
	JP	PAR_ADR
;
CURE:	;
	JP	RETURN
;
SVW_SP:	EQU	SAVSP
