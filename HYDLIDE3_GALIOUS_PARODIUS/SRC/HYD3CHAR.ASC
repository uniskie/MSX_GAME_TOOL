10 CLEAR1800,&HBEFF:DEFINT A-Z:PM=30:IM=69
20 KEYOFF:SCREEN0:WIDTH40
30 A=&H800+254*8:FORI=2TO3:VPOKE A+I*2,&HAA:VPOKE A+I*2+1,&H55:NEXT
40 PRINT"set usr5";:A$="FE02C02AF8F706FFAF862310FCC64A6F260022F8F7C9":FOR I=1TO LEN(A$)-1 STEP 2:POKE &HBF00+I\2,VAL("&H"+MID$(A$,I,2)):PRINT".";:NEXT:DEFUSR5=&HBF00:PRINT
50 AB=&HC000:DIM IT$(IM),PR$(PM),PL(3),PA(3),P$(2),OD(4)
60 ON STOP GOSUB 1220:STOPON:ON ERROR GOTO 2260
70 CL$="FighterRobber Priest Monk   WarriorThief  Bishop Bishop Hero   "
80 WP$="45000102030405060708090a0b0c0d"'WEAPON
90 AR$="450e0f1011121342"'ARMOR
100 SH$="451415161718"'SHIELD
110 HE$="45191a1b1c"'HELMET
120 ' PARAM
130 MV=20*2:MV$=STRING$(MV*2,255):PRINT"param";
140 FOR I=0 TO PM:PRINT".";
150  READ PR$(I)
160  PR$=PR$(I):X=VAL(MID$(PR$(I),1,2)):Y=VAL(MID$(PR$(I),3,2))
170  J=Y*2+X\20:MID$(MV$,J*2+1,2)=RIGHT$("0"+MID$(STR$(I+1),2),2)
180 NEXT:PRINT
190 ' ITEM
200 PRINT"item";:FOR I=0 TO IM-1:PRINT".";
210  READ IT$(I)
220  IT$(I)=IT$(I)+SPACE$(16-LEN(IT$(I)))
230 NEXT:PRINT
240 IT$(I)="   - empty -    "
250 ' CHECK
260 FOR I=0TO3
270  AD=AB+I*256:A=USR5(AD)
280  OD(I)=-((A=PEEK(AD+255))AND(0<>PEEK(AD)))
290 NEXT
300 '
310 ' START EDIT
320 AD=AB+PI*256:OD=OD(PI)
330 X=0:Y=0:BN=0
340 ' SCREEN
350 CLS:LOCATE0,23,0:PRINT"* HYDLIDE III CHARACTER EDITOR *";:LOCATE0,0
360 FOR I=0 TO MV
370  N=VAL(MID$(MV$,I+I+1,2))-1
380  IF N>-1 THEN GOSUB 1300
390  I=I+(1-OD)*MV
400 NEXT
410 IF OD=O THEN PRINT" (NO DATA)"
420 ' LOOP
430 BN=(BN+X+Y*2)*OD
440 IF BN>=MV THEN BN=BN-MV
450 IF BN<0 THEN BN=MV+BN
460 PN=VAL(MID$(MV$,BN*2+1,2))
470 IF PN=0 THEN 420
480 A$=PR$(PN-1)
490 LOCATE VAL(LEFT$(A$,2)),VAL(MID$(A$,3,2)),1:TIME=0:BK=1
500 IF TIME>29 THEN BK=1-BK:LOCATE,,BK:TIME=0
510 K=ASC(INKEY$+CHR$(0)):IF K=0 THEN 500
520 X=(K=29)-(K=28):Y=(K=30)-(K=31)
530 IF K<>13 THEN 420
540 N=PN-1:GOSUB 1580:GOSUB 570 'EDIT
550 IF PL>13 THEN 340'REDRAW
560 GOSUB 1300:GOTO 420
570 ' EDIT PARAM
580 LOCATE 0,20,0':PRINT"No.";PN;
590 PRINT PR$
600 PL=PL(0):DO=AD+PA(0)
610 ON PL GOSUB 640,650,660,670,680,690,700,710,720,730,740,750,760,770,1200
620 LOCATE 0,20:PRINT SPACE$(39):RETURN
630 ' INPUT FUNC
640 GOSUB 1430:GOTO 1670'NAME
650 MX#=12:GOSUB 1400:GOTO 1780
660 MX#=255:GOSUB 1400:GOTO 1780
670 POKE DO,1-PEEK(DO):RETURN'FLAG
680 MX#=65535!:GOSUB 1410:GOTO 1780
690 GOTO 2140'CLASS
700 RETURN'G
710 MX#=16777215#:GOSUB 1420:GOTO 1780
720 EN$=WP$:GOTO 2050'WEAPON
730 EN$=AR$:GOTO 2050'ARMOR
740 EN$=SH$:GOTO 2050'SHIELD
750 EN$=HE$:GOTO 2050'HELMET
760 GOTO 1960'ITEM
770 ' DISPLAY ITEMS
780 CLS:PRINT"---- Item Display ";STRING$(22,"-")
790 EM=19:VM=EM+4:VN=0:VX=0:VY=0
800 FOR VN=0 TO VM:GOSUB 1040:NEXT:VN=0
810 PRINT STRING$(40,"-");
820 PRINT"* ESC=END, RET=ITEM, C=COUNT, SPC=USE";
830 '
840 VN=VN+VX+VY*2
850 IF VN>VM THEN VN=VN-VM-1
860 IF VN<0 THEN VN=VM+VN+1
870 IF VN=VM THEN 840'SKIP
880 IF VN>EM THEN VA=&H83+VN-EM-1 ELSE VA=&H47+VN*3
890 LOCATE (VN AND 1)*20,(VN\2)+1,1:TIME=0:BK=1
900 IF TIME>29 THEN BK=1-BK:LOCATE,,BK:TIME=0
910 K=ASC(INKEY$+CHR$(0)):IF K=0 THEN 900
920 VX=(K=29)-(K=28):VY=(K=30)-(K=31)
930 IF K=ASC("C")OR K=ASC("c")THEN 970'C
940 IF K=13 THEN IF VN>EM THEN 970 ELSE 990 'RETURN
950 IF K=32 THEN IF VN>EM THEN 970 ELSE 980'SPACE
960 IF K=27 THEN PL=14:RETURN ELSE 830
970 P$(0)="Count":PA(0)=VA-(VN<=EM):PL(0)=3:PL(1)=0:GOTO 1000
980 P$(0)="Using":PA(0)=VA+2:PL(0)=4:PL(1)=0:GOTO 1000
990 P$(0)="Item":PA(0)=VA+0:PL(0)=13:PL(1)=0:GOTO 1000
1000 DO=AD+PA(0):GOSUB 1010:PR$=A$+" - "+P$(0):GOSUB 570:LOCATE,,0:GOSUB1040:GOTO 830
1010 'ITEM NAME (VN)
1020 IF VN>EM THEN A$=IT$(47+VN-EM-1):RETURN
1030 HI=PEEK(AD+VA):IF HI<IM THEN A$=IT$(HI):RETURN ELSE A$=IT$(IM):RETURN
1040 'PRINT ITEM(1)
1050 IF VN>=VM THEN PRINT SPC(20);:RETURN ELSE IF VN>EM THEN 1130
1060  DO=AD+&H47+VN*3
1070  HI=PEEK(DO)
1080  IC$=MID$(STR$(PEEK(DO+1)),2)
1090  IF HI<IM THEN A$=IT$(HI) ELSE A$=IT$(IM)
1100  LOCATE (VN AND 1)*20,(VN\2)+1
1110  PRINT RIGHT$("  "+IC$,3);MID$(":+",1+PEEK(DO+2),1);A$;
1120 RETURN
1130 ' COIN
1140  A=VN-EM-1:DO=AD+&H83+A
1150  IC$=MID$(STR$(PEEK(DO)),2)
1160  A$=IT$(47+A)
1170  LOCATE (VN AND 1)*20,(VN\2)+1
1180  PRINT RIGHT$("  "+IC$,3);":";A$;
1190 RETURN
1200 ' CHANGE CHARA
1210 PI=(PI+1)AND3:GOTO310 'REDRAW ALL
1220 ' END
1230 LOCATE,,0:BEEP
1240 FOR I=0TO3
1250  IF OD(I) THEN AD=AB+I*256:A=USR5(AD):POKE AD+255,A
1260 NEXT
1270 LOCATE0,0,0:PRINT"RUN"+CHR$(34)+"HYDLIDE3.BAS"
1280 ON ERROR GOTO 0:END
1290 '
1300 ' RPINT PARAM
1310 A$=PR$(N):GOSUB 1580:P=0
1320 LOCATE CX,CY
1330 PRINT PR$;
1340 PL=PL(P):P$=P$(P)
1350 DO=AD+PA(P)
1360 ON PL GOSUB 1430,1400,1400,1510,1410,1500,1530,1420,1450,1460,1470,1480,1440,1520,1540
1370 IF PL=0 THEN DO=AD+PA(0):RETURN
1380 PRINT P$;PP$;
1390 P=P+1:GOTO 1340
1400 A#=PEEK(DO):GOTO 1560
1410 A#=PEEK(DO)+PEEK(DO+1)*256:GOTO 1560
1420 A#=PEEK(DO)+PEEK(DO+1)*256+PEEK(DO+2)*65536!:GOTO 1560
1430 PP$="":FOR K=0 TO 15:A=PEEK(DO+K):PP$=PP$+CHR$(A-(A=0)*32):NEXT:RETURN
1440 A=PEEK(DO):IF(A<IM)THEN PP$=IT$(A):RETURN ELSE PP$=IT$(IM):RETURN'ITEM
1450 A=PEEK(DO):E=A- 1:IF(A>0)AND(E<IM)THEN PP$=IT$(E):RETURN ELSE PP$=IT$(IM):RETURN'WEAPON
1460 A=PEEK(DO):E=A+13:IF(A>0)AND(E<IM)THEN PP$=IT$(E):RETURN ELSE PP$=IT$(IM):RETURN'ARMOR
1470 A=PEEK(DO):E=A+19:IF(A>0)AND(E<IM)THEN PP$=IT$(E):RETURN ELSE PP$=IT$(IM):RETURN'SHIELD
1480 A=PEEK(DO):E=A+24:IF(A>0)AND(E<IM)THEN PP$=IT$(E):RETURN ELSE PP$=IT$(IM):RETURN'HELMET
1490 MX=LEN(EN$)\2-1:IF(A>0)AND(A<=MX)THEN PP$=IT$(VAL("&H"+MID$(EN$,2*A+1,2))):RETURN ELSE PP$=IT$(IM):RETURN
1500 PP$=MID$(CL$,PEEK(DO)*7+1,7):RETURN
1510 PP$=MID$("OFFON ",PEEK(DO)*3+1,3):RETURN
1520 PL=0:RETURN
1530 PRINT"g":PL=0:RETURN
1540 PP$=HEX$(PI):RETURN
1550 '
1560 PP$=STR$(A#):PP$=RIGHT$(PP$,LEN(PP$)-1)
1570 PP$=RIGHT$("0000000"+PP$,PL):RETURN
1580 'GET PARAMETER
1590 CX=VAL(LEFT$(A$,2)):CY=VAL(MID$(A$,3,2))
1600 D=INSTR(A$,":"):PR$=MID$(A$,5,D-5)
1610 FOR K=0 TO 2
1620  PL(K)=VAL("&h"+MID$(A$,D+K*4+1,1))
1630  PA(K)=VAL("&h"+MID$(A$,D+K*4+2,2))
1640  P$(K)=MID$(A$,D+K*4,1)
1650 NEXT:RETURN
1660 '
1670 ' INPUT NAME
1680 NL=15:PP$="":FOR K=0TO NL:PP$=PP$+CHR$(PEEK(DO+K)):NEXT
1690 IX=0:IY=PEEK(&HF3DC)-1:LOCATE0:PRINT"Input Name:";:GOSUB 1770
1700 LOCATE11+IX,,1:A=ASC(INPUT$(1))
1710 IF(A>31)AND(A<>127)THEN MID$(PP$,IX+1,1)=CHR$(A):PRINT CHR$(A);:A=28
1720 IF A=127 THEN PP$=LEFT$(PP$,IX)+MID$(PP$,IX+2)+CHR$(0):LOCATE11,,0:GOSUB 1770
1730 IX=IX+(A=29 AND IX>0)-(A=28 AND IX<NL)
1740 IF A<>13 THEN 1700
1750 PP$=LEFT$(PP$+STRING$(16,0),16):FORI=OTO15:POKE DO+I,ASC(MID$(PP$,I+1,1)):NEXT
1760 LOCATE0,IY:GOTO 2240'END
1770 FORI=0TOPL:A=ASC(MID$(PP$,I+1,1)):PRINT CHR$(-A*(A>31 AND A<>127)-254*(A<32 OR A=127));:NEXT:RETURN
1780 ' INPUT NUMERIC
1790 IX=0:IY=PEEK(&HF3DC)-1
1800 PRINT CHR$(13)+"Input Value:";PP$;" max:";MX#;
1810 LOCATE IX+12,,1:A=ASC(INPUT$(1))
1820 IF A>47 AND A<58 THEN MID$(PP$,IX+1,1)=CHR$(A):PRINT CHR$(A);:A=28
1830 IX=IX+(A=29 AND IX>0)-(A=28):IF IX=PL THEN IX=0
1840 IF A<>13 THEN 1810
1850 A#=VAL(PP$)
1860 IF A#>MX# THEN BEEP:GOTO 1810
1870 A2=INT(A#/65536#):A3#=A#-A2*65536#
1880 A1=INT(A3#/256#):A0=A3#-A1*256#
1890 FOR K=0 TO 1
1900  IF(K>0)AND(PL(K)<>5)THEN 1940 ' PL=5:CUR/MAX
1910  DO=AD+PA(K):POKE DO,A0
1920  IF MX#>256# THEN POKE DO+1,A1
1930  IF MX#>65535# THEN POKE DO+2,A2
1940 NEXT
1950 GOTO 2240'ENDI
1960 'SELECT ITEM
1970 MX=IM:EN=PEEK(DO):IF EN>IM THENEN=IM
1980 PRINT CHR$(13)+"ITEM(";RIGHT$(" "+MID$(STR$(EN),2),2);"):";IT$(EN);
1990 A=ASC(INPUT$(1))
2000 EN=EN+(A=30)+(A=29)-(A=31)-(A=28)
2010 IF EN<0 THEN EN=MX
2020 IF EN>MX THEN EN=0
2030 IF A<>13 THEN 1980
2040 IF EN=MX THEN EN=255:GOTO 2220 ELSE 2220'WRITE&ENDI
2050 'SELECT EQUIPMENT
2060 MX=LEN(EN$)\2-1:EN=PEEK(DO):EB=VAL("&h"+MID$(EN$,3,2)):IF EN>MX THEN EN=MX
2070 E=VAL("&h"+MID$(EN$,EN*2+1,2)):EI=-(E-EB+1)*(EN>0):PRINT CHR$(13)+"EQUIPMENT(";HEX$(EI);"):";IT$(E);
2080 A=ASC(INPUT$(1))
2090 EN=EN+(A=30)+(A=29)-(A=31)-(A=28)
2100 IF EN<0 THEN EN=MX
2110 IF EN>MX THEN EN=0
2120 IF A<>13 THEN 2070
2130 EN=EI:GOTO 2220'WRITE&ENDI
2140 'SELECT CLASS
2150 MX=LEN(CL$)\7-1:EN=PEEK(DO)
2160 PRINT CHR$(13)+"CLASS(";HEX$(EN);"):";MID$(CL$,EN*7+1,7);
2170 A=ASC(INPUT$(1))
2180 EN=EN+(A=30)+(A=19)-(A=31)-(A=28)
2190 IF EN<0 THEN EN=MX
2200 IF EN>MX THEN EN=0
2210 IF A<>13 THEN 2160
2220 ' WRITE BACK
2230 POKE DO,EN
2240 ' END INPUT
2250 LOCATE 0,,0:PRINT CHR$(13);SPC(39);:RETURN
2260 ' ERROR
2270 FOR I=0TO3
2280  IF OD(I) THEN AD=AB+I*256:A=USR5(AD):POKE AD+255,A
2290 NEXT
2300 ON ERROR GOTO 0:RESUME
2310 '
2320 'PARAM
2330 DATA"0000No.:f00/0"
2340 DATA"0001Name:100/0
2350 DATA"3001Savior:4fc/0
2360 DATA"0002Class  :613/0
2370 DATA"3002Fairy :43b/0
2380 DATA"0003Monster:43c/0
2390 DATA"0004Level  :312/0
2400 DATA"2504Magic Spell:224/0
2410 DATA"0006Life   :516/514/0
2420 DATA"0007Magic  :520/51e/0
2430 DATA"0008Experience:835/0
2440 DATA"2006Attack Point:325/326/0
2450 DATA"2007Armor Class :327/328/0
2460 DATA"2008Agility     :329/32a/0
2470 DATA"0009Strength   :31c/31d/0
2480 DATA"2009Dextality   :32c/0
2490 DATA"0010Mind Force :334/0
2500 DATA"2010Inteligence :32e/0
2510 DATA"0011Charm      :332/333/0
2520 DATA"2011Luck        :330/331/0
2530 DATA"0012Have Weight:53eg7
2540 DATA"2012Master Weight:540g7
2550 DATA"0013< Item Display >:e47/0
2560 DATA"2213Item Weight:542g7
2570 DATA"0015Weapon:993/0
2580 DATA"0016Armor :a94/0
2590 DATA"0017Shield:b95/0
2600 DATA"0018Helmet:c96/0
2610 DATA"2315Attack Range :397/0
2620 DATA"2316Shot Distance:398/0
2630 DATA"2317Bullet Shape :399/0
2640 'ITEM
2650 DATA"Dagger","Short Sword","Long Sword","Holy Sword"
2660 DATA"Silver Sword","Fire Sword","Laser Sword","sling"
2670 DATA"Spear","Little Bow","Great Bow","Club"
2680 DATA"Axe","Battle Axe","Leather Suit","Chain Suit"
2690 DATA"Steal Armor","Silver Armor","Magic Armor","Fairy Armor"
2700 DATA"Shield","Mind Shield","Steel Shield","Life Sield"
2710 DATA"Fairy Shield","Helmet","Yamatec","Star Helmet"
2720 DATA"Fairy Helmet","Heal","Antidote","Herb"
2730 DATA"+3 Heal","+5 Heal","Max Heal","Cup of Soma"
2740 DATA"Phony Medicine","Gold Exchanger","Cross of God","Stone of Fighter"
2750 DATA"Tool of Camp","Midnight eye","Amulet of Magic","Food"
2760 DATA"Lamp","Oil","Super Oil","$10-Coin"
2770 DATA"$100-Coin","$1000-Coin","VIP Card","Cream of NIBEA'N"
2780 DATA"Amulet of ATSUTA","Doll of YUMI","T&E Package","Odo-Later"
2790 DATA"Stone of Pickles","Sun-Oil","Fang of Dragon","JIM'S Scroll"
2800 DATA"ID-Card","Stone of Cloud","Bugle","Idol of VARARYS"
2810 DATA"Amulet of Exit","Space Compass","Space Suit","Amulet of DM"
2820 DATA"Holy water"
