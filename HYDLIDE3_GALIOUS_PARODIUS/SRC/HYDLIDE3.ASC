10 '* EXTENSION TOOL for *
20 '*  HYDLIDE III MSX1  *
30 '
40 CLEAR320,&H9FFF:DEFINTA-Z:AD=&HA000:EA=&HF975:WA=&HFB8B:CB=&HC000'main/int/work/charedit
50 '
60 BLOAD"HYDLIDE3.BIN":BLOAD"HYD3-EXP.BIN"
70 A=&HF3F8:POKE A,&HF5:POKE A+2,&HF0:POKE A+1,&HFB:POKE A+3,&HFB
80 FORI=0TO4:POKE&HFBF0+I,ASC(MID$(",run-",I+1,1))-32:NEXT'CLS+"RUN"+RET
90 CLS:DELETE 60-90
100 '
110 RC=1:DEFUSR9=&H156:ONSTOPGOSUB1150:STOPON
120 DEFUSR0=AD'Game
130 DEFUSR1=AD+3'Chk
140 DEFUSR2=AD+6'Save
150 DEFUSR3=AD+9'Load
160 DEFUSR4=AD+12'CopyPlayChar
170 DEFUSR5=AD+16'CalcCheckSum
180 DEFUSR6=AD+18'Evac/Ret.EditArea
190 AS=AD+26'SaveAvilable
200 AE=AD+27'EditAeaEvac'ed?
210 AH=AD+29'BankHash
220 MV=PEEK(&H2D)'msx1=0
230 '
240 COLOR 3,0,0:KEYOFF:SCREEN0:WIDTH40:GOSUB1190
250 Z$="WWWWWWWWWW":Z$=Z$+Z$+Z$+MID$(Z$,5)
270 CLS:LOCATE,,0
271 PRINT"X"+Z$+"Y";
272 PRINT"V  HYDLIDE III  - The Space Memories - V";
273 PRINT"V";SPC(38);"V";
274 PRINT"V  for MSX1/MSX2  (C)T&E SOFT 1987     V";
275 PRINT"V";SPC(38);"V";
276 PRINT"V";SPC(10);"QUICK SAVER/LOADER";SPC(10);"V";
277 PRINT"Z";Z$;"["
280 PRINT "      - INSERT CARTRIDGE in SLOT -      "
290 PRINT "PAUSE¼Ã ROM¦ »¼Ã¶× PAUSE¦ ¶²¼Þ® ¼Ã¸ÀÞ»²":PRINT
300 IF RC THEN VE=USR1(EA)-1:SV=PEEK(AS)-1:IF VE<0 THEN 300
310 RC=0:PRINT"  - Identified HYDLIDE III for MSX";HEX$(VE+1);" -  ";CHR$(10)
320 SOUND0,100:SOUND1,0:SOUND7,190:FORJ=0TO4:SOUND8,10-J*2:FORI=0TO4:SOUND0,30+I*28-J*4:TIME=0:FORK=0TO1:K=TIME:NEXT:NEXT:NEXT:SOUND8,0
330 '
340 PRINT"X";Z$;"Y";
350 PRINT"V[G] PLAY GAME     [M] FORCE CAMP NOW  V";
360 IF VE=SV THEN PRINT"V[C] CONTINUE GAME";SPC(21);"V"; ELSE PRINT"V";SPC(38);"V";
370 PRINT"V[R] STATE LOAD    [L] LOAD CHARACTERS V";
380 PRINT"V[W] STATE SAVE    [S] SAVE CHARACTERS V";
390 IF(VE=0)AND(MV>0)THEN PRINT"V[P] COLOR:MSX";HEX$(2-PT);SPC(4); ELSE PRINT"V";SPC(18);
400 PRINT "[E] CHARA DATA EDIT V";
410 PRINT"Z";Z$;"[";
420 PRINT SPC(2);"COMMAND? _";
430 A=USR9(0):C$="":LOCATE,,1
440 I$=INKEY$:A=USR1(0)-1:IF VE<>A THEN BEEP:RC=1:GOTO240
450 IF I$="" THEN 440 ELSE LOCATE,,0
460 I=ASC(I$):I$=CHR$(I+((I>&H60)AND(I<&H7B))*&H20)
470 IF I$="G"THEN C$="GAME"
480 IF I$="C"THEN C$="CONT"
490 IF I$="M"THEN C$="CAMP"
500 IF I$="L"THEN C$="LOAD"
510 IF I$="S"THEN C$="SAVE"
520 IF I$="R"THEN C$="S_LOAD"
530 IF I$="W"THEN C$="S_SAVE"
540 IF I$="E"THEN C$="C_EDIT"
550 IF VE=0 AND(I$="P"OR I$="p")THEN PT=-(PT=0):GOSUB1190:GOTO270
560 IF I$=CHR$(27) THEN RC=1:GOTO240
570 IF I$="?" THEN GOSUB1090:GOTO270'BKDFILE
580 IF C$="" THEN BEEP:GOTO430
590 PRINT C$;
600 SOUND0,100:SOUND1,0:SOUND7,190:FORJ=0TO7:SOUND8,10-J:FORI=0TO4:SOUND0,239-I*20-J:TIME=0:FORK=0TO1:K=TIME:NEXT:NEXT:NEXT:SOUND8,0
610 '
620 IF C$="CAMP"THEN GOSUB720
630 IF C$="LOAD"THEN GOSUB1340
640 IF C$="SAVE"THEN GOSUB1430
650 IF C$="S_LOAD"THEN GOSUB1500
660 IF C$="S_SAVE"THEN GOSUB1640
670 IF C$="C_EDIT"THEN CLEAR200,&HBEFF:RUN"HYD3CHAR.BAS"
680 IF C$="CONT"THEN GOSUB840
690 IF C$="GAME"THEN SV=-1:GOSUB840
700 GOTO240
710 '
720 '-ForceCamp-
730 CLS
740 A=USR4(0)
750 IF A=0 THEN PRINT "Not Playing":GOTO820
760 A=A-1:PRINT"CHARA No.";A
770 CA=CB+A*256
780 PRINT"NAME:";:FOR I=0TO15:PRINT CHR$(PEEK(CA+I));:NEXT
790 PRINT:PRINT"LEVEL:";PEEK(CA+&H12);" EXP.:";
800 A!=PEEK(CA+&H36)*256+PEEK(CA+&H35):PRINTA!
810 C$="SAVE"
820 A=USR9(0):I$=INPUT$(1):RETURN
830 '
840 '-Game-
850 GOSUB930'Guide
860 IF VE<>SV THEN AB=USR0(EA):GOTO880'NEW GAME
870 V=VE:GOSUB1240:A=USR3(0):AB=USR0(-EA)'CONTINUE
880 A=USR2(0)'pushvram
890 ER$="Version Error":IF AB=0 THEN2090
900 GOSUB1020'Info
910 RC=1:RETURN
920 '
930 '-GUIDE-
940 CLS:WIDTH40:IF VE=SV THEN PRINT"CONTINUE" ELSE PRINT"NEW GAME"
950 PRINT"XWW- (STOP) : EXTRA-PAUSE (EX MODE) -WWYV";SPC(38);"V";
960 PRINT"V      (STOP)    : QUIT EXTRA-PAUSE    VV     (CTRL+@)   :   QUIT TO BASIC     VV    (CTRL+HOME) :  RESET TO TITLE     VV      (0/1/2)   :   WAIT OFF/1/2      V";
970 PRINT"V     (CTRL+F5)  :     SECRET ??       VZ";Z$;"[";SPC(12);"- HIT ANY KEY -";
980 A=USR9(0)
990 I$=INKEY$:I=STRIG(1) OR STRIG(3):IF(I$="")AND(I=0)THEN 990
1000 PRINT CHR$(13);SPC(15);"- START -";SPC(6);:RETURN
1010 '
1020 '-ROM INFO-
1030 SCREEN0:WIDTH40:A=USR9(0):GOSUB1190
1040 FOR I=0 TO 3:PRINT HEX$(I*32+64);"00h:bank(";RIGHT$("0"+HEX$(PEEK(AB+I)),2);")":NEXT
1050 A=PEEK(AB+4):IF A<255 THEN A$="SLOT:"+HEX$(A) ELSE A$="NOTHING"
1060 PRINT"FMPAC/PAC ";A$
1070 PRINT "HIT NAY KEY";:I$=INPUT$(1):RETURN
1080 '
1090 '-BANK HASH FILE-
1100 OPEN"HYD3M"+HEX$(VE+1)+"ID.TXT"FOROUTPUTAS#1:AB=AH:AC=AB+5+64*6:IS=6
1110 PRINT#1,"Bank:Address:Identify(HitCount:AllZero)"
1120 FORI=0TO63:PRINT#1,RIGHT$("0"+HEX$(I),2);":";RIGHT$("0"+HEX$(I*2),2);"000:";:FORJ=0TO IS-1:PRINT#1,RIGHT$("0"+HEX$(PEEK(AB+5+I*IS+J)),2);:NEXT:PRINT#1,"(";RIGHT$("0"+HEX$(PEEK(AC+I)AND 63),2);":";MID$("NZ",2+((PEEK(AC+I)AND 128)=0),1);")";
1130 PRINT#1,CHR$(32-(I AND 1)*19);:NEXT:CLOSE#1:RETURN
1140 '
1150 '-End-
1160 ONERRORGOTO0:LOCATE,,0
1170 SOUND0,100:SOUND1,3:SOUND7,190:FORJ=0TO3:SOUND8,10-J*2:FORI=0TO3:SOUND0,30+I*40+J:SOUND1,3-J:TIME=0:FORK=0TO1:K=TIME:NEXT:NEXT:NEXT:SOUND8,0:END
1180 '
1190 '-PAL-
1200 IF PT THEN 1220
1210 COLOR=NEW:RETURN'MSX2PAL
1220 A$="000000353464327437532356632643563664241535555777":GOTO1290'MSX1PAL
1230 '
1240 '-ScrMode-
1250 IF V THEN 1270
1260 A$="026206FF0336070028000000000000000F000000003B0500":GOSUB1300:RETURN'MSX1
1270 A$="0A621F8001F73E0F28000003000000000F000000003B0500":GOSUB1300'MSX2
1280 A$="567455430537541056163750153432222754015610777000"'MSX2PAL(TOWN/NOON)
1290 FOR I=0 TO 15:COLOR=(I,VAL(MID$(A$,I*3+1,1)),VAL(MID$(A$,I*3+2,1)),VAL(MID$(A$,I*3+3,1))):NEXT:RETURN
1300 FORI=0TO7:VDP(I)=VAL("&H"+MID$(A$,I*2+1,2)):NEXT
1310 FORI=8TO14:VDP(I+1)=VAL("&H"+MID$(A$,I*2+1,2)):NEXT
1320 VDP(24)=0:RETURN
1330 '
1340 '-Load-
1350 V$="":E$=".HD3":GOSUB1770
1360 ER$="Read":ONERRORGOTO2020
1370 OPENF$+E$FORINPUTAS#1:I$=INPUT$(3,#1):CLOSE#1
1380 OF#=&HC0*256-(ASC(MID$(I$,2,1))+ASC(MID$(I$,3,1))*256#)
1390 BLOAD F$+E$,OF#
1400 ONERRORGOTO0
1410 C$="GAME":SV=-1:RETURN
1420 '
1430 '-Save-
1440 V$="":E$=".HD3":GOSUB1770
1450 ER$="Write":ONERRORGOTO2020
1460 BSAVE F$+E$,&HC000,&HC3FF
1470 ONERRORGOTO0
1480 RETURN
1490 '
1500 '-QLOAD-
1510 V$=" MSX"+HEX$(VE+1):IF VE THEN E$=".H3M" ELSE E$=".H3B"
1520 V=VE:GOSUB1770:GOSUB1240'screen
1530 RS=1:ER$="Read":ONERRORGOTO2020
1540 U=USR6(0)'move to evac area
1550 A=ASC(LEFT$(USR3(F$),1))'load
1560 ONERRORGOTO0:IF A<>0THEN2090
1570 AB=USR0(-EA)'GAME
1580 A=USR2(0)'pushvram
1590 POKE AE,0'invalidate evac area
1600 ER$="Version Error":IF AB=0 THEN2090
1610 RS=0:GOSUB1020
1620 RETURN
1630 '
1640 '-QSAVE-
1650 ER$="No data":IF SV<0 THEN2090'SAVVER
1660 V$=" MSX"+HEX$(SV+1):IF SV THEN E$=".H3M" ELSE E$=".H3B"
1670 V=SV:GOSUB1770:GOSUB1240
1680 U=USR6(0)'move to evac area
1690 A=USR3(0)'popvram
1700 ER$="Write":ONERRORGOTO2020
1710 A=ASC(LEFT$(USR2(F$),1))'save
1720 ONERRORGOTO0
1730 IF A<>0THEN2090
1740 U=USR6(1)'back from evac area
1750 RETURN
1760 '
1770 '-InputFilename-
1780 CLS:LOCATE0,6:PRINT STRING$(40,"-");
1790 LOCATE 20-(LEN(C$)+8)\2,6:PRINT" ";C$;" FILES "
1800 ONERRORGOTO1990
1810 FILES "*"+E$:IF POS(0) THEN PRINT
1820 ONERRORGOTO0
1830 PRINT STRING$(40,"-");
1840 '
1850 LOCATE 0,0:PRINT SPC(200)
1860 LOCATE 0,0:I=USR9(0)
1870 PRINT C$;V$;:LINEINPUT" FILENAME _";F$
1880 IF F$="" THEN F$="HYDLIDE3"
1890 F$=LEFT$(F$+"       ",8)
1900 LOCATE5,2:PRINT "FILENAME:";F$;E$
1910 '
1920 LOCATE7,4:PRINT C$;" OK? (Y/N)_";
1930 I$=INPUT$(1)
1940 IF I$=CHR$(27) THEN GOTO240
1950 IF I$="Y"OR I$="y" THEN PRINT"Y":RETURN
1960 IF I$="N"OR I$="n" THEN PRINT"N":GOTO1850
1970 BEEP:GOTO1930
1980 '
1990 '-FilesError
2000 LOCATE 11:PRINT"NO FILE":RESUME NEXT
2010 '
2020 '-ONERROR
2030 E1=ERR:E2=ERL
2040 IF E1=66 THEN ER$="DISK FULL"
2050 IF E1=68 THEN ER$="WRITE PROTECT"
2060 IF E1=70 THEN ER$="NOT READY"
2070 RESUME 2120
2080 '
2090 '-ERROR
2100 GOTO2120
2110 '
2120 '-DISP.ERROR
2130 ONERRORGOTO0:FORI=0TO1:I=-(INKEY$=""):NEXT:ER$="ERROR: "+ER$
2140 IF PEEK(AE) THEN U=USR6(1)'BACK from EVACAREA
2150 SCREEN0:LOCATE 20-LEN(ER$)\2,11:COLOR9:PRINT ER$;
2160 BEEP:I$=INPUT$(1):COLOR15:GOTO240:GOTO1150'END
