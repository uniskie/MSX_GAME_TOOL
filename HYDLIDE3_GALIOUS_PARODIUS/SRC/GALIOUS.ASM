;**********************************
;   SET TRAP for Maze of GALIOUS
;**********************************
; for tniasm v0.45
; http://www.tni.nl/products/tniasm.html

; A=USR( TIME-TRAP É ±ÄÞÚ½ )
;
	FNAME	"GALIOUS.BIN"	;*BINHEAD*
	DB	0FEH	;*BINHEAD*
	DW	ORGADR	;*BINHEAD*
	DW	ENDFL-1	;*BINHEAD*
	DW	ORGADR	;*BINHEAD*

;BIOS LABELS for GAME UTILS
	INCLUDE "BIOS-DEF.ASM"

ORGADR:	EQU	0C000H
	ORG	ORGADR
;
	JP	GALIOUS_
	JP	GAL_CHK
GAL_REF:	;
	DB	0
GAL_MUS:	;
	DB	88H,01H,0
ROMSLT:	DB	1
;
GALIOUS_:	;
	CALL CPUZ80
	DI			;F3
	LD	(SVW_SP),SP
	CALL	RSLREG
	LD	(W_SLREG),A
	LD	A,V_PG3	;COMMON
	CALL	SAVE_	;COMMON
;
	LD	HL,H_KEYI
	LD	DE,SV_HOOK
	LD	BC,5
	LDIR
;
	IM	1		;ED 56
	LD	SP,0F0F0H	;31 F0 F0
;
	LD	A,(ROMSLT)
	LD	H,40H
	CALL	ENASLT
	LD	A,(ROMSLT)
	LD	H,80H
	CALL	ENASLT
;
	LD	A,1
	CALL	CHGCAP	;COMMON
;
	LD	A,0C3H		;3E C3
	LD	(H_KEYI),A	;32 9A FD
	LD	HL,EX_TRAP	;21 XX XX COMMON
	LD	(H_KEYI+1),HL	;22 9B FD
;
	LD	A,(GAL_REF)
	DEC	A
	JP	Z,LOAD	;COMMON
	INC	A
	JP	NZ,LOAD_AT	;COMMON
	JP	GAL_ADR
GAL_ADR:	EQU	40AEH
;
;4002	95 40		DW	4095H
;
;4095	F3		DI
;4096	ED 56		IM	1
;4098	CD D3 40	CALL	40D3H	;GET PAGE1 SLOT No.
;409B	26 80		LD	H,80H
;409D	CD 24 00	CALL	0024H
;40A0	3E C3		LD	A,0C3H
;40A2	32 9A FD	LD	(H_KEYI),A
;40A5	21 EF 40	LD	HL,40EFH
;40A8	22 9B FD	LD	(H_KEYI+1),HL
;40AB	31 F0 F0	LD	SP,0F0F0H
;40AE
;
GAL_CHK:	;
;	PUSH	HL
;;
;	LD	A,(ROMSLT)
;	LD	H,40H
;	CALL	ENASLT
;;
;	POP	HL
;	INC	HL
;	INC	HL
;	LD	DE,(4002H)
;	LD	(HL),E
;	INC	HL
;	LD	(HL),D
;;
;	LD	A,(EXPTBL)
;	LD	H,40H
;	CALL	ENASLT
;	EI
;	RET
;-------------------
;A=USR(MATCH VALUE)
;OUT:	SUCCESS:   A=MATCH VALUE 
;	NOT FOUND: A=0
ROM_CHK:	;
	INC	HL
	INC	HL
	PUSH	HL
	LD	C,1
	CALL	SLTCHK
	JR	Z,ROM_CHKE
	LD	C,2
	CALL	SLTCHK
	JR	Z,ROM_CHKE
;
	LD	BC,0
ROM_CHKE:	;
	POP	HL
	LD	(HL),C
	INC	HL
	LD	(HL),B	;RET VAL
;
	LD	A,(EXPTBL)
	LD	H,40H
	CALL	ENASLT
	EI
	RET
;
; (CHECK SLOT/EXP SLOT)
;(OUT: Z=NG / NZ=OK)
SLTCHK:	;
	PUSH	HL
	LD	HL,EXPTBL
	LD	A,C
	CALL	ADDHLA
	LD	A,(HL)	;EXPSLT CHECK
	POP	HL
	AND	80H
	LD	A,C
	JR	NZ,EXPSLTC	;EXPANDED
	LD	(ROMSLT),A
	JR	ROMCHK_S
; (CHECK EXP SLOT)
EXPSLTC:	;
	LD	B,04H
	OR	80H
	LD	C,A
EXPSLTL:	;
	LD	(ROMSLT),A
	CALL	ROMCHK_S
	RET	Z
;
	LD	A,C
	ADD	A,04H	;EXPSLT+1
	LD	C,A
	DJNZ	EXPSLTL
	OR	1
	RET
;
;(CHECK MATCH ROM VALUE)
;IN	A=SLOT
;OUT	Z=NG / NZ=OK
ROMCHK_S:	;
	PUSH	HL
	LD	H,40H
	CALL	ENASLT
	POP	HL
;
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	DEC	HL
;
	EX	DE,HL
	LD	BC,(4002H)
	OR	A
	SBC	HL,BC
	EX	DE,HL
;
	LD	BC,(ROMSLT)
	LD	B,0
	RET
;
;
RET_BAS:	;
	LD	A,(W_SLREG)
	CALL	WSLREG
;
	LD	HL,SV_HOOK
	LD	DE,H_KEYI
	LD	BC,5
	LDIR
	LD	SP,(SVW_SP)	;COMMON
;	IM	0
	EI
;
	CALL	CPURES
	RET
;
;
W_SLREG:	;
	DB	0
SV_HOOK:	;
	DS	5
;
;---:	GALIOUS ROM RUTINE
SETMUSIC:	EQU	4E34H
SETFONT0:	EQU	5C46H
SETFONT1:	EQU	5AB3H
;
;
PLAY:	LD	HL,H_KEYI
	LD	DE,SVPHOOK
	LD	BC,5
	LDIR

	DI
	LD	A,0C3H
	LD	HL,PLAY_TR
	LD	(H_KEYI),A
	LD	(H_KEYI+1),HL

	LD	A,V_PG1	;COMMON
	CALL	SAVE_	;COMMON

	CALL	WR_MES_AT
	DW	3800H
	DB	'xvv/SOUND[BOX/vvvvvvvvvvvvvvvvv~'	;*DM*
	DB	'w//////////////////////////////w'	;*DM*
	DB	'w/SOUND/v00v//////NOW/PLAYING//w'	;*DM*
	DB	'w/////////////xvvvvvvvvvvvvvvv~w'	;*DM*
	DB	'w/MUSIC/v00v//w///////////////ww'	;*DM*
	DB	'w/////////////yvvvvvvvvvvvvvvv'	;*DM*
	DB	127,'w'
	DB	'w/EXIT/////////////////////////w'	;*DM*
	DB	'yvvvvvvvvvvvvvvvHOME[/FADE[OUTv'	;*DM*
	DB	127,'¡'

;RG8SAV:	EQU	0FFE7H
	LD	A,(RG8SAV)
	OR	2
	OUT	(99H),A	;SPRITE OFF
	LD	A,8+80H
	OUT	(99H),A
	XOR	A
	OUT	(99H),A	;BACK COLOR=0
	LD	A,7+80H
	OUT	(99H),A

	LD	A,2
	LD	(GAL_MUS+2),A
	LD	A,(0E1C7H)
	LD	(GAL_MUS+0),A
	LD	B,A
	XOR	A
	CALL	SETMUSIC
	LD	A,B
	CALL	SETMUSIC
	CALL	SETFONT

	CALL	WR_PNO
	CALL	WR_PNUM
	CALL	WR_TITL
	JP	L_PLAY

PLAY_R:	DI
	LD	A,7
	OUT	(PSGIOA),A	;COMMON
	LD	A,0BFH
	OUT	(PSGIOW),A	;COMMON
;
	LD	DE,H_KEYI
	LD	HL,SVPHOOK
	LD	BC,5
	LDIR
;
	LD	A,V_PG1	;COMMON
	CALL	LOAD_	;COMMON
;
	LD	A,(RG8SAV)
	AND	253
	OUT	(99H),A	;SPRITE ON
	LD	A,8+80H
	OUT	(99H),A
;
	LD	DE,0801H	;'SPC'
	CALL	KEYIN
	JP	Z,$-6
	JP	RETURN
;
;
L_PLAY:	;
	CALL	WR_PNO
	CALL	WR_PNUM
	CALL	WR_TITL
	XOR	A
	LD	(WAITCT),A
	EI
L_PLAYW:	;
	LD	DE,8FFH
	CALL	KEYIN
	CPL
	OR	A
	JP	Z,L_PLAYJ
	LD	A,(WAITCT)	;WAIT INTERVAL
	CP	20		;20/60 SEC
	JR	C,L_PLAYW
L_PLAYJ:	;
	EI
L_PLAY0:	;
	LD	DE,8FFH
	CALL	KEYIN
	LD	HL,P_JUMPT
	LD	B,8
L_PL0:	RLA
	JP	C,J_PL0
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	EX	DE,HL
	JP	(HL)
J_PL0:	INC	HL
	INC	HL
	DJNZ	L_PL0
	JP	L_PLAY0
P_JUMPT:	;
	DW	P_RIGH,P_UP,P_DOWN,P_LEFT
	DW	P_DEL,P_INS,P_HOME,P_SPAC
;
P_LCH:	POP	DE
	LD	A,(GAL_MUS+2)
	CP	2
	JP	Z,L_PLAY
	LD	HL,GAL_MUS+1
	PUSH	DE
	OR	A
	RET	Z
	DEC	HL
	RET
;
P_INS:	LD	C,4
	JP	$+5
P_RIGH:	LD	C,1
	CALL	P_LCH
	OR	A
	JR	NZ,P_ADD2
P_ADD:	LD	A,(HL)
	ADD	A,C
	CP	31H
	JP	C,$+5
	SUB	30H
	JR	P_ADDE
P_ADD2:	LD	A,(HL)
	ADD	A,C
	AND	255
	OR	128
P_ADDE:	LD	(HL),A
	JP	L_PLAY
;
P_DEL:	LD	C,4
	JR	$+5
P_LEFT:	LD	C,1
	CALL	P_LCH
	OR	A
	JR	NZ,P_SUB2
P_SUB:	LD	A,(HL)
	SUB	C
	JR	C,$+6
	JR	NZ,$+5
	ADD	A,30H
	JR	P_ADDE
P_SUB2:	LD	A,(HL)
	SUB	C
	AND	255
	OR	128
	JR	P_ADDE
;
P_DOWN:	LD	A,(GAL_MUS+2)
	DEC	A
	CP	255
	JR	NZ,L_PUD
	LD	A,2
	JP	L_PUD
;
P_UP:	LD	A,(GAL_MUS+2)
	INC	A
	CP	3
	JR	NZ,L_PUD
	XOR	A
L_PUD:	LD	(GAL_MUS+2),A
	JP	L_PLAY
;
;
P_HOME:	XOR	A
	LD	(0E1C1H),A
	INC	A
	LD	(0E1C0H),A
	JP	L_PLAY
;
P_SPAC:	LD	A,(GAL_MUS+2)
	CP	2
	JP	Z,PLAY_R
	CALL	P_LCH
	LD	C,(HL)
	JR	Z,J_PL3
	LD	A,C
	CP	0FBH
	JR	NC,J_PL3
	XOR	A
	CALL	SETMUSIC
J_PL3:	LD	A,C
	CALL	SETMUSIC
	LD	DE,0801H
	CALL	KEYIN
	JR	Z,$-6
	JP	L_PLAY
;
WR_MES_AT:
	POP	HL
	CALL	WR_MES
	JP	(HL)
;
WR_MES:	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	EX	DE,HL
	DI
	CALL	NSTWRT
	DI
	EX	DE,HL
	INC	HL
WR_MES_:	;
	LD	A,(HL)
	CP	'¡'
	JP	Z,J_PL4
	CP	'A'
	JP	C,$+5	;TO SUB 15
	SUB	7
	SUB	15+32
	OUT	(98H),A
	INC	HL
	JP	WR_MES_
J_PL4:	INC	HL
	RET
;
WR_PNUM:	;
	DI
	LD	HL,3849H
	CALL	NSTWRT
	LD	A,(GAL_MUS+1)
	CALL	WR_PNU_
	LD	HL,3889H
	CALL	NSTWRT
	LD	A,(GAL_MUS+0)
	CALL	WR_PNU_
	EI
	RET
WR_PNU_:	;
	LD	B,A
	RRA
	RRA
	RRA
	RRA
	AND	15
	INC	A
	OUT	(98H),A
	LD	A,B
	AND	15
	INC	A
	OUT	(98H),A
	RET
;
WR_PNO:	LD	HL,3841H
	LD	BC,300H
L_PL5:	LD	A,(GAL_MUS+2)
	CP	C
	LD	A,0
	LD	D,0
	JP	NZ,$+7	;TO LD D,A
	LD	A,44H
	LD	D,45H
	DI
	CALL	NWRVRM
	LD	A,L
	ADD	A,6
	LD	L,A
	LD	E,A
	LD	A,2
	CP	C
	JP	NZ,$+4
	DEC	L
	LD	A,D
	CALL	NWRVRM
	EI
	LD	L,E
	INC	C
	LD	A,40H-6
	ADD	A,L
	LD	L,A
	DJNZ	L_PL5
	RET
;
;
RDVDP:	EQU	013EH
PLAY_TR:	;
	DI
	CALL	RDVDP
	LD	A,0DH
	LD	(6000H),A
	INC	A
	LD	(8000H),A
	CALL	6004H
	DI
	LD	A,(0F0F1H)
	LD	(6000H),A
	LD	A,(0F0F2H)
	LD	(8000H),A
	CALL	RDVDP
;
	LD	A,(WAITCT)
	ADD	A,1
	SBC	A,0	; 255 = STOP
	LD	(WAITCT),A
;
	EI
	RET
;
WAITCT:	DB	0	;WAIT CNT
SVPHOOK:	;
	DS	5
;
SETFONT:	;
	CALL	41D0H	;BANK D,E,F
	DI
	LD	DE,9AD3H
	LD	HL,2008H
	CALL	SETFONT0
	LD	DE,9B24H
	LD	HL,2058H
	CALL	SETFONT0
	LD	A,0F0H
	LD	HL,0008H
	LD	BC,0120H
	CALL	BIGFIL
	CALL	41B7H	;BANK A,B,C
	DI
	LD	IX,D_FDAT
	CALL	SETFONT1
D_FDAT:		;
	DB	4,99H,63H,25H,63H,65H,0
;
;
WR_TITL:	;
	DI
	LD	HL,388FH
	CALL	NSTWRT
	LD	D,0
	LD	A,(GAL_MUS)
	CP	0FBH
	JP	C,J_WTIT1
	SUB	0FBH-16H
	JP	J_WTIT2
;
J_WTIT1:	;
	SUB	80H
	CP	16H
	JP	C,J_WTIT2
	XOR	A
;
J_WTIT2:	;
	RLA
	RLA
	RLA
	RLA
	JP	NC,J_WTIT3
	INC	D
J_WTIT3:	;
	AND	0F0H
	LD	E,A
	LD	HL,MUS_TIT
	ADD	HL,DE
	CALL	WR_MES_
	RET
;
	INCLUDE	"GAL-EXPS.ASM"	;*FILE*
	INCLUDE	"EX-BIOS.ASM"	;*FILE*
	INCLUDE	"GAL-TITL.ASM"	;*FILE*
;
ENDFL:	DB	0
