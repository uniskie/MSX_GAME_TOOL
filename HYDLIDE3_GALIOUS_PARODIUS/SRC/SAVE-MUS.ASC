10 ' PARODIUS/GALIOUS ROM SAVE for MUSIC
20 CLEAR2000,&HBFFF:DEFINTA-Z
30 BLOAD"TFROM.BIN"
40 ONINTERVAL=1GOSUB440:INTERVALON
50 '
60 COLOR 9,0,0:KEYOFF:SCREEN0:WIDTH40
70 A$="WWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWW
80 PRINT"X"+A$+"YV      ROM EXTRACTER for JUKE BOX      VV     (PARODIUS/GALIOUS/F1-SPIRIT)     VV";SPC(38);"VZ";A$;"["
90 '
100 DEFUSR9=&H156
110 DEFUSR0=&HC000
120 DEFUSR1=&HC003
130 '
140 PRINT CHR$(10);"      - INSERT CARTRIDGE in SLOT -      "
150 PRINT CHR$(10);"PAUSE ¼Ã ROM ¦ »¼Ã¶× PAUSE ¶²¼Þ® ¼Ã¸ÀÞ»²
160 '
170 A=USR0(&H4099):IF A<>0 THEN T$="PARODIUS":N$="PARO-MUS.OBJ":T=0:GOTO200
180 A=USR0(&H4095):IF A<>0 THEN T$="GAROIUS":N$="GAR-MUS.OBJ":T=1:GOTO200
185 A=USR0(&H40E4):IF A<>0 THEN T$="F1SPIRIT":N$="F1SP-MUS.OBJ":T=2:GOTO200
190 GOTO 170
200 '
210 IF A<4 THEN S$=" "+HEX$(A)+" " ELSE S$=HEX$(A AND 3)+"-"+HEX$((A AND 12)\4)
220 A$="- Identified "+T$+" in SLOT "+S$+" -"
230 PRINT SPC(20-LEN(A$)\2);A$
240 '
250 SOUND0,110:SOUND1,0:SOUND7,190:SOUND8,0
260 FORJ=0TO7:SOUND8,10-J:FORI=0TO4:SOUND0,25+I*24-J:W=0:FORK=0TO1:K=W:NEXT:NEXT:NEXT:SOUND8,0
270 '
280 IF T=0 THEN BK$="000405060A"
290 IF T=1 THEN BK$="000D0E"
295 IF T=2 THEN BK$="0D0E0F"
300 B$=CHR$(0)+CHR$(&H40):FORI=1TOLEN(BK$)STEP2:B$=B$+CHR$(VAL("&H"+MID$(BK$,I,2))):NEXT
310 '
320 SCREEN7:SETPAGE1,1:CLS
330 A$=USR1(B$)
340 IF ASC(MID$(A$,1,1))<>255 THEN SCREEN0:BEEP:PRINT"ERROR":END
350 '
360 ED=&H40+(LEN(B$)-2)*&H20-1:ED$=RIGHT$("0"+HEX$(ED),2)
370 BSAVE N$,&H4000,VAL("&H"+ED$+"FF"),S
380 SCREEN0
390 PRINT"BANK:";:FORI=3TOLEN(B$):PRINT HEX$(ASC(MID$(B$,I,1)));"h ";:NEXT:PRINT
400 PRINT"ROM DATA:VRAM 14000H-1";ED$;"FFH"
410 PRINT"SCREEN7:SETPAGE1,1:";
420 PRINT"BSAVE";CHR$(34);N$;CHR$(34);",&h4000,&H";ED$;"FF,S"
430 END
440 '
450 W=W-(W<255):RETURN
