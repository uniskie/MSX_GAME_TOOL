	FNAME	"LAY-COPY.BIN"	;*BINHEAD*
;--> BIN FILE HEADER -->
	DB	0FEH	;*BINHEAD*
	DW	ORGADR	;*BINHEAD*
	DW	ENDFL-1	;*BINHEAD*
	DW	ORGADR	;*BINHEAD*
;<-- BIN FILE HEADER <--
;
ORIGIN:	EQU	0D000H
ORGADR:	EQU	ORIGIN
	ORG	ORGADR
;====== SUPER LAYDOCK SAVER ======
GAMESLT:	EQU	1	; slot 1

WRSLT:	EQU	0014H
ENASLT:	EQU	0024H
RAMAD0:	EQU	0F341H	;DISK WORK
EXPTBL:	EQU	0FCC1H
SLTTBL:	EQU	0FCC5H

COPY:
	LD	DE,MESG0
	CALL	F_PRINT

	LD	A,GAMESLT
	LD	H,80H
	CALL	ENASLT
	EI

CHECK:	LD	HL,8004H
	LD	DE,MOJI
	LD	B,12
CHECK_L:	LD	A,(DE)
	CP	(HL)
	JP	NZ,CHECK
	INC	HL
	INC	DE
	DJNZ	CHECK_L

	LD	DE,MESG1
	LD	C,_PRINT
	CALL	BDOS

;=== WRITE FILE ===
	LD	DE,FCB
	CALL	F_MAKE
	LD	DE,8000H
	CALL	F_DMA

	LD	B,16
	LD	E,0
WRITE:	PUSH	BC
	LD	HL,7000H
	LD	A,GAMESLT
	CALL	WRSLT
	INC	E
	LD	HL,7800H
	LD	A,GAMESLT
	CALL	WRSLT
	INC	E
	PUSH	DE
	LD	DE,FCB
	LD	HL,2
	CALL	F_WRITE
	POP	DE
	POP	BC
	DJNZ	WRITE

ENDE:	LD	DE,MESG2
	LD	C,_PRINT
	CALL	BDOS
	LD	DE,FCB
	CALL	F_CLOSE

	LD	A,(RAMAD0+2)
	LD	H,80H
	CALL	ENASLT
	EI
	RET

;====== DATA ======
FCB:	DB	0
	DB	'SLAYDOCKDAT'
	DS	37-12

MESG0:	DB	'=== Super LAYDOCK  disk buckup tool==='
	DB	10,10,13
	DB	'insert CARTRIDGE in SLOT '
	DB	30H+GAMESLT	;'0'+GAMESLT
	DB	10,10,13,'$'
MESG1:	DB	'identified CARTRIDGE = Super LAYDOCK'
	DB	10,13
	DB	'write file "SLAYDOCK.DAT"'
	DB	10,10,13,'$'
MESG2:	DB	'Completed Buckup'
	DB	10,13,'$'

MOJI:	DB	'SuperLAYDOCK'

;====== INCLUDE ======

	INCLUDE	"MRG-IO.ASM"	;*FILE*

ENDFL:	;
