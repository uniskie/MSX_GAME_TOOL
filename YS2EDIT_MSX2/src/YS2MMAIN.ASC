10 '# YS2 MESSAGE EDITOR (MAIN)
20 DEFINTA-Z:COLOR15,0,0:VDP(9)=VDP(9)OR2 'SPRITE OFF
30 DIMC$(15):WA=&HD600:DA=&HA500:BA=&HD500:DEFUSR9=&H156 'KEYBUF CLEAR
50 '# WA=WORKAREA (4 byte)                                                   
60 '#  [DISK]=[+0,1=SECTOR+(W=800h/R=0)] [+2:R/W COUNT] [+3:DMA H(**00h)]
70 '#  [MOJI]=[+0:X] [+1:Y] [+2:OFFSET(PATERN No.#64)] [+3:SIZE]
80 '# DA=DMA(??00h) (3000hBytes)
90 '# BA=CHR LIST ADRS (100hBytes)
110 SCREEN5:COLOR=(4,3,3,2):COLOR=(3,5,5,0):COLOR=(2,3,6,4)
120 COLOR=(13,3,3,0):COLOR=(12,1,3,2):COLOR=(5,2,3,3):COLOR=(6,0,1,7)
130 AM=1:GOTO260
140 '# MOJI (CENTER)
150 LX=(256-LEN(A$)*8)\2:LY=Y*9:GOTO180
160 '# MOJI (A$,lX,lY)
170 LX=X*8:LY=Y*9
180 POKE WA,LX:POKE WA+1,LY:POKE WA+2,TP*64:U$=USR(A$)
190 X=PEEK(WA)\8:Y=PEEK(WA+1)\9:RETURN
220 '# PUT SECTOR No
230 LINE(0,9)-(127,17),0,BF
240 TP=0:X=0:Y=1:A$="SECTOR:"+MID$(STR$(SC),2)+"-"+MID$(STR$(SC+SH-1),2):GOSUB160
250 RETURN
260 '# MAIN
270 SC=0:SH=24:RD=0:WT=0:PT=0:PG=0'TP=PT=FONT 0:NORMAL 1:TITLE
280 LINE(0,0)-(255,8),6,BF
290 X=4:Y=0:TP=1:A$="- YS"+CHR$(25)+CHR$(26):GOSUB160
300 TP=0:A$=" MESSAGE EDITOR -":GOSUB160
320 GOSUB220
330 TP=0:Y=2:A$="-MENU-":GOSUB140
340 X=9:Y=3:A$="1:READ":GOSUB160
350 X=9:Y=4:A$="2:WRITE":GOSUB160
360 X=9:Y=5:A$="3:EDIT":GOSUB160
370 X=9:Y=6:A$="4:DIR":GOSUB160
380 TP=1:Y=8:A$="ESC:CANCEL":GOSUB140
390 I$=INPUT$(1):IF I$<"1" OR I$>"4" THEN BEEP:GOTO390
400 LINE(0,71)-(255,80),6,BF
410 TP=0:Y=8:A$="SELECT:"+I$:GOSUB140
420 ON VAL(I$) GOSUB450,840,1070,2030
430 LINE(0,9)-(255,212),0,BF
440 GOTO320
450 '# INPUT SECTOR NO
460 IF AM=1 AND WT=1 AND RD=1 THEN GOSUB790'MODIFIED:WRITE?
470 LINE(0,10*9-4)-(255,15*9+3),12,BF
480 Y=11:A$="READ SECTOR(24)":GOSUB140
490 X=3:Y=13:A$="SECTOR? (0-"+RIGHT$(STR$(1440-SH),4)+")? ":GOSUB160
500 XX=X:A$="0000":II$=A$:GOSUB160
510 FORI=3TO0STEP-1
520 I$=INPUT$(1)
530 IF I$=CHR$(13) THEN I=0:NEXT:GOTO580
540 IF I$<"0" OR I$>"9" THEN IF I$=CHR$(27) THEN BEEP:RETURN ELSE BEEP:GOTO520
550 LINE(XX*8,Y*9)-(XX*8+31,Y*9+7),12,BF
560 X=XX:II$=MID$(II$,2,3)+I$:A$=II$:GOSUB160
570 NEXT:GOTO510
580 SC=VAL(II$):IF SC>(1440-SH) THEN BEEP:GOTO510
590 ER=0:GOSUB600:IF ER THEN RETURN ELSE CX=0:CY=0:PG=0:RETURN
600 '# READ (SC=SEC/SH=CNT/DA=ADR)
610 GOSUB220
620 POKE WA,SC AND 255:POKE WA+1,SC\256
630 POKE WA+2,SH:POKE WA+3,VAL("&h"+LEFT$(HEX$(DA),2)):ON ERROR GOTO2500:U=USR1(0):RC=1
640 ONERROR GOTO0:ER=PEEK(WA):IF ER THEN 660'ERRCHK
650 RD=1:WT=0:RETURN
660 BEEP'# DISK ERROR
670 LINE(6,15*9-1)-(249,16*9),8,BF
680 A$="OTHER ERROR
690 IF ER=11 THEN A$="WRITE FAULT
700 EC=ER AND &HFE
710 IF EC=0 THEN A$="WRITE PROTECTED
720 IF EC=2 THEN A$="NOT READY
730 IF EC=4 THEN A$="DATA CRC ERROR
740 IF EC=6 THEN A$="SEEK ERROR
741 IF EC=8 THEN A$="RECORD NOT FOUND
742 IF EC=10 THEN A$="UNSUPPORTED MEDIA
746 IF EC=128 THEN A$="BAD FAT
750 TP=0:Y=15:A$="ERR"+STR$(ER)+":"+A$:GOSUB140
760 I$=INPUT$(1)
770 IF RC=1 THEN RD=0 ELSE WR=1'R/W ERROR
780 RETURN
790 '# WRITE IF MODIFIED
800 IF WT=0 OR RD=0 THEN RETURN'WT=0:NO CHANGE
810 BEEP:LINE(0,10*9-2)-(255,11*9+1),8,BF
820 TP=0:Y=10:A$="DATA IS MODIFIED":GOSUB140
830 GOTO950
840 '# WRITE SECTOR
850 IF RD=1 THEN 890'RD=0:NO READ
860 BEEP:LINE(0,10*9-2)-(255,11*9+1),8,BF
870 TP=0:Y=10:A$="NO DATA! NEED TO READ":GOSUB140
880 GOTO950
890 IF WT=1 THEN 950'WT=0:NO CHANGE
900 BEEP:LINE(0,10*9-2)-(255,11*9+1),8,BF
910 TP=0:Y=10:A$="DATA IS NOT MODIFIED":GOSUB140
920 GOTO950
930 BEEP:LINE(0,10*9-2)-(255,11*9+1),5,BF
940 TP=0:Y=10:A$="(DATA IS MODIFIED)":GOSUB140
950 '# WRITE OK?(Y/N)
960 LINE(0,12*9-4)-(255,15*9+3),5,BF
970 Y=12:A$="WRTIE SECTOR"+STR$(SC)+" -"+STR$(SC+SH-1):GOSUB140
980 X=8:Y=14:A$="WRITE OK? (Y/N) ":GOSUB160
990 I$=INPUT$(1)
1000 IF I$="N" OR I$="n" OR I$=CHR$(27) THEN A$="N":BEEP:GOSUB160:RETURN
1010 IF I$="Y" OR I$="y" THEN A$="Y":GOSUB160:GOTO1040
1020 BEEP:GOTO990
1030 '# WRITE (SC=SEC/SH=CNT/DA=ADRS)
1040 POKE WA,SC AND255:POKE WA+1,SC\256+&H8 'WRITE BIT
1050 POKE WA+2,SH:POKE WA+3,VAL("&H"+LEFT$(HEX$(DA),2)):ON ERROR GOTO2500:A=USR1(0):RC=0
1060 ON ERROR GOTO0:GOTO640'ERROR CHECK
1070 '# EDIT START
1080 IF RD=1 THEN 1120'RD=0:CAN'T EDIT
1090 BEEP:LINE(0,11*9-2)-(255,12*9+1),8,BF
1100 TP=0:Y=11:A$="CAN'T EDIT! NEED TO READ":GOSUB140
1110 I$=INPUT$(1):RETURN
1120 '# EDIT
1130 MO=0
1140 LINE(0,9)-(255,211),0,BF
1150 LINE(0,36)-(118,61),5,BF
1160 TP=0:X=0:Y=4:A$="TAB:FONT PAGE":GOSUB160
1170 X=0:Y=5:A$="RET:INPUT MODE":GOSUB160
1180 X=0:Y=6:A$="INS/DEL=PAGE+/-":GOSUB160
1190 GOSUB220:GOSUB1780:LINE(0,27)-(118,34),4,BF
1200 TP=PT:X=0:Y=3:A$="SELECT:FONT="+HEX$(PT):GOSUB160
1210 'DISP
1220 LINE(0,18)-(127,26),0,BF
1230 TP=1:X=0:Y=2:A$="SC="+MID$(STR$(SC+PG),2):GOSUB160
1240 X=8:Y=2:A$="DATA=":GOSUB160
1250 LINE(0,7*9)-(255,22*9+7),12,BF
1260 X=0:Y=7:POKE WA,X*8:POKE WA+1,Y*9:POKE WA+2,PT*64:POKEWA+3,0 '256 byte
1270 FORI=0TO1:U=USR(DA+PG*512+I*256):NEXT
1280 U=USR9(0)
1290 '# LOOP
1300 LINE(CX*8,(CY+7)*9)-(CX*8+7,(CY+7)*9+7),15,BF,XOR
1310 LINE(13*8,18)-(14*8+7,26),0,BF
1320 TP=1:X=13:Y=2:A$=RIGHT$("0"+HEX$(PEEK(DA+PG*512+CY*32+CX)),2):GOSUB160
1330 I$=INPUT$(1):C=ASC(I$)
1340 LINE(CX*8,(CY+7)*9)-(CX*8+7,(CY+7)*9+7),15,BF,XOR
1350 IF C=27 THEN BEEP:RETURN
1360 CX=CX-(C=28)+(C=29)
1370 CY=CY-(C=31)+(C=30)
1380 IF CX<0 THEN CX=31:CY=CY-1
1390 IF CX>31 THEN CX=0:CY=CY+1
1400 IF CY<0 THEN CY=15
1410 IF CY>15 THEN CY=0
1420 IF C=11 THEN CX=0:CY=0
1430 IF C=12 THEN CX=0:CY=0:MO=0:PG=0:PT=0:CH=0:GOTO1190
1440 IF C=18 THEN PG=PG+1:GOSUB1910:PG=PG MOD SH:GOTO1210
1450 IF C=127 THEN PG=PG-1:GOSUB1910:PG=PG-(PG<0)*SH:GOTO1210
1460 IF C=13 THEN MO=(MO+1)AND 1:GOSUB1780
1470 IF C=24 THEN PT=(PT+1)MOD 3:TP=PT:GOTO1190
1480 ON MO+1 GOSUB1500,1600
1490 GOTO1290
1500 '# DIRECT MODE
1510 IF I$<" " THEN RETURN
1520 IF (I$>="a" AND I$<="z") OR (C>=128 AND C<160) THEN C=C-32:I$=CHR$(C)
1530 IF C>=224 AND C<254 THEN C=C-96:I$=CHR$(C)
1540 LINE(CX*8,(CY+7)*9)-(CX*8+7,(CY+7)*9+7),12,BF
1550 TP=PT:X=CX:Y=CY+7:A$=I$:GOSUB160
1560 POKE DA+PG*512+CY*32+CX,C:WT=1
1570 CX=CX+1:IF CX>31 THEN CY=CY+1:CX=0
1580 IF CY>15 THEN CY=15
1590 RETURN
1600 '# FONT PAGE
1610 IF I$="+" THEN I$=HEX$((CH+1)AND 15):GOTO1720
1620 IF I$="-" THEN I$=HEX$((CH-1)AND 15):GOTO1720
1630 IF C<>9 THEN 1730'TAB
1640 LINE(16*8,2*9)-(255,3*9-1),0,BF
1650 TP=0:X=18:Y=2:A$="NUMBER(0-F)?":GOSUB160
1660 I$=INPUT$(1):C=ASC(I$)
1670 IF C=18 OR I$="+" THEN I$=HEX$((CH+1)AND 15):GOTO1720
1680 IF C=127 OR I$="-" THEN I$=HEX$((CH-1)AND 15):GOTO1720
1690 IF I$>"Z" THEN C=C-32:I$=CHR$(C)
1700 IF I$<"0" OR (I$>"9" AND I$<"A") OR I$>"F" THEN BEEP:GOTO1660
1710 A$=I$:GOSUB160
1720 CH=VAL("&H"+I$):GOTO1780
1730 '# CHR CODE
1740 IF I$>"Z" THEN C=C-32:I$=CHR$(C)
1750 IF I$<"0" OR (I$>"9" AND I$<"A") OR I$>"F" THEN RETURN
1760 I$=CHR$(CH*16+VAL("&H"+I$)):C=ASC(I$)
1770 GOTO1540'WRITE CHR
1780 '# DISP. CHRARACTER LIST
1790 TP=PT:X=18:Y=1:IF MO=1 THEN 1820
1800 LINE(16*8,9)-(255,6*9-1),0,BF
1810 TP=PT:X=18:Y=1:IF MO=0 THEN A$="DIRECT MODE":GOSUB160:RETURN
1820 LINE(16*8,9)-(255,4*9-1),0,BF
1830 A$="CODE(HEX)MODE":GOSUB160
1840 LINE(16*8,4*9)-(255,5*9-1),4,BF
1850 LINE(16*8,5*9)-(255,6*9-1),5,BF
1860 TP=PT:X=16:Y=3:A$="CHR:"+HEX$(CH)+"0 (-/+)":GOSUB160
1870 TP=PT:X=16:Y=4:A$="0123456789ABCDEF":GOSUB160
1880 POKEWA,16*8:POKEWA+1,5*9:POKEWA+2,PT*64:POKEWA+3,16'16 byte
1890 U=USR(BA+CH*16)
1900 RETURN
1910 '# AUTO READ SECTOR
1920 IF AM=0 OR (PG>=0 AND PG<SH) THEN RETURN
1930 IF SC=0 AND PG<0 THEN PG=0:RETURN
1940 IF SC=(1440-SH) AND PG>=SH THEN PG=SH-1:RETURN
1950 SB=SH:ER=0:GOSUB790:IF ER THEN 2000'WRITE IF MODIFIED
1960 IF PG>=SH THEN SC=SC+SH:IF SC>(1440-SH) THEN PO=SC-1440+SH:SC=1440-SH
1970 IF PG<0 THEN SC=SC-SH:IF SC<0 THEN PO=SC:SC=0
1980 ER=0:GOSUB600:IF ER THEN 2000'READ
1990 PG=(PG+PO+SH)MOD SH:RETURN
2000 'AUTO R/W ERROR
2010 IF PG<0 THEN PG=0 ELSE PG=SH-1
2020 SC=SB:RETURN
2030 '# DIR
2040 LINE(0,10*9)-(255,211),0,BF
2050 LINE(5*8,10*9)-(27*8-1,11*9-1),5,BF
2060 TP=0:Y=10:A$="-DIR-":GOSUB140
2070 IF WT=0 AND SC=0 AND RD=1 THEN 2170
2080 SB=SH:ER=0:GOSUB790:IF ER THEN RETURN'WRITE IF MODIFIED
2090 IF WT=1 THEN RETURN
2100 LINE(8,12*9)-(247,13*9-1),6,BF
2110 X=2:Y=12:A$="READ SECTOR 0-23 OK? (Y/N)":GOSUB160
2120 I$=INPUT$(1)
2130 IF I$="N" OR I$="n" OR I$=CHR$(27) THEN A$="N":BEEP:GOSUB160:RETURN
2140 IF I$="Y" OR I$="y" THEN A$="Y":GOSUB160:GOTO2160
2150 BEEP:GOTO2120
2160 SC=0:ER=0:GOSUB600:IF ER THEN RETURN'READ
2170 'LIST
2180 TC=0:LP=0:LA=DA+512
2190 LINE(0,12*9)-(255,13*9-1),0,BF
2200 LINE(0,13*9)-(255,21*9-1),12,BF:GOSUB2340
2210 TX=-(TC>7)*16:TY=TC AND 7
2220 LINE(TX*8,(TY+13)*9)-(127+TX*8,(TY+14)*9-1),15,BF,XOR
2230 I$=INPUT$(1):C=ASC(I$):IF C=27 THEN BEEP:RETURN
2240 LINE(TX*8,(TY+13)*9)-(127+TX*8,(TY+14)*9-1),15,BF,XOR
2250 IF C=32 OR C=13 THEN GOTO2290
2260 TC=TC-(C=31)+(C=30)+(-(C=28)+(C=29))*8
2270 IF TC<0 OR TC>15 THEN LP=(LP-(TC>15)+(TC<0)) AND 7:TC=TC AND 15:GOTO2190
2280 TC=TC AND 15:GOTO2210
2290 'DECIDE
2300 A1=LA+(LP*16+TC)*16:IF PEEK(A1)>127 THEN BEEP:GOTO2210
2310 FA=PEEK(A1+12)+PEEK(A1+13)*256:FE=PEEK(A1+14)+PEEK(A1+15)*256
2320 SC=FA:ER=0:GOSUB600:IF ER THEN RETURN'READ
2330 CX=0:CY=0:PG=0:GOTO1070
2340 'DISP LIST
2350 TP=1:Y=12:X=8:A$="PAGE"+STR$(LP)+"/8":GOSUB160:LQ=LP*16
2360 Y=13:FOR I=0 TO 15:A1=LA+(I+LQ)*16:IF PEEK(A1)>127 THEN I=16:GOTO2420
2370 X=-(I>7)*16:POKE WA,X*8:POKE WA+1,Y*9:POKE WA+2,TP*64:POKE WA+3,6'6MOJI
2380 U=USR(A1)
2390 A$="&H"+HEX$(PEEK(A1+13))+RIGHT$("0"+HEX$(PEEK(A1+12)),2):FA=VAL(A$)
2400 A$="&H"+HEX$(PEEK(A1+15))+RIGHT$("0"+HEX$(PEEK(A1+14)),2):FE=VAL(A$)
2410 TP=0:X=X+6:A$=STR$(FA)+","+STR$(FE-FA+1):GOSUB160
2420 Y=Y+1:Y=Y+(Y>20)*8:NEXT:RETURN
2500 ' ERROR HOOK
2510 U=0
2520 IF ERR=60 THEN U=128'BAD FAT
2530 IF ERR=63 THEN U=6'SEEK ERR
2540 IF ERR=68 THEN U=1'WRITE PROTECT
2550 IF ERR=69 THENU=12'OTHER ERR
2560 IF ERR=70 THEN U=2'NOT READY
2570 POKE(WA),U:IF U THEN RESUME NEXT
2580 ON ERROR GOTO0:RESUME
