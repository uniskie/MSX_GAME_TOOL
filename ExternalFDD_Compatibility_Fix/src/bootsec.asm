;===========================================================
; External FDD Compatibility Fix
; 内蔵FDD以外から起動できないゲームのためのツールです。
;
; 原因:
;   ブートセクタのCE1EHが呼ばれたときに
;   DEレジスタにはいっている
;   「ページ1がRAMに切り換わるアドレス(F36BH)」について
;   外付けだと機能しないケースがあり、
;   このルーチンを使用してページ1がRAMになることを期待した
;   プログラムがうまく動作しない問題へ力技で対処するものです。
;
; 対処:
;   ページ1をRAMに変更してからキー入力を待ち、
;   何かキーが押されたら
;   ゲームのブートセクタを読み込み
;   ゲーム側の処理へジャンプします。
;
; 動作条件:
;   PHYDIOを呼ぶのでDOS1専用です
;===========================================================

BASE_ADDR EQU 0BD00H ; 処理実行用の転送先アドレス
DMA_ADR: EQU 0BE00H  ; BE00H〜BFFFHに読み込んでから256バイトだけC000Hへ転送する

;-----------------------------------------------------------
H_PHYD:  EQU 0FFA7H  ; WORKAREA: PHYDIOから呼ばれる実体
PHYDIO:  EQU 0144H   ; BIOS: 物理ドライブ直接読み書き (DOS1専用隠しBIOS)
                     ; HL=DMA、DE=セクタ先頭、B=セクタ数
                     ; C=メディアID、Cy=0なら読み込み/1なら書き込み

RAMAD0:  EQU 0F341H  ; WORKAREA: システムRAMのスロットID（ページ0）
RAMAD1:  EQU 0F342H  ; WORKAREA: システムRAMのスロットID（ページ1）
RAMAD2:  EQU 0F343H  ; WORKAREA: システムRAMのスロットID（ページ2）
RAMAD3:  EQU 0F344H  ; WORKAREA: システムRAMのスロットID（ページ3）
EXPTBL:  EQU 0FCC1H  ; WORKAREA: MAIN ROMのスロット番号（ページ0）
ENASLT:  EQU 0024H   ; BIOS: スロットを変更する(A=SLOT, H=ADDRESS HIGH)
CALSLT:  EQU 001CH   ; インタースロットコール(IYH=SLOT, IX=ADDRESS)
CALLF:   EQU 0030H   ; インタースロットコール(RST 30H)(DB スロット、DW アドレス)

CHPUT:   EQU 00A2H   ; BIOS: 1文字表示する
CHGET:   EQU 009FH   ; BIOS: CHGET

PROCNM:  EQU 0F3E9H  ; WORKAREA: 前景色
BAKCLR:  EQU 0F3EAH  ; WORKAREA: 背景色
BDRCLR:  EQU 0F3EBH  ; WORKAREA: 周辺色
LINL32:  EQU 0F3AFH  ; WORKAREA: SCREEN1の幅
CHGMOD:  EQU 005FH   ; BIOS: 画面モード変更
;-----------------------------------------------------------

;-----------------------------------------------------------
BOOT_ADR: EQU 0C000H  ; ブートセクタ読み込み先頭
BOOT_SIZE: EQU 100H   ; ブートセクタ読み込みサイズ
BOOT_INIT: EQU 0C01EH ; ブートセクタプログラムの先頭
;-----------------------------------------------------------

        ORG     BOOT_ADR

;-----------------------------------------------------------
; Boot Header
;-----------------------------------------------------------
    DB   0EBH, 0FEH     ; +00: JMP $
    DB   090H           ; +02: NOP
    DB   "EDSKTOOL"     ; +03: OEM名

    ; メディア情報
    DW  2000H           ; +0B: セクタサイズ (512バイト)
    DB  2               ; +0D: クラスタサイズ (セクタ数)
    DW  01H             ; +0E: 予約セクタ数 (FAT領域の先頭セクタ)
    DB  2               ; +10: FATのコピー数
    DW  70H             ; +11: ディレクトリエントリ数 (112)
    DW  5A0H            ; +13: 総セクタ数 (1440)
    DB  0F9H            ; +15: メディアID (2DD,9セクタ)
    DW  3               ; +16: FATサイズ (3セクタ)
    DW  900H            ; +18: トラックあたりのセクタ数 (720)
    DW  2               ; +1A: ディスクの面数 (2)
    DW  0               ; +1C: 隠されたセクタ数 (0)

    ;|                              | 1DD,9sec | 2DD,9sec | 1DD,8sec | 2DD,8sec |
    ;| ---------------------------- | -------- | -------- | -------- | -------- |
    ;| メディアID                   | 0F8H     | 0F9H     | 0FAH     | 0FBH     |
    ;| 面数                         | 1        | 2        | 1        | 2        |
    ;| 1 面に含まれるトラック数     | 80       | 80       | 80       | 80       |
    ;| 1 トラックに含まれるセクタ数 | 9        | 9        | 8        | 8        |
    ;| 1 セクタに含まれるバイト数   | 512      | 512      | 512      | 512      |
    ;| クラスタのサイズ             | 2 セクタ | 2 セクタ | 2 セクタ | 2セクタ  |
    ;| FAT のサイズ                 | 2 セクタ | 3 セクタ | 1 セクタ | 2セクタ  |
    ;| FAT の個数                   | 2        | 2        | 2        | 2        |
    ;| 作成可能ファイル数           | 112      | 112      | 112      | 112      |

;-----------------------------------------------------------
; 開始1 C01EH
;-----------------------------------------------------------
Start:
    RET  NC             ; 基本的にncでは準備ができてないので戻る
    NOP

;-----------------------------------------------------------
; 開始2 C020H (Cy = 1は各種初期化が終わった状態)
;-----------------------------------------------------------
Start_C:

    ; Aレジスタにはブートドライブ番号
    ; DEレジスタにはコールするとページ1がRAMに切り換わる番地(具体的にはF36BH)
    ;           ※ 外付けだとF36BHをコールしてもページ0はRAMにならない
    ; HLレジスタにはディスクエラー処理ルーチンの番地が書いてある番地(具体的にはF323H)
    ; が入っている

    ;-------------------------------------------------------
    ; ページ1をRAMに変更する
    ;-------------------------------------------------------
    ;EX   DE,HL          ; DEレジスタにはコールするとページ1がRAMに切り換わる番地(具体的にはF36BH)
    ;CALL JPHL           ; ページ1をRAMに切り替える
    ;    外付けだとF36BHをコールしてもページ0はRAMにならないので
    ;     ↓ 普通に切り替える
    LD   A, (RAMAD1)    ; ページ1のRAMスロット
    LD   H, 40H         ; ページ1
    CALL ENASLT         ; スロットを変更する

    ;-------------------------------------------------------
    ; ドライブ番号保存
    ;-------------------------------------------------------
    LD   (BootDrive - MainCode + BASE_ADDR), A

    ;-------------------------------------------------------
    ; メッセージ表示
    ;-------------------------------------------------------
    LD   HL, ScreenColor
    LD   DE, PROCNM     ; 前景色
    LDI                 ; PROCNM: 前景色
    LDI                 ; BAKCLR: 背景色
    LDI                 ; BDRCLR: 周辺色
    LD   A,32
    LD   (LINL32),A     ; SCREEN1のWIDTH
    LD   A,1            ; SCREEN1
    LD   IY,(EXPTBL-1)  ; IYH=ROM SLOT
    LD   IX,CHGMOD      ; 画面モード変更
    CALL CALSLT         ; インタースロットコール

    LD   HL, Message
PrintLoop:
    LD   A, (HL)
    OR   A
    JR   Z, WaitKey
    LD   IY,(EXPTBL-1)  ; IYH=ROM SLOT
    LD   IX,CHPUT       ; BIOS: 1文字表示する
    CALL CALSLT         ; インタースロットコール
    INC  HL
    JR   PrintLoop

    ;-------------------------------------------------------
    ; 何かキーが押されるまで待機
    ;-------------------------------------------------------
WaitKey:
    EI
    LD   IY,(EXPTBL-1)  ; IYH=ROM SLOT
    LD   IX,CHGET       ; BIOS: 1文字入力を受け付ける
    CALL CALSLT         ; インタースロットコール

    ;-------------------------------------------------------
    ; 自分の処理(MainCode以降)をワークエリア(BASE_ADDR)へコピー
    ;-------------------------------------------------------
    LD   HL, MainCode
    LD   DE, BASE_ADDR
    LD   BC, CodeEnd - MainCode
    LDIR

    ; ワークエリア(BASE_ADDR)へジャンプ
    ;    (Aレジスタの値は維持)
    JP   BASE_ADDR

;===========================================================
; Main Code
; これ以降は BASE_ADDR に転送されて実行される
;===========================================================
MainCode:
    ;-------------------------------------------------------
    ; ブートセクタ読み込み (C000Hへ)
    ; BIOSのPHYDIOを呼ぶのでDOS1専用
    ;-------------------------------------------------------
    ; スタックポインタがC100H〜C1FFHにあるので
    ; BE00H〜BFFFHに読み込んでから256バイトだけC000Hへ転送する

    EI                  ; 割り込みを許可しないとディスク読み込みで止まる
    LD   A, (BootDrive - MainCode + BASE_ADDR) ; Drive
    LD   B, 1           ; 1 Sector
    LD   C, 0F9H        ; Media ID (2DD/9Sector)
    LD   DE, 0          ; Sector 0
    LD   HL, DMA_ADR    ; DMA Address
    OR   A              ; Cy = 0 (Read Mode)
    CALL H_PHYD         ; PHYDIOの代わりにフックを直接呼ぶ

    ; C000Hへコピー
    LD   HL,DMA_ADR
    LD   DE,BOOT_ADR
    LD   BC,BOOT_SIZE
    LDIR

    ;-------------------------------------------------------
    ; 読み込んだ新しいブートセクタの0C01EHへジャンプ
    ;-------------------------------------------------------
    SCF                 ; キャリーフラグはリセット状態で呼ぶ
    JP   BOOT_INIT

;JPHL:
;    JP (HL)

;===========================================================
; Data Area
;===========================================================
ScreenColor:
    DB   15             ; PROCNM: 前景色
    DB   0              ; BAKCLR: 背景色
    DB   0              ; BDRCLR: 周辺色

Message:
    DB   "INSERT DISK AND HIT ANY KEY", 0

BootDrive:
    DB   0

CodeEnd:
    ; サイズチェック: (CodeEnd - MainCode) は 256バイト以内でなければならない

Dummy:
    DS  BOOT_ADR + BOOT_SIZE - CodeEnd-1, AAH
    DB  AAH

    END
