; for tniASM
;*********************************

;;*** BASIC ERROR HOOK
;H_ERRO EQU 0FFB1H	; IN: E=ERROR CODE
;;
;;** JUMP TO BASIC ERROR
;;
;ERROR:	LD SP,(STACK)
;	JP H_ERRO
;;
;;** WORK AREA
;STACK:	DW	0
;
;*********************************
;	FILE I/O
;*********************************
;
;*** FILE I/O
SYSCAL:	EQU	0F37DH	; FUNCTION CALL / IN: C=CMD / etc.
FCBADR:	EQU	0F353H	; (FCBBASE) FCB ADDRESS
DMAADR:	EQU	0F34FH	; (BUFFER) DOS SECTOR BUFFER
DIRADR:	EQU	0F351H	; (DIRBUF) DOS DIRECTRY BUFFER
SECBUF:	EQU	0F34DH	; (DECBUF) DRIVER SECTOR BUFFER
;
;*** SYSTEM CALL FUNCTION COMMAND
C_OPEN:	EQU	0FH	; FILE OPEN
C_CLOSE:EQU	10H	; FILE CLOSE
C_DMA:	EQU	1AH	; SET DMA ADDRESS
C_READ:	EQU	27H	; FILE RANDOM READ 2
C_WRIT:	EQU	26H	; FILE RANDOM WRITE 2
C_SRCH:	EQU	11H	; SEARCH FILE
C_SRH2:	EQU	12H	; SEARCH FILE 2
;
;*** FCB AREA
;F_RECSZ:	EQU	FCB+14
;F_RDREC:	EQU	FCB+33
;F_FLSIZ:	EQU	FCB+16
A_SIZ:	EQU	+14
A_REC:	EQU	+33
;
;** FILE OPEN
;IN:	HL=FILENAME ADDRESS
;OUT:	ERROR:Z=0 / OK:Z=1
;
F_OPEND:
	LD	DE,(DMAADR)
;
;** FILE OPEND
;IN:	HL=FILENAME ADDRESS
;	DE=DMA ADDRESS
;OUT:	ERROR:Z=0 / OK:Z=1
;
F_OPEN:
	PUSH	HL		;FNAME
	LD	C,C_DMA
	CALL	SYSCAL
	POP	HL		;FNAME
	CALL	SETFNAME
	LD	DE,(FCBADR)
	LD	C,C_OPEN
	CALL	SYSCAL
;
	LD	IX,(FCBADR)
	LD	HL,1
	LD	(IX+14),L
	LD	(IX+15),H	;RECORD SIZE=1byte
;
	LD	(IX+33),H
	LD	(IX+34),H
	LD	(IX+35),H
	LD	(IX+36),H	;RANDOM RECORD No.=0
;
	OR	A
	RET
;
;------------------------------
;** FILE CLOSE
;OUT:	ERROR:Z=0 / OK:Z=1
;
F_CLOSE:
	LD	DE,(FCBADR)
	LD	C,C_CLOSE
	CALL	SYSCAL
	OR	A
	RET
;
;------------------------------
;** FILE READ
;IN:	HL=REQUEST RECORD SIZE
;OUT:	HL=READ RECORD SIZE
;	ERROR:Z=0 / OK:Z=1
;
F_READ:
	LD	DE,(FCBADR)
	LD	C,C_READ
	CALL	SYSCAL
	OR	A
	RET
;
;------------------------------
;** FILE WRITE
;IN:	HL=REQUEST RECORD SIZE
;OUT:	ERROR:Z=0 / OK:Z=1
;
F_WRITE:
	LD	DE,(FCBADR)
	LD	C,C_WRIT
	CALL	SYSCAL
	OR	A
	RET
;
;------------------------------
;
;** SET FILE NAME
;IN:	HL=FILE NAME ADDRESS
;
SETFNAME:
	LD	DE,(FCBADR)
	XOR	A		;DEFAULT DRIVE
	LD	(DE),A		;FCB+0:DRIVE NO
	INC	DE		;FCB+1:FILENAME
	LD	BC,11
	LDIR
	RET
;
;** DMA SET
;IN:	DE=DMA ADDRESS
;
SETDMA:
	LD	C,C_DMA
	CALL	SYSCAL
	RET
;
